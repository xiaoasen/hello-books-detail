---
comments: true
---

# 2.1.4  Extended Exercise: Interval Arithmetic(扩展练习：区间算术
)
Alyssa P. Hacker is designing a system to help people solve engineering problems. One feature she wants to provide in her system is the ability to manipulate inexact quantities (such as measured parameters of physical devices) with known precision, so that when computations are done with such approximate quantities the results will be numbers of known precision.<br>
Alyssa P.Hacker正在设计一个帮助人们求解工程问题的系统。她希望这个系统提供的一个特征是能够去操作不准确的量(例如物理设备的测量参数),这种量具有已知的精度，所以，在对这种近似量进行计算时，得到的结果也应该是已知精度的数值。

Electrical engineers will be using Alyssa's system to compute electrical quantities. It is sometimes necessary for them to compute the value of a parallel equivalent resistance ${R_p}$ of two resistors ${R_1}$ and ${R_2}$ using the formula<br>
电子工程师将会用Alyssa的系统去计算一些电子量。有时他们必须使用下面公式，从两个电阻 ${R_1}$ 和 ${R_2}$ 计算出并联等价电阻 ${R_p}$ 的值：

$$ R_p = {1 \over 1 \big/ R_1 + 1 \big/ R_2}\,.  $$

Resistance values are usually known only up to some tolerance guaranteed by the manufacturer of the resistor. For example, if you buy a resistor labeled ''6.8 ohms with 10% tolerance'' you can only be sure that the resistor has a resistance between 6.8 - 0.68 = 6.12 and 6.8 + 0.68 = 7.48 ohms. Thus, if you have a 6.8-ohm 10% resistor in parallel with a 4.7-ohm 5% resistor, the resistance of the combination can range from about 2.58 ohms (if the two resistors are at the lower bounds) to about 2.97 ohms (if the two resistors are at the upper bounds).<br>
此时所知的电阻值通常是由电阻生产厂商给出的带误差保证的值，例如你可能买到一支标明
“6.8欧姆误差10%”的电阻，这时我们就只能确定，这支电阻的阻值在6.8-0.68=6.12和6.8
+0.68=7.48欧姆之间。这样，如果将一支6.8欧姆误差10%的电阻与另一支4.7欧姆误差5%的电阻并联，这一组合的电阻值可以在大约2.58欧姆(如果两支电阻都有最小值)和2.97欧姆(如果两支电阻都是最大值)之间。

Alyssa's idea is to implement ''interval arithmetic'' as a set of arithmetic operations for combining ''intervals'' (objects that represent the range of possible values of an inexact quantity). The result of adding, subtracting, multiplying, or dividing two intervals is itself an interval, representing the range of the result.<br>
Alyssa的想法是实现一套“区间算术”,即作为可以用于组合“区间”(表示某种不准确量的可能值的对象)的一组算术运算。两个区间的加、减、乘、除的结果仍是一个区间，表示的是计算结果的范围。

Alyssa postulates the existence of an abstract object called an ''interval'' that has two endpoints: a lower bound and an upper bound. She also presumes that, given the endpoints of an interval, she can construct the interval using the data constructor `make-interval`. Alyssa first writes a procedure for adding two intervals. She reasons that the minimum value the sum could be is the sum of the two lower bounds and the maximum value it could be is the sum of the two upper bounds:<br>
Alyssa假设有一种称为“区间”的抽象对象，这种对象有两个端点，一个下界和一个上界。她还假定，给了一个区间的两个端点，就可以用数据构造函数`make-interval`构造出相应的区间来。Alyssa首先写出了一个做区间加法的过程，她推理说，和的最小值应该是两个区间的下界之和，其最大值应该是两个区间的上界之和：

```
(define (add-interval x y)
  (make-interval (+ (lower-bound x) (lower-bound y))
                 (+ (upper-bound x) (upper-bound y))))
```

Alyssa also works out the product of two intervals by finding the minimum and the maximum of the products of the bounds and using them as the bounds of the resulting interval. (`Min` and `max` are primitives that find the minimum or maximum of any number of arguments.)<br>
Alyssa还找出了这种界的乘积的最小和最大值，用它们做出了两个区间的乘积(`min`和`max`是求出任意多个参数中的最小值和最大值的基本过程)。

```
(define (mul-interval x y)
  (let ((p1 (* (lower-bound x) (lower-bound y)))
        (p2 (* (lower-bound x) (upper-bound y)))
        (p3 (* (upper-bound x) (lower-bound y)))
        (p4 (* (upper-bound x) (upper-bound y))))
    (make-interval (min p1 p2 p3 p4)
                   (max p1 p2 p3 p4))))
```

To divide two intervals, Alyssa multiplies the first by the reciprocal of the second. Note that the bounds of the reciprocal interval are the reciprocal of the upper bound and the reciprocal of the lower bound, in that order.<br>
为了做出两个区间的除法，Alyssa用第一个区间乘上第二个区间的倒数。请注意，倒数的两个限界分别是原来区间的上界的倒数和下界的倒数：

```
(define (div-interval x y)
  (mul-interval x 
                (make-interval (/ 1.0 (upper-bound y))
                               (/ 1.0 (lower-bound y)))))
```

<div id="Exercise2.7" markdown>

Exercise 2.7.  Alyssa's program is incomplete because she has not specified the implementation of the interval abstraction. Here is a definition of the interval constructor:<br>
练习2.7 Alyssa的程序是不完整的，因为她还没有确定区间抽象的实现。这里是区间构造符的定义：

```
(define (make-interval a b) (cons a b))
```

Define selectors `upper-bound` and `lower-bound` to complete the implementation.<br>
请定义选择符`upper-bound`和`lower-bound`,完成这一实现。
</div>

<div id="Exercise2.8" markdown>

Exercise 2.8.  Using reasoning analogous to Alyssa's, describe how the difference of two intervals may be computed. Define a corresponding subtraction procedure, called `sub-interval`.<br>
练习2.8  通过类似于Alyssa的推理，说明两个区间的差应该怎样计算。请定义出相应的减法过程`sub-interval`。
</div>

<div id="Exercise2.9" markdown>

Exercise 2.9.  The width of an interval is half of the difference between its upper and lower bounds. The width is a measure of the uncertainty of the number specified by the interval. For some arithmetic operations the width of the result of combining two intervals is a function only of the widths of the argument intervals, whereas for others the width of the combination is not a function of the widths of the argument intervals. Show that the width of the sum (or difference) of two intervals is a function only of the widths of the intervals being added (or subtracted). Give examples to show that this is not true for multiplication or division.<br>
练习2.9  区间的宽度就是其上界和下界之差的一半。区间宽度是有关区间所描述的相应数值的非确定性的一种度量。对于某些算术运算，两个区间的组合结果的宽度就是参数区间的宽度的函数，而对其他运算，组合区间的宽度则不是参数区间宽度的函数。证明两个区间的和(与差)的宽度就是被加(或减)的区间的宽度的函数。举例说明，对于乘和除而言，情况并非如此。
</div>

<div id="Exercise2.10" markdown>

Exercise 2.10.  Ben Bitdiddle, an expert systems programmer, looks over Alyssa's shoulder and comments that it is not clear what it means to divide by an interval that spans zero. Modify Alyssa's code to check for this condition and to signal an error if it occurs.<br>
练习2.10 Ben Bitdiddle是个专业程序员，他看了Alyssa工作后评论说，除以一个跨过横跨0的区间的意义不清楚。请修改Alyssa的代码，检查这种情况并在出现这一情况时报错。
</div>

<div id="Exercise2.11" markdown>

Exercise 2.11.  In passing, Ben also cryptically comments: ''By testing the signs of the endpoints of the intervals, it is possible to break `mul-interval` into nine cases, only one of which requires more than two multiplications.'' Rewrite this procedure using Ben's suggestion.<br>

练习2.11  在看了这些东西之后，Ben又说出了下面这段有些神秘的话：“通过监测区间的端点，有可能将`mul-interval`分解为9种情况，每种情况中所需的乘法都不超过两次"。请根据Ben的建议重写这个过程。

After debugging her program, Alyssa shows it to a potential user, who complains that her program solves the wrong problem. He wants a program that can deal with numbers represented as a center value and an additive tolerance; for example, he wants to work with intervals such as 3.5± 0.15 rather than [3.35, 3.65]. Alyssa returns to her desk and fixes this problem by supplying an alternate constructor and alternate selectors:<br>
在排除了自己程序里的错误之后，Alyssa给一个可能用户演示自己的程序。那个用户却说她的程序解决的问题根本不对。他希望能够有一个程序，可以用于处理那种用一个中间值和一个附加误差的形式表示的数，也就是说，希望程序能处理3.5±0.15而不是 [3.35, 3.65]。Alyssa回到自己的办公桌来纠正这一问题，另外提供了一个构造符和一个选择符：

```
(define (make-center-width c w)
  (make-interval (- c w) (+ c w)))
(define (center i)
  (/ (+ (lower-bound i) (upper-bound i)) 2))
(define (width i)
  (/ (- (upper-bound i) (lower-bound i)) 2))
```

Unfortunately, most of Alyssa's users are engineers. Real engineering situations usually involve measurements with only a small uncertainty, measured as the ratio of the width of the interval to the midpoint of the interval. Engineers usually specify percentage tolerances on the parameters of devices, as in the resistor specifications given earlier.<br>
不幸的是，Alyssa的大部分用户是工程师，现实中的工程师经常遇到只有很小非准确性的测量值，而且常常是以区间宽度对区间中点的比值作为度量值。他们通常用的是基于有关部件的参数的百分数描述的误差，就像前面描述电阻值的那种方式一样。
</div>

<div id="Exercise2.12" markdown>

Exercise 2.12.  Define a constructor `make-center-percent` that takes a center and a percentage tolerance and produces the desired interval. You must also define a selector `percent` that produces the percentage tolerance for a given interval. The `center` selector is the same as the one shown above.<br>
练习2.12请定义一个构造函数`make-center-percent`,它以一个中心点和一个百分比为参数，产生出所需要的区间。你还需要定义选择函数`percent`,通过它可以得到给定区间的百分数误差，选择函数`center`与前面定义的一样。
</div>

<div id="Exercise2.13" markdown>

Exercise 2.13.  Show that under the assumption of small percentage tolerances there is a simple formula for the approximate percentage tolerance of the product of two intervals in terms of the tolerances of the factors. You may simplify the problem by assuming that all numbers are positive.<br>
练习2.13  请证明，在误差为很小的百分数的条件下，存在着一个简单公式，利用它可以从两个被乘区间的误差算出乘积的百分数误差值。你可以假定所有的数为正，以简化这一问题。

After considerable work, Alyssa P. Hacker delivers her finished system. Several years later, after she has forgotten all about it, she gets a frenzied call from an irate user, Lem E. Tweakit. It seems that Lem has noticed that the formula for parallel resistors can be written in two algebraically equivalent ways:<br>
经过相当多的工作之后，Alyssa P.Hacker发布了她的最后系统。几年之后，在她已经忘记了这个系统之后，接到了一个愤怒的用户Lem E.Tweakit的发疯式的电话。看起来Lem注意到并联电阻的公式可以写成两个代数上等价的公式：

$$ R_1 R_2 \over R_1 + R_2 $$

and<br>
和

$$ {1 \over 1 \big/ R_1 + 1 \big/ R_2}\,. $$

He has written the following two programs, each of which computes the parallel-resistors formula differently:<br>
这样他就写了两个程序，它们以不同的方式计算并联电阻值：

```
(define (par1 r1 r2)
  (div-interval (mul-interval r1 r2)
                (add-interval r1 r2)))
(define (par2 r1 r2)
  (let ((one (make-interval 1 1))) 
    (div-interval one
                  (add-interval (div-interval one r1)
                                (div-interval one r2)))))
```

Lem complains that Alyssa's program gives different answers for the two ways of computing. This is a serious complaint.<br>
Lem抱怨说，Alyssa程序对两种不同计算方法给出不同的值。这确实是很严重的抱怨。

</div>

<div id="Exercise2.14" markdown>

Exercise 2.14.  Demonstrate that Lem is right. Investigate the behavior of the system on a variety of arithmetic expressions. Make some intervals ${A}$ and ${B}$, and use them in computing the expressions ${A \big/\! A}$ and ${A \big/\! B}$. You will get the most insight by using intervals whose width is a small percentage of the center value. Examine the results of the computation in center-percent form (see exercise [2.12](#Exercise2.12)).<br>
练习2.14请确认Lem是对的。请你用各种不同的算术表达式来检查这一系统的行为。请做出两个区间 ${A}$ 和 ${B}$,并用它们计算表达式 ${A \big/\! A}$ 和 ${A \big/\! B}$。如果所用区间的宽度相对于中心值取很小百分数，你将会得到更多的认识。请检查对于中心-百分比形式(见练习[2.12](#Exercise2.12))进行计算
</div>

<div id="Exercise2.15" markdown>

Exercise 2.15.  Eva Lu Ator, another user, has also noticed the different intervals computed by different but algebraically equivalent expressions. She says that a formula to compute with intervals using Alyssa's system will produce tighter error bounds if it can be written in such a form that no variable that represents an uncertain number is repeated. Thus, she says, `par2` is a ''better'' program for parallel resistances than `par1`. Is she right? Why?<br>
练习2.15  另一用户Eva Lu Ator也注意到了由不同的等价代数表达式计算出的区间的差异。她说，如果一个公式可以写成一种形式，其中具有非准确性的变量不重复出现，那么Alyssa的系统产生出的区间的限界更紧一些。她说，因此，在计算并联电阻时，`par2`是比`par1`“更好的”程序。她说得对吗?
</div>

<div id="Exercise2.16" markdown>

Exercise 2.16.  Explain, in general, why equivalent algebraic expressions may lead to different answers. Can you devise an interval-arithmetic package that does not have this shortcoming, or is this task impossible? (Warning: This problem is very difficult.)<br>
练习2.16  请给出一个一般性的解释：为什么等价的代数表达式可能导致不同计算结果?你能设计出一个区间算术包，使之没有这种缺陷吗?或者这件事情根本不可能做到?(警告：这个问题非常难。)
</div>