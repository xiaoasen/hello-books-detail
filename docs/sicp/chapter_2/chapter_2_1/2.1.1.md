---
comments: true
---

# 2.1.1  Example: Arithmetic Operations for Rational Numbers（实例：有理数的算术运算）
Suppose we want to do arithmetic with rational numbers. We want to be able to add, subtract, multiply, and divide them and to test whether two rational numbers are equal.<br>
假定我们希望做有理数上的算术，希望能做有理数的加减乘除运算，比较两个有理数是否相等，等等。

Let us begin by assuming that we already have a way of constructing a rational number from a numerator and a denominator. We also assume that, given a rational number, we have a way of extracting (or selecting) its numerator and its denominator. Let us further assume that the constructor and selectors are available as procedures:<br>
作为开始，我们假定已经有了一种从分子和分母构造有理数的方法。并进一步假定，如果有了一个有理数，我们有一种方法取得(选出)它的分子和分母。现在再假定有关的构造函数和选择函数都可以作为过程使用：

- (make-rat <n> <d>) returns the rational number whose numerator is the integer <n> and whose denominator is the integer <d>.<br>
(make-rat <n> <d>)返回一个有理数，其分子是整数<n>，分母是整数<d>。

- (numer <x>) returns the numerator of the rational number <x>.<br>
(numer <x>)返回有理数<x>的分子。

- (denom <x>) returns the denominator of the rational number <x>.<br>
(denom <x>)返回有理数<x>的分母。

We are using here a powerful strategy of synthesis: **wishful thinking**. We haven't yet said how a rational number is represented, or how the procedures `numer`, `denom`, and `make-rat`s should be implemented. Even so, if we did have these three procedures, we could then add, subtract, multiply, divide, and test equality by using the following relations:<br>
我们要在这里使用一种称为 **按愿望思维** 的强有力的综合策略。现在我们还没有说有理数将如何表示，也没有说过程`numer`、`denom`和`make-rat`应如何实现。然而，如果我们真的有了这三个过程，那么就可以根据下面关系去做有理数的加减乘除和相等判断了：

$$
\eqalign{
{n_1 \over d_1} + {n_2 \over d_2} 	&= {n_1 d_2 + n_2 d_1 \over d_1 d_2}, \cr
{n_1 \over d_1} - {n_2 \over d_2} 	&= {n_1 d_2 - n_2 d_1 \over d_1 d_2}, \cr
{n_1 \over d_1} \times {n_2 \over d_2} 	&= {n_1 n_2 \over d_1 d_2}, \cr
{n_1 \,\big/ d_1} \over {n_2 \,\big/ d_2} 	&= {n_1 d_2 \over d_1 n_2}, \cr
} $$

We can express these rules as procedures:<br>
我们可以将这些规则表述为如下几个过程：

```
(define (add-rat x y)
  (make-rat (+ (* (numer x) (denom y))
               (* (numer y) (denom x)))
            (* (denom x) (denom y))))
(define (sub-rat x y)
  (make-rat (- (* (numer x) (denom y))
               (* (numer y) (denom x)))
            (* (denom x) (denom y))))
(define (mul-rat x y)
  (make-rat (* (numer x) (numer y))
            (* (denom x) (denom y))))
(define (div-rat x y)
  (make-rat (* (numer x) (denom y))
            (* (denom x) (numer y))))
(define (equal-rat? x y)
  (= (* (numer x) (denom y))
     (* (numer y) (denom x))))
```

Now we have the operations on rational numbers defined in terms of the selector and constructor procedures `numer`, `denom`, and `make-rat`. But we haven't yet defined these. What we need is some way to glue together a numerator and a denominator to form a rational number.<br>
这样，我们已经有了定义在选择和构造过程`numer`、`denom`和`make-rat`基础之上的各种有理数运算，而这些基础还没有定义。现在需要有某种方式，将一个分子和一个分母粘接起来，构成一个有理数。

## Pairs(序对)
To enable us to implement the concrete level of our data abstraction, our language provides a compound structure called a **pair**, which can be constructed with the primitive procedure `cons`. This procedure takes two arguments and returns a compound data object that contains the two arguments as parts. Given a pair, we can extract the parts using the primitive procedures `car` and `cdr`.[^1] Thus, we can use `cons`, `car`, and `cdr` as follows:<br>
为了在具体的层面上实现这一数据抽象，我们所用的语言提供了一种称为 **序对** 的复合结构，这种结构可以通过基本过程`cons`构造出来。过程`cons`取两个参数，返回一个包含这两个参数作为其成分的复合数据对象。如果给了一个序对，我们可以用基本过程`car`和`cdr`[^1]按如下方式提取出其中各个部分：

```
(define x (cons 1 2))

(car x)
1

(cdr x)
2
```

Notice that a pair is a data object that can be given a name and manipulated, just like a primitive data object. Moreover, `cons` can be used to form pairs whose elements are pairs, and so on:<br>
请注意，一个序对也是一个数据对象，可以像基本数据对象一样给它一个名字且操作它。进一步说，还可以用`cons`去构造那种其元素本身就是序对的序对，并继续这样做下去。

```
(define x (cons 1 2))

(define y (cons 3 4))

(define z (cons x y))

(car (car z))
1

(car (cdr z))
3
```

In section [2.2] we will see how this ability to combine pairs means that pairs can be used as general-purpose building blocks to create all sorts of complex data structures. The single compound-data primitive pair, implemented by the procedures `cons`, `car`, and `cdr`, is the only glue we need. Data objects constructed from pairs are called **list-structured** data.<br>
在[2.2]节里我们将看到，这种组合起序对的能力表明，序对可以用作构造任意种类的复杂数据结构的通用的基本构件。通过过程`cons`、`car`和`cdr`实现的这样一种最基本的复合数据，序对，也就是我们需要的所有东西。从序对构造起来的数据对象称为 **表结构** 数据。

## Representing rational numbers(有理数的表示)
Pairs offer a natural way to complete the rational-number system. Simply represent a rational number as a pair of two integers: a numerator and a denominator. Then `make-rat`, `numer`, and `denom` are readily implemented as follows:[^2]<br>
序对为完成这里的有理数系统提供了一种自然方式，我们可以将有理数简单表示为两个整数(分子和分母)的序对。这样就很容易做出下面`make-rat`、`numer`和`denom`的实现[^2]：

```
(define (make-rat n d) (cons n d))

(define (numer x) (car x))

(define (denom x) (cdr x))
```

Also, in order to display the results of our computations, we can print rational numbers by printing the numerator, a slash, and the denominator:[^3]<br>
还有，为了显示这里的计算结果，我们可以将有理数打印为一个分子，在斜线符之后打印相应的分母[^3]:

```
(define (print-rat x)
  (newline)
  (display (numer x))
  (display "/")
  (display (denom x)))
```

Now we can try our rational-number procedures:<br>
现在就可以试验我们的有理数过程了：

```
(define one-half (make-rat 1 2))

(print-rat one-half)
1/2

(define one-third (make-rat 1 3))
(print-rat (add-rat one-half one-third))
5/6

(print-rat (mul-rat one-half one-third))
1/6

(print-rat (add-rat one-third one-third))
6/9
```

As the final example shows, our rational-number implementation does not reduce rational numbers to lowest terms. We can remedy this by changing `make-rat`. If we have a `gcd` procedure like the one in section [1.2.5] that produces the greatest common divisor of two integers, we can use gcd to reduce the numerator and the denominator to lowest terms before constructing the pair:<br>
正如上面最后一个例子所显示的，我们的有理数实现并没有将有理数约化到最简形式。通过修改`make-rat`很容易做到这件事。如果我们有了一个如[1.2.5]节中那样的gcd过程，用它可以求出两个整数的最大公约数，那么现在就可以利用它，在构造序对之前将分子和分母约化为最简单的项：

```
(define (make-rat n d)
  (let ((g (gcd n d)))
    (cons (/ n g) (/ d g))))
```

Now we have<br>
现在我们就有：

```
(print-rat (add-rat one-third one-third))
2/3
```

as desired. This modification was accomplished by changing the constructor `make-rat` without changing any of the procedures (such as `add-rat` and `mul-rat`) that implement the actual operations.<br>
正如所期望的。为了完成这一改动，我们只需修改构造符`make-rat`,完全不必修改任何实
现实际运算的过程(例如`add-rat`和`mul-rat`)。

<div id="Exercise2.1" markdown>

Exercise 2.1.  Define a better version of `make-rat` that handles both positive and negative arguments. `Make-rat` should normalize the sign so that if the rational number is positive, both the numerator and denominator are positive, and if the rational number is negative, only the numerator is negative.<br>
练习2.1  请定义出`make-rat`的一个更好的版本，使之可以正确处理正数和负数。当有理数为正时，`make-rat`应当将其规范化，使它的分子和分母都是正的。如果有理数为负，那么就应只让分子为负。
</div>

[^1]: 
    The name cons stands for ''construct.'' The names car and cdr derive from the original implementation of Lisp on the IBM 704. That machine had an addressing scheme that allowed one to reference the ''address'' and ''decrement'' parts of a memory location. Car stands for ''Contents of Address part of Register'' and cdr (pronounced ''could-er'') stands for ''Contents of Decrement part of Register.''<br>
    名字cons表示“构造”(construct)。名字car和cdr则来自Lisp最初在IBM 704机器上的实现。在这种机器有一种取址模式，使人可以访间一个存储地址中的“地址”(address)部分和“减量”(decrement)部分。car表示“Contents of Address part of Register”(寄存器的地址部分的内容),cdr(读作"could-er")表示“Contents of Decrement part of Register”(寄存器的减量部分的内容)。

[^2]:
    Another way to define the selectors and constructor is<br>
    定义选择符和构造符的另一种方式是：
    ```
    (define make-rat cons)
    (define numer car)
    (define denom cdr)
    ```
    The first definition associates the name `make-rat` with the value of the expression `cons`, which is the primitive procedure that constructs pairs. Thus `make-rat` and `cons` are names for the same primitive constructor.<br>
    这里的第一个定义将名字`make-rat`关联于表达式`cons`的值，也就是那个构造序对的过程。这样就使`make-rat`和`cons`成了同一个基本过程的名字。

    Defining selectors and constructors in this way is efficient: Instead of `make-rat` calling `cons`, `make-rat` is `cons`, so there is only one procedure called, not two, when `make-rat` is called. On the other hand, doing this defeats debugging aids that trace procedure calls or put breakpoints on procedure calls: You may want to watch `make-rat` being called, but you certainly don't want to watch every call to `cons`.<br>
    按照这种方式定义出选择函数和构造函数的效率更高，因为它不是让`make-rat`去调用`cons`,而是使`make-rat`本身就是`cons`,因此，如果调用`make-rat`,在这里就只有一次过程调用而不是两次调用。而在另一方面，这种做法也会击溃系统的排错辅助功能，那种功能可以追踪过程的调用或者在过程调用处放入断点。你有可能希望监视对`make-rat`的调用，而决不会希望去监视程序里的每个`cons`调用。

    We have chosen not to use this style of definition in this book. <br>
    我们的选择是，不在本书中采用这里所说的定义风格。

[^3]:
    `Display` is the Scheme primitive for printing data. The Scheme primitive `newline` starts a new line for printing. Neither of these procedures returns a useful value, so in the uses of `print-rat` below, we show only what `print-rat` prints, not what the interpreter prints as the value returned by `print-rat`.<br>
    `display`是Scheme系统里打印数据的基本过程，基本过程`newline`为随后的打印开始一个新行。这两个过程都不返回有用的值，所以，在下面使用`print-rat`时，我们只显示了`print-rat`打印的是什么，而没有显式解释器对`print-rat`的返回值打印了什么。

[2.2]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_2/

[1.2.5]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_2/1.2.5