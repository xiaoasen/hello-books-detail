---
comments: true
---

# 2.1.3  What Is Meant by Data? (数据意味着什么)
We began the rational-number implementation in section [2.1.1] by implementing the rational-number operations `add-rat`, `sub-rat`, and so on in terms of three unspecified procedures: `make-rat`, `numer`, and `denom`. At that point, we could think of the operations as being defined in terms of data objects -- numerators, denominators, and rational numbers -- whose behavior was specified by the latter three procedures.<br>
在[2.1.1]节里实现有理数时，我们基于三个尚未定义的过程`make-rat`、`numer`和`denom`,由这些出发去做有理数操作`add-rat`、`sub-rat`等等的实现。按照那时的想法，这些操作是基于数据对象(分子、分母、有理数)定义的，这些对象的行为完全由前面三个过程刻画。

But exactly what is meant by data? It is not enough to say ''whatever is implemented by the given selectors and constructors.'' Clearly, not every arbitrary set of three procedures can serve as an appropriate basis for the rational-number implementation. We need to guarantee that, if we construct a rational number `x` from a pair of integers `n` and `d`, then extracting the `numer` and the `denom` of x and dividing them should yield the same result as dividing `n` by `d`. In other words, `make-rat`, `numer`, and `denom` must satisfy the condition that, for any integer `n` and any non-zero integer `d`, if `x` is `(make-rat n d)`, then<br>
那么，数据究竟意味着什么呢?说它就是“由给定的构造函数和选择函数所实现的东西”还是不够的。显然，并不是任意的三个过程都适合作为有理数实现的基础。在这里，我们需要保证，如果从一对整数`n`和`d`构造出一个有理数`x`,那么，抽取出`x`的`numer`和`denom`并将它们相除，得到的结果应该与`n`除以`d`相同。换句话说，`make-rat`、`numer`和`denom`必须满足下面条件，对任意整数`n`和任意非零整数`d`,如果`x`是`(make-rat n d)`,那么：

$$ \frac{(\text{numer x})}{(\text{denom x})}=\frac{n}{\text{d}}.  $$

In fact, this is the only condition `make-rat`, `numer`, and `denom` must fulfill in order to form a suitable basis for a rational-number representation. In general, we can think of data as defined by some collection of selectors and constructors, together with specified conditions that these procedures must fulfill in order to be a valid representation.[^1]<br>
事实上，这就是为了能成为适宜表示有理数的基础，`make-rat`、`numer`和`denom`必须满足的全部条件。一般而言，我们总可以将数据定义为一组适当的选择函数和构造函数，以及为使这些过程成为一套合法表示，它们就必须满足的一组特定条件.[^1]

This point of view can serve to define not only ''high-level'' data objects, such as rational numbers, but lower-level objects as well. Consider the notion of a pair, which we used in order to define our rational numbers. We never actually said what a pair was, only that the language supplied procedures `cons`, `car`, and `cdr` for operating on pairs. But the only thing we need to know about these three operations is that if we glue two objects together using `cons` we can retrieve the objects using `car` and `cdr`. That is, the operations satisfy the condition that, for any objects `x` and `y`, if `z` is `(cons x y)` then `(car z)` is `x` and `(cdr z)` is `y`. Indeed, we mentioned that these three procedures are included as primitives in our language. However, any triple of procedures that satisfies the above condition can be used as the basis for implementing pairs. This point is illustrated strikingly by the fact that we could implement `cons`, `car`, and `cdr` without using any data structures at all but only using procedures. Here are the definitions:<br>
这一观点不仅可以服务于“高层”数据对象的定义，例如有理数，同样也可用于低层的对象。请考虑序对的概念，我们在前面用它定义有理数。我们从来都没有说过序对究竟是什么，只说所用的语言为序对的操作提供了三个过程`cons`、`car`和`cdr`。有关这三个操作，我们需要知道的全部东西就是，如果用cons将两个对象粘接到一起，那么就可以借助于`car`和`cdr`提取出这两个对象。也就是说，这些操作满足的条件是：对任何对象`x`和`y`,如果`z`是`(cons x y)`,那么`(car z)`就是`x`,而`(cdr z)`就是`Y`。我们确实说过这三个过程是所用的语言里的基本过程。然而，任何能满足上述条件的三个过程都可以成为实现序对的基础。下面这个令人吃惊的事实能够最好地说明这一点：我们完全可以不用任何数据结构，只使用过程就可以实现序对。下面是有关的定义：

```
(define (cons x y)
  (define (dispatch m)
    (cond ((= m 0) x)
          ((= m 1) y)
          (else (error "Argument not 0 or 1 -- CONS" m))))
  dispatch)

(define (car z) (z 0))

(define (cdr z) (z 1))
```

This use of procedures corresponds to nothing like our intuitive notion of what data should be. Nevertheless, all we need to do to show that this is a valid way to represent pairs is to verify that these procedures satisfy the condition given above.<br>
过程的这一使用方式与我们有关数据应该是什么的直观认识大相径庭。但不管怎么说，如果要求我们说明这确实是一种表示序对的合法方式，那么只需要验证，上述几个过程满足了前面提出的所有条件。

The subtle point to notice is that the value returned by `(cons x y)` is a procedure -- namely the internally defined procedure `dispatch`, which takes one argument and returns either `x` or `y` depending on whether the argument is 0 or 1. Correspondingly, `(car z)` is defined to apply `z` to 0. Hence, if z is the procedure formed by (cons x y), then z applied to 0 will yield `x`. Thus, we have shown that `(car (cons x y))` yields `x`, as desired. Similarly, `(cdr (cons x y))` applies the procedure returned by `(cons x y)` to 1, which returns `y`. Therefore, this procedural implementation of pairs is a valid implementation, and if we access pairs using only `cons`, `car`, and `cdr` we cannot distinguish this implementation from one that uses ''real'' data structures.<br>
应该特别注意这里的一个微妙之处：由`(cons x y)`返回的值是一个过程---也就是那个内部定义的过程`dispatch`,它有一个参数，并能根据参数是0还是1,分别返回`×`或者`y`。与此相对应，`(car z)`被定义为将z应用于0,这样，如果z是由`(cons x y)`形成的过程，将`z`应用于0将会产生`x`,这样就证明了`(car(cons x y))`产生出`x`,正如我们所需要的。与此类似，`(cdr(cons x y))`将`(cons x y)`产生的过程应用于1而得到`y`。因此，序对的这一过程实现确实是一个合法的实现，如果只通过`cons`、`car`和`cdr`访问序对，我们将无法把这一实现与“真正的”数据结构区分开。

The point of exhibiting the procedural representation of pairs is not that our language works this way (Scheme, and Lisp systems in general, implement pairs directly, for efficiency reasons) but that it could work this way. The procedural representation, although obscure, is a perfectly adequate way to represent pairs, since it fulfills the only conditions that pairs need to fulfill. This example also demonstrates that the ability to manipulate procedures as objects automatically provides the ability to represent compound data. This may seem a curiosity now, but procedural representations of data will play a central role in our programming repertoire. This style of programming is often called **message passing**, and we will be using it as a basic tool in chapter 3 when we address the issues of modeling and simulation.<br>
上面展示了序对的一种过程性表示，这并不意味着我们所用的语言就是这样做的(Scheme和一般的Lisp系统都直接实现序对，主要是为了效率),而是说它确实可以这样做。这一过程性表示虽然有些隐晦，但它确实是一种完全合适的表示序对的方式，因为它满足了序对需要满足的所有条件。这一实例也说明可以将过程作为对象去操作，因此就自动地为我们提供了一种表示复合数据的能力。这些东西现在看起来好像只是很好玩，但实际上，数据的过程性表示将在我们的程序设计宝库里扮演一种核心角色。有关的程序设计风格通常称为 **消息传递**。在第3章里讨论模型和模拟时，我们将用它作为一种基本工具。

<div id="Exercise2.4" markdown>

Exercise 2.4.  Here is an alternative procedural representation of pairs. For this representation, verify that `(car (cons x y))` yields `x` for any objects `x` and `y`.<br>
练习2.4 下面是序对的另一种过程性表示方式。请针对这一表示验证，对于任意的`x`和`y`,`(car(cons x y))`都将产生出`x`。

```
(define (cons x y)
  (lambda (m) (m x y)))

(define (car z)
  (z (lambda (p q) p)))
```

What is the corresponding definition of `cdr?` (Hint: To verify that this works, make use of the substitution model of section [1.1.5].)<br>
对应的`cdr`应该如何定义?(提示：为了验证这一表示确实能行，请利用[1.1.5]节的代换模型。)
</div>

<div id="Exercise2.5" markdown>

Exercise 2.5.  Show that we can represent pairs of nonnegative integers using only numbers and arithmetic operations if we represent the pair a and b as the integer that is the product ${2^a 3^b}$. Give the corresponding definitions of the procedures `cons`, `car`, and `cdr`.<br>
练习2.5  请证明，如果将a和b的序对表示为乘积 ${2^a 3^b}$ 对应的整数，我们就可以只用非负整数和算术运算表示序对。请给出对应的过程`cons`、`car`和`cdr`的定义。
</div>

<div id="Exercise2.6" markdown>

Exercise 2.6.  In case representing pairs as procedures wasn't mind-boggling enough, consider that, in a language that can manipulate procedures, we can get by without numbers (at least insofar as nonnegative integers are concerned) by implementing 0 and the operation of adding 1 as<br>
练习2.6  如果觉得将序对表示为过程还不足以令人如雷灌顶，那么请考虑，在一个可以对过程做各种操作的语言里，我们完全可以没有数(至少在只考虑非负整数的情况下),可以将0和加一操作实现为：

```
(define zero (lambda (f) (lambda (x) x)))

(define (add-1 n)
  (lambda (f) (lambda (x) (f ((n f) x)))))
```

This representation is known as **Church numerals**, after its inventor, Alonzo Church, the logician who invented the ${\lambda}$ calculus.<br>
这一表示形式称为 **Church计数**，名字来源于其发明人数理逻辑学家Alonzo Church(丘奇)，${\lambda}$ 演算也是他发明的。

Define `one` and `two` directly (not in terms of `zero` and `add-1`). (Hint: Use substitution to evaluate `(add-1 zero)`). Give a direct definition of the addition procedure + (not in terms of repeated application of `add-1`).
请直接定义`one`和`two`(不用`zero`和`add-1`)(提示：利用代换去求值`(add-1 zero))`。请给出加法过程+的一个直接定义(不要通过反复应用`add-1`)。
</div>

[^1]:
    Surprisingly, this idea is very difficult to formulate rigorously. There are two approaches to giving such a formulation. One, pioneered by C. A. R. Hoare (1972), is known as the method of **abstract models**. It formalizes the ''procedures plus conditions'' specification as outlined in the rational-number example above. Note that the condition on the rational-number representation was stated in terms of facts about integers (equality and division). In general, abstract models define new kinds of data objects in terms of previously defined types of data objects. Assertions about data objects can therefore be checked by reducing them to assertions about previously defined data objects. Another approach, introduced by Zilles at MIT, by Goguen, Thatcher, Wagner, and Wright at IBM (see Thatcher, Wagner, and Wright 1978), and by Guttag at Toronto (see Guttag 1977), is called **algebraic specification**. It regards the ''procedures'' as elements of an abstract algebraic system whose behavior is specified by axioms that correspond to our ``conditions,'' and uses the techniques of abstract algebra to check assertions about data objects. Both methods are surveyed in the paper by Liskov and Zilles (1975).<br>
    令人吃惊的是，将这一思想严格地形式化却非常困难。目前存在着两种完成这一形式化的途径。第一种由C.A.R.Hoare(1972)提出，称为 **抽象模型** 方法，它形式化了如上面有理数实例中所勾勒出的，“过程加条件”的规范描述。请注意，这里对于有理数表示的条件是基于有关整数的事实(相等和除法)陈述的。一般而言，抽象模型方法总是基于某些已经有定义的数据对象类型，定义出一类新的数据对象。这样，有关这些新对象的断言就可以归约为有关已有定义的数据对象的断言。另一种途径由MIT的Zilles、Goguen和IBM的Thatcher、Wagner和Wright(见Thatcher,Wagner, and Wright 1978),以及Toronto的Guttag(见Guttag 1977)提出，称为 **代数规范**。这一方式将“过程”看作是一个抽象代数系统的元素，系统的行为由一些对应于我们的“条件”的公理刻画，并通过抽象代数的技术去检查有关数据对象的断言。Liskov和Zilles的论文(Liskov andZilles1975)里综述了这两种方法。


[2.1.1]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_1/2.1.1/

[1.1.5]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_1/1.1.5/