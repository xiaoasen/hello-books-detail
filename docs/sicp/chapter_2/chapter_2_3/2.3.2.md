---
comments: true
---

# 2.3.2  Example: Symbolic Differentiation（实例：符号求导）
As an illustration of symbol manipulation and a further illustration of data abstraction, consider the design of a procedure that performs symbolic differentiation of algebraic expressions. We would like the procedure to take as arguments an algebraic expression and a variable and to return the derivative of the expression with respect to the variable. For example, if the arguments to the procedure are ${ax^2 + bx + c}$ and ${x}$, the procedure should return ${2ax + b}$. Symbolic differentiation is of special historical significance in Lisp. It was one of the motivating examples behind the development of a computer language for symbol manipulation. Furthermore, it marked the beginning of the line of research that led to the development of powerful systems for symbolic mathematical work, which are currently being used by a growing number of applied mathematicians and physicists.<br>
为了阐释符号操作的情况，并进一步阐释数据抽象的思想，现在考虑设计一个执行代数表达式的符号求导的过程。我们希望该过程以一个代数表达式和一个变量作为参数，返回这个表达式相对于该变量的导数。例如，如果送给这个过程的参数是 ${ax^2 + bx + c}$ 和 ${x}$,它应该返回 ${2ax + b}$。符号求导数对于Lisp有着特殊的历史意义，它正是推动人们去为符号操作开发计算机语言的重要实例之一。进一步说，它也是人们为符号数学工作开发强有力系统的研究领域的开端，今天已经有越来越多的应用数学家和物理学家们正在使用这类系统。

In developing the symbolic-differentiation program, we will follow the same strategy of data abstraction that we followed in developing the rational-number system of section [2.1.1]. That is, we will first define a differentiation algorithm that operates on abstract objects such as ''sums,'' ''products,'' and ''variables'' without worrying about how these are to be represented. Only afterward will we address the representation problem.<br>
为了开发出一个符号计算程序，我们将按照[2.1.1]节开发有理数系统那样，采用同样的数据抽象策略。也就是说，首先定义一个求导算法，令它在一些抽象对象上操作，例如“和”、“乘积”和"变量",并不考虑这些对象实际上如何表示，以后才去关心具体表示的问题。

## The differentiation program with abstract data(对抽象数据的求导程序)
In order to keep things simple, we will consider a very simple symbolic-differentiation program that handles expressions that are built up using only the operations of addition and multiplication with two arguments. Differentiation of any such expression can be carried out by applying the following reduction rules:<br>
为了使有关的讨论简单化，我们在这里考虑一个非常简单的符号求导程序，它处理的表达式都是由对于两个参数的加和乘运算构造起来的。对于这种表达式求导的工作可以通过下面几条归约规则完成：

$$ \frac{dc}{dr}=0\text{ for c a constant or a variable different from r} $$

$$ \frac{dx}{dx}=1, $$

$$ \frac{d(u+\nu)}{dx}=\frac{du}{dx}+\frac{d\nu}{dx}, $$

$$ \frac{d(uv)}{dx}=u\frac{dv}{dx}+v\frac{du}{dx}. $$

Observe that the latter two rules are recursive in nature. That is, to obtain the derivative of a sum we first find the derivatives of the terms and add them. Each of the terms may in turn be an expression that needs to be decomposed. Decomposing into smaller and smaller pieces will eventually produce pieces that are either constants or variables, whose derivatives will be either 0 or 1.<br>
可以看到，这里的最后两条规则具有递归的性质，也就是说，要想得到一个和式的导数，我们首先要找出其中各个项的导数，而后将它们相加。这里的每个项又可能是需要进一步分解的表达式。通过这种分解，我们能得到越来越小的片段，最终将产生出常量或者变量，它们的导数就是0或者1。

To embody these rules in a procedure we indulge in a little wishful thinking, as we did in designing the rational-number implementation. If we had a means for representing algebraic expressions, we should be able to tell whether an expression is a sum, a product, a constant, or a variable. We should be able to extract the parts of an expression. For a sum, for example we want to be able to extract the addend (first term) and the augend (second term). We should also be able to construct expressions from parts. Let us assume that we already have procedures to implement the following selectors, constructors, and predicates:<br>
为了能在一个过程中体现这些规则，我们用一下按愿望思维，就像在前面设计有理数的实现时所做的那样。如果现在有了一种表示代数表达式的方式，我们一定能判断出某个表达式是否为一个和式、乘式、常量或者变量，也能提取出表达式里的各个部分。对于一个和式(举例来说),我们可能希望取得其被加项(第一个项)和加项(第二个项)。我们还需要能从几个部分出发构造出整个表达式。让我们假定现在已经有了一些过程，它们实现了下述的构造函数、选择函数和谓词：

```
(variable? e)	Is e a variable?
(same-variable? v1 v2)	Are v1 and v2 the same variable?
(sum? e) Is e a sum?
(addend e)	Addend of the sum e.
(augend e)	Augend of the sum e.
(make-sum a1 a2)	Construct the sum of a1 and a2.
(product? e) Is e a product?
(multiplier e)	Multiplier of the product e.
(multiplicand e)	Multiplicand of the product e.
(make-product m1 m2)	Construct the product of m1 and m2.
```

Using these, and the primitive predicate `number?`, which identifies numbers, we can express the differentiation rules as the following procedure:<br>
利用这些过程，以及判断表达式是否数值的基本过程`number?`,我们就可以将各种求导规则用下面的过程表达出来了：

```
(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
         (if (same-variable? exp var) 1 0))
        ((sum? exp)
         (make-sum (deriv (addend exp) var)
                   (deriv (augend exp) var)))
        ((product? exp)
         (make-sum
           (make-product (multiplier exp)
                         (deriv (multiplicand exp) var))
           (make-product (deriv (multiplier exp) var)
                         (multiplicand exp))))
        (else
         (error "unknown expression type -- DERIV" exp))))
```

This `deriv` procedure incorporates the complete differentiation algorithm. Since it is expressed in terms of abstract data, it will work no matter how we choose to represent algebraic expressions, as long as we design a proper set of selectors and constructors. This is the issue we must address next.<br>
过程`deriv`里包含了一个完整的求导算法。因为它是基于抽象数据表述的，因此，无论我们如何选择代数表达式的具体表示，只要设计了一组正确的选择函数和构造函数，这个过程都可以工作。表示的问题是下面必须考虑的问题。

## Representing algebraic expressions(代数表达式的表示)
We can imagine many ways to use list structure to represent algebraic expressions. For example, we could use lists of symbols that mirror the usual algebraic notation, representing ${ax + b}$ as the list `(a * x + b)`. However, one especially straightforward choice is to use the same parenthesized prefix notation that Lisp uses for combinations; that is, to represent ${ax + b}$ as `(+ (* a x) b)`. Then our data representation for the differentiation problem is as follows:<br>
我们可以设想出许多用表结构表示代数表达式的方法。例如，可以利用符号的表去直接反应代数的记法形式，将表达式 ${ax + b}$ 表示为表`(a * x + b)`。然而，一种特别直截了当的选择，是采用Lisp里面表示组合式的那种带括号的前缀形式，也就是说，将ax+b表示为`(+ (* a x) b)`。这样，我们有关求导问题的数据表示就是：

- The variables are symbols. They are identified by the primitive predicate `symbol?`:<br>
变量就是符号，它们可以用基本谓词symbol?判断：

```
(define (variable? x) (symbol? x))
```

- Two variables are the same if the symbols representing them are `eq?`:<br>
两个变量相同就是表示它们的符号相互eq?

```
(define (same-variable? v1 v2)
  (and (variable? v1) (variable? v2) (eq? v1 v2)))
```

- Sums and products are constructed as lists:<br>
和式与乘式都构造为表

```
(define (make-sum a1 a2) (list '+ a1 a2))

(define (make-product m1 m2) (list '* m1 m2))
```

- A sum is a list whose first element is the symbol `+`:<br>
和式就是第一个元素为符号+的表

```
(define (sum? x)
  (and (pair? x) (eq? (car x) '+)))
```

- The addend is the second item of the sum list:<br>
被加数是表示和式的表里的第二个元素：

```
(define (addend s) (cadr s))
```

- The augend is the third item of the sum list:<br>
加数是表示和式的表里的第三个元素：

```
(define (augend s) (caddr s))
```

- A product is a list whose first element is the symbol `*`:<br>
乘式就是第一个元素为符号`*`的表：

```
(define (product? x)
  (and (pair? x) (eq? (car x) '*)))
```

- The multiplier is the second item of the product list:<br>
被乘数是表示乘式的表里的第二个元素：

```
(define (multiplier p) (cadr p))
```

- The multiplicand is the third item of the product list:<br>
乘数是表示乘式的表里的第三个元素：

```
(define (multiplicand p) (caddr p))
```

Thus, we need only combine these with the algorithm as embodied by `deriv` in order to have a working symbolic-differentiation program. Let us look at some examples of its behavior:<br>
这样，为了得到一个能够工作的符号求导程序，我们只需将这些过程与`deriv`装在一起。现在让我们看几个表现这一程序的行为的实例：

```
(deriv '(+ x 3) 'x)
(+ 1 0)
(deriv '(* x y) 'x)
(+ (* x 0) (* 1 y))
(deriv '(* (* x y) (+ x 3)) 'x)
(+ (* (* x y) (+ 1 0))
   (* (+ (* x 0) (* 1 y))
      (+  x 3)))
```

The program produces answers that are correct; however, they are unsimplified. It is true that<br>
程序产生出的这些结果是对的，但是它们没有经过化简。我们确实有：

$$ \frac{d(xy)}{dx}=x\cdot 0+1\cdot y, $$

but we would like the program to know that ${x \cdot 0 = 0}$, ${1 \cdot y = y}$, and ${0 + y = y}$. The answer for the second example should have been simply `y`. As the third example shows, this becomes a serious issue when the expressions are complex.<br>
当然，我们也可能希望这一程序能够知道 ${x \cdot 0 = 0}$, ${1 \cdot y = y}$ 以及 ${0 + y = y}$。因此，第二个例子的结果就应该是简单的y。正如上面的第三个例子所显示的，当表达式变得更加复杂时，这一情况也可能变成严重的问题。

Our difficulty is much like the one we encountered with the rational-number implementation: we haven't reduced answers to simplest form. To accomplish the rational-number reduction, we needed to change only the constructors and the selectors of the implementation. We can adopt a similar strategy here. We won't change `deriv` at all. Instead, we will change `make-sum` so that if both summands are numbers, `make-sum` will add them and return their sum. Also, if one of the summands is 0, then `make-sum` will return the other summand.<br>
现在所面临的困难很像我们在做有理数首先时所遇到的问题：希望将结果化简到最简单的形式。为了完成有理数的化简，我们只需要修改构造函数和选择函数的实现。这里也可以采取同样的策略。我们在这里也完全不必修改`deriv`,只需要修改`make-sum`,使得当两个求和对象都是数时，`make-sum`求出它们的和返回。还有，如果其中的一个求和对象是0,那么`make-sum`就直接返回另一个对象。

```
(define (make-sum a1 a2)
  (cond ((=number? a1 0) a2)
        ((=number? a2 0) a1)
        ((and (number? a1) (number? a2)) (+ a1 a2))
        (else (list '+ a1 a2))))
```

This uses the procedure `=number?`, which checks whether an expression is equal to a given number:<br>
在这个实现里用到了过程`=number?`,它检查某个表达式是否等于一个给定的数。

```
(define (=number? exp num)
  (and (number? exp) (= exp num)))
```

Similarly, we will change `make-product` to build in the rules that 0 times anything is 0 and 1 times anything is the thing itself:<br>
与此类似，我们也需要修改`make-product`,设法引进下面的规则：0与任何东西的乘积都是0,1与任何东西的乘积总是那个东西：

```
(define (make-product m1 m2)
  (cond ((or (=number? m1 0) (=number? m2 0)) 0)
        ((=number? m1 1) m2)
        ((=number? m2 1) m1)
        ((and (number? m1) (number? m2)) (* m1 m2))
        (else (list '* m1 m2))))
```

Here is how this version works on our three examples:<br>
下面是这一新过程版本对前面三个例子的结果：<br>

```
(deriv '(+ x 3) 'x)
1
(deriv '(* x y) 'x)
y
(deriv '(* (* x y) (+ x 3)) 'x)
(+ (* x y) (* y (+ x 3)))
```

Although this is quite an improvement, the third example shows that there is still a long way to go before we get a program that puts expressions into a form that we might agree is ''simplest.'' The problem of algebraic simplification is complex because, among other reasons, a form that may be simplest for one purpose may not be for another.<br>
显然情况已经大大改观。但是，第三个例子还是说明，要想做出一个程序，使它能将表达式做成我们都能同意的“最简单”形式，前面还有很长的路要走。代数化简是一个非常复杂的问题，除了其他各种因素之外，还有另一个根本性的问题：对于某种用途的最简形式，对于另一用途可能就不是最简形式。

<div id="Exercise2.56" markdown>

Exercise 2.56.  Show how to extend the basic differentiator to handle more kinds of expressions. For instance, implement the differentiation rule<br>
练习2.56  请说明如何扩充基本求导规则，以便能够处理更多种类的表达式。例如，实现下述求导规则:

$$ \frac{d(u^{n})}{d x}=n u^{n-1}\left(\frac{d u}{d x}\right) $$

by adding a new clause to the `deriv` program and defining appropriate procedures `exponentiation?`, `base`, `exponent`, and `make-exponentiation`. (You may use the symbol `**` to denote exponentiation.) Build in the rules that anything raised to the power 0 is 1 and anything raised to the power 1 is the thing itself.<br>
通过给程序`deriv`增加一个新子句，并以适当方式定义过程`exponentiation?`、`base`、`exponent`和`make-exponentiation`的方式. (你可以考虑用符号**表示乘幂):
</div>

<div id="Exercise2.57" markdown>

Exercise 2.57.  Extend the differentiation program to handle sums and products of arbitrary numbers of (two or more) terms. Then the last example above could be expressed as<br>
练习2.57  请扩充求导程序，使之能处理任意项(两项或者更多项)的和与乘积。这样，上面的最后一个例子就可以表示为：

```
(deriv '(* x y (+ x 3)) 'x)
```

Try to do this by changing only the representation for sums and products, without changing the `deriv` procedure at all. For example, the `addend` of a sum would be the first term, and the `augend` would be the sum of the rest of the terms.<br>
设法通过只修改和与乘积的表示，而完全不修改过程`deriv`的方式完成这一扩充。例如，让一个和式的`addend`是它的第一项，而其`augend`是和式中的其余项。

</div>

<div id="Exercise2.58" markdown>

Exercise 2.58.  Suppose we want to modify the differentiation program so that it works with ordinary mathematical notation, in which `+` and `*` are infix rather than prefix operators. Since the differentiation program is defined in terms of abstract data, we can modify it to work with different representations of expressions solely by changing the predicates, selectors, and constructors that define the representation of the algebraic expressions on which the differentiator is to operate.<br>
练习2.58  假定我们希望修改求导程序，使它能用于常规数学公式，其中`+`和`*`采用的是中缀运算符而不是前缀。由于求导程序是基于抽象数据定义的，要修改它，使之能用于另一种不同的表达式表示，我们只需要换一套工作在新的、求导程序需要使用的代数表达式的表示形式上的谓词、选择函数和构造函数。

a. Show how to do this in order to differentiate algebraic expressions presented in infix form, such as `(x + (3 * (x + (y + 2))))`. To simplify the task, assume that + and * always take two arguments and that expressions are fully parenthesized.<br>
请说明怎样做出这些过程，以便完成在中缀表示形式(例如`(x + (3 * (x + (y + 2))))`)上的代数表达式求导。为了简化有关的工作，现在可以假定`+`和 `*` 总是取两个参数，而且表达式中已经加上了所有的括号。

b. The problem becomes substantially harder if we allow standard algebraic notation, such as `(x + 3 * (x + y + 2))`, which drops unnecessary parentheses and assumes that multiplication is done before addition. Can you design appropriate predicates, selectors, and constructors for this notation such that our derivative program still works?<br>
如果允许标准的代数写法，例如 `(x + 3 * (x + y + 2))`,问题就会变得更困难许多。在这种表达式里可能不写不必要的括号，并要假定乘法应该在加法之前完成。你还能为这种表示方式设计好适当的谓词、选择函数和构造函数，使我们的求导程序仍然能工作吗?

</div>


[2.1.1]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_1/2.1.1/