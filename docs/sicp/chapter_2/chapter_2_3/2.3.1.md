---
comments: true
---

# 2.3.1  Quotation(引号)
If we can form compound data using symbols, we can have lists such as<br>
如果我们能构造出采用符号的复合数据，我们就可以有下面这类的表：

```
(a b c d)
(23 45 17)
((Norah 12) (Molly 9) (Anna 7) (Lauren 6) (Charlotte 4))
```

Lists containing symbols can look just like the expressions of our language:<br>
这些包含着符号的表看起来就像是我们语言里的表达式：

```
(* (+ 23 45) (+ x 9))

(define (fact n) (if (= n 1) 1 (* n (fact (- n 1)))))
```

In order to manipulate symbols we need a new element in our language: the ability to quote a data object. Suppose we want to construct the list `(a b)`. We can't accomplish this with `(list a b)`, because this expression constructs a list of the values of `a` and `b` rather than the symbols themselves. This issue is well known in the context of natural languages, where words and sentences may be regarded either as semantic entities or as character strings (syntactic entities). The common practice in natural languages is to use quotation marks to indicate that a word or a sentence is to be treated literally as a string of characters. For instance, the first letter of ''John'' is clearly ''J.'' If we tell somebody ''say your name aloud,'' we expect to hear that person's name. However, if we tell somebody ''say 'your name' aloud,'' we expect to hear the words ''your name.'' Note that we are forced to nest quotation marks to describe what somebody else might say.[^1]<br>
为了能够操作这些符号，我们的语言里就需要有一种新元素：为数据对象加引号的能力。假定我们希望构造出表`(a b)`,当然不能用`(list a b)`完成这件事，因为这一表达式将要构造出的是`a`和`b`的值的表，而不是这两个符号本身的表。在自然语言的环境中，这种情况也是众所周知的，在那里的单词和句子可能看作语义实体，也可以看作是字符的序列(语法实体)。在自然语言里，常见的方式就是用引号表明一个词或者一个句子应作为文字看待，将它们直接作为字符的序列。例如说，“John”的第一个字母显然是“J”。如果我们对某人说“大声说你的名字",此时希望听到的是那个人的名字。如果说“大声说‘你的名字’”,此时希望听到的就是词组“你的名字”。请注意，我们在这里不得不用嵌套的引号去描述别人应该说的东西。[^1]

We can follow this same practice to identify lists and symbols that are to be treated as data objects rather than as expressions to be evaluated. However, our format for quoting differs from that of natural languages in that we place a quotation mark (traditionally, the single quote symbol ') only at the beginning of the object to be quoted. We can get away with this in Scheme syntax because we rely on blanks and parentheses to delimit objects. Thus, the meaning of the single quote character is to quote the next object.[^2]<br>
我们可以按照同样的方式，将表和符号标记为应该作为数据对象看待，而不是作为应该求值的表达式。然而，这里所用的引号形式与自然语言中的不同，我们只在被引对象的前面放一个引号(按照习惯，在这里用单引号)。在Scheme里可以不写结束引号，因为这里已经靠空白和括号将对象分隔开，一个单引号的意义就是引用下一个对象[^2]。

Now we can distinguish between symbols and their values:<br>
现在我们就可以区分符号和它们的值了：

```
(define a 1)

(define b 2)

(list a b)
(1 2)

(list 'a 'b)
(a b)

(list 'a b)
(a 2)
```

Quotation also allows us to type in compound objects, using the conventional printed representation for lists:[^3]<br>
引号也可以用于复合对象，其中采用的是表的方便的输出表示方式[^3];

```
(car '(a b c))
a

(cdr '(a b c))
(b c)
```

In keeping with this, we can obtain the empty list by evaluating `'()`, and thus dispense with the variable `nil`.<br>
记住这些之后，我们就可以通过求值`'()`得到空表，这样就可以丢掉变量`nil`了。

One additional primitive used in manipulating symbols is `eq?`, which takes two symbols as arguments and tests whether they are the same.[^4] Using `eq?`, we can implement a useful procedure called `memq`. This takes two arguments, a symbol and a list. If the symbol is not contained in the list (i.e., is not `eq?` to any item in the list), then `memq` returns false. Otherwise, it returns the sublist of the list beginning with the first occurrence of the symbol:<br>
为了能对符号做各种操作，我们还需要用另一个基本过程`eq?`,这个过程以两个符号作为参数，检查它们是否为同样的符号[^4]。利用`eq?`可以实现一个称为`memq`的有用过程，它以一个符号和一个表为参数。如果这个符号不包含在这个表里(也就是说，它与表里的任何项目都不`eq?`),`memq`就返回假；否则就返回该表的由这个符号的第一次出现开始的那个子表：

```
(define (memq item x)
  (cond ((null? x) false)
        ((eq? item (car x)) x)
        (else (memq item (cdr x)))))
```

For example, the value of<br>
举例来说，表达式：

```
(memq 'apple '(pear banana prune))
```

is false, whereas the value of<br>
的值是假，而表达式：

```
(memq 'apple '(x (apple sauce) y apple pear))
```

is (apple pear).<br>
的值是(apple pear)。

<div id="Exercise2.53" markdown>

Exercise 2.53.  What would the interpreter print in response to evaluating each of the following expressions?<br>
练习2.53  解释器在求值下面各个表达式时将打印出什么?

```
(list 'a 'b 'c)

(list (list 'george))
(cdr '((x1 x2) (y1 y2)))

(cadr '((x1 x2) (y1 y2)))
(pair? (car '(a short list)))
(memq 'red '((red shoes) (blue socks)))

(memq 'red '(red shoes blue socks))
```
</div>

<div id="Exercise2.54" markdown>

Exercise 2.54.  Two lists are said to be `equal?` if they contain equal elements arranged in the same order. For example,<br>
练习2.54  如果两个表包含着同样元素，这些元素也按同样顺序排列，那么就称这两个表`equal?`。例如

```
(equal? '(this is a list) '(this is a list))
```

is true, but<br>
是真；而

```
(equal? '(this is a list) '(this (is a) list))
```

is false. To be more precise, we can define `equal?` recursively in terms of the basic `eq?` equality of symbols by saying that `a` and `b` are `equal?` if they are both symbols and the symbols are `eq?`, or if they are both lists such that `(car a)` is `equal?` to `(car b)` and `(cdr a)` is `equal?` to `(cdr b)`. Using this idea, implement `equal?` as a procedure.[^5]<br>
是假。说得更准确些，我们可以从符号相等的基本eq?出发，以递归方式定义出`equal?`。`a`和`b`是`equal?`的，如果它们都是符号，而且这两个符号满足eq?;或者它们都是表，而且`(car a)`和`(car b)`相互`equal?`,它们的`(cdr a)`和`(cdr b)`也是`equal?`。请利用这一思路定义出equal?过程[^5]。

</div>

<div id="Exercise2.55" markdown>

Exercise 2.55.  Eva Lu Ator types to the interpreter the expression<br>
练习2.55 Eva Lu Ator输入了表达式：

```
(car ''abracadabra)
```

To her surprise, the interpreter prints back `quote`. Explain.<br>
令她吃惊的是解释器打印出的是`quote`。请解释这一情况。
</div>

[^1]:
    Allowing quotation in a language wreaks havoc with the ability to reason about the language in simple terms, because it destroys the notion that equals can be substituted for equals. For example, three is one plus two, but the word ''three'' is not the phrase ''one plus two.'' Quotation is powerful because it gives us a way to build expressions that manipulate other expressions (as we will see when we write an interpreter in chapter 4). But allowing statements in a language that talk about other statements in that language makes it very difficult to maintain any coherent principle of what ''equals can be substituted for equals'' should mean. For example, if we know that the evening star is the morning star, then from the statement ''the evening star is Venus'' we can deduce ''the morning star is Venus.'' However, given that ''John knows that the evening star is Venus'' we cannot infer that ''John knows that the morning star is Venus.''<br>
    允许在一个语言中使用引号，将会极大地损害根据简单词语在语言中做推理的能力，因为它破坏了对等的东西可以相互替换的观念。举个例子，三等于二加一，但是“三”这个字却不等于“二加一”这个短语。引号是很有威力的东西，因为它使我们可以构造起一种能操作其他表达式的表达式(正如我们将在第4章里看到的那样)。但是，在一种语言里允许用语句去讨论这一语言里的其他语句，那么有关“对等的东西可以相互代换”究竟是什么意思，我们就很难给任何具有内在统一性的说法了。举例说，如果我们知道长庚星就是启明星，那么我们就可以从句子“长庚屋就是金星”推导出“启明星就是金星”。然而，即使有“张三知道长庚屋就是金星”,我们也无法推论说“张三知道启明星就是金星”。

[^2]:
    The single quote is different from the double quote we have been using to enclose character strings to be printed. Whereas the single quote can be used to denote lists or symbols, the double quote is used only with character strings. In this book, the only use for character strings is as items to be printed.<br>
    单引号和用于括起应该打印输出的字符串的双引号不同，单引号可以用于括起表和符号，而双引号只能用于字符串。在本书里只将字符串用于需要打印输出的对象。

[^3]:
    Strictly, our use of the quotation mark violates the general rule that all compound expressions in our language should be delimited by parentheses and look like lists. We can recover this consistency by introducing a special form `quote`, which serves the same purpose as the quotation mark. Thus, we would type `(quote a)` instead of `'a`, and we would type `(quote (a b c))` instead of `'(a b c)`. This is precisely how the interpreter works. The quotation mark is just a single-character abbreviation for wrapping the next complete expression with `quote` to form `(quote <expression>)`. This is important because it maintains the principle that any expression seen by the interpreter can be manipulated as a data object. For instance, we could construct the expression `(car '(a b c))`, which is the same as `(car (quote (a b c)))`, by evaluating `(list 'car (list 'quote '(a b c)))`.<br>
    严格地说，引号的这种使用方式，违背了我们语言中所有复合表达式都应该由括号限定，都具有表的形式的普遍性原则。通过引进特殊形式quote就可以恢复这种一致性，这种特殊形式的作用与引号完全一样。因此，我们完全可以用`(quote a)`代替`'a`,采用`(quote(a b c))`而不是`'(a b c)`。这也就是解释器的实际工作方式。引号只不过是一种将下一完整表达式用`(quote <expression>)`形式包裹起来的单字符缩写形式。这一点非常重要，因为它维持了我们的原则：解释器看到的所有表达式都可以作为数据对象去操作。例如，我们可以构造出表达式`(car'(a b c))`,它就等同于通过对表达式`(list 'car(list 'quote'(ab c)))`的求值而得到的`(car (quote (a b c)))`。


[^4]:
    We can consider two symbols to be ''the same'' if they consist of the same characters in the same order. Such a definition skirts a deep issue that we are not yet ready to address: the meaning of ''sameness'' in a programming language. We will return to this in chapter 3 (section [3.1.3]).<br>
    我们可以认为，两个符号是“同样”的，如果它们是由同样字符按照同样顺序构成。这一定义回避了一个我们目前尚且无法去探讨的深入问题：程序设计语言里“同样”的意义问题。我们将在第3章中重新回到这个问题(第[3.1.3]节)

[^5]:
    In practice, programmers use `equal?` to compare lists that contain numbers as well as symbols. Numbers are not considered to be symbols. The question of whether two numerically equal numbers (as tested by =) are also `eq?` is highly implementation-dependent. A better definition of `equal?` (such as the one that comes as a primitive in Scheme) would also stipulate that if `a` and `b` are both numbers, then `a` and `b` are `equal?` if they are numerically equal.<br>
    在实践中，程序员们不仅用`equal?`比较包含符号的表，也用它比较包含数值的表。有关两个数值相等的数(用检测)是否也`eq?`的问题高度依赖于具体实现。对于`equa1?`的一个更好的定义(例如Scheme中的基本过程)还要去检查 `a` 和 `b` 是否为两个数，如果是它们都是数，数值相等时就认为它们`equal?`。

[3.1.3]: {{ config.extra.homepage_sicp }}/chapter_3/chapter_3_1/3.1.3/