---
comments: true
---

# 2.2.3  Sequences as Conventional Interfaces (序列作为一种约定的界面)
In working with compound data, we've stressed how data abstraction permits us to design programs without becoming enmeshed in the details of data representations, and how abstraction preserves for us the flexibility to experiment with alternative representations. In this section, we introduce another powerful design principle for working with data structures -- the use of conventional interfaces.<br>
我们一直强调数据抽象在对复合数据的工作中的作用，借助这种思想，我们就能设计出不会被数据表示的细节纠缠的程序，使程序能够保持很好的弹性，得以应用到不同的具体表示上。在这一节里，我们将要介绍与数据结构有关的另一种强有力的设计原理——使用约定的界面。

In section [1.3] we saw how program abstractions, implemented as higher-order procedures, can capture common patterns in programs that deal with numerical data. Our ability to formulate analogous operations for working with compound data depends crucially on the style in which we manipulate our data structures. Consider, for example, the following procedure, analogous to the `count-leaves` procedure of section [2.2.2], which takes a tree as argument and computes the sum of the squares of the leaves that are odd:<br>
在[1.3]节里我们看到，可以通过实现为高阶过程的程序抽象，抓住处理数值数据的一些程序模式。要在复合数据上工作做出类似的操作，则对我们操控数据结构的方式有着深刻的依赖性。举个例子，考虑下面与[2.2.2]节中`count-leaves`过程类似的过程，它以一棵树为参数，计算出那些值为奇数的叶子的平方和：

```
(define (sum-odd-squares tree)
  (cond ((null? tree) 0)  
        ((not (pair? tree))
         (if (odd? tree) (square tree) 0))
        (else (+ (sum-odd-squares (car tree))
                 (sum-odd-squares (cdr tree))))))
```

On the surface, this procedure is very different from the following one, which constructs a list of all the even Fibonacci numbers `Fib(k)`, where `k` is less than or equal to a given integer `n`:<br>
从表面上看，这一过程与下面的过程很不一样。下面这个过程构造出的是所有偶数的斐波那契数`Fib(k)`的一个表，其中的`k`小于等于某个给定整数`n`:

```
(define (even-fibs n)
  (define (next k)
    (if (> k n)
        nil
        (let ((f (fib k)))
          (if (even? f)
              (cons f (next (+ k 1)))
              (next (+ k 1))))))
  (next 0))
```

Despite the fact that these two procedures are structurally very different, a more abstract description of the two computations reveals a great deal of similarity. The first program<br>
虽然这两个过程在结构上差异非常大，但是对于两个计算的抽象描述却会揭示出它们之间极大的相似性。第一个程序：

- enumerates the leaves of a tree;<br>
枚举出一棵树的树叶；
- filters them, selecting the odd ones;<br>
过滤它们，选出其中的奇数；
- squares each of the selected ones; and<br>
对选出的每一个数求平方；
- accumulates the results using +, starting with 0.<br>
用+累积起得到的结果，从0开始。

The second program<br>
而第二个程序：

- enumerates the integers from 0 to ${n}$;<br>
枚举从0到 ${n}$ 的整数；
- computes the Fibonacci number for each integer;<br>
对每个整数计算相应的斐波那契数；
- filters them, selecting the even ones; and<br>
过滤它们，选出其中的偶数；
- accumulates the results using `cons`, starting with the empty list.<br>
用`cons`累积得到的结果，从空表开始。

A signal-processing engineer would find it natural to conceptualize these processes in terms of signals flowing through a cascade of stages, each of which implements part of the program plan, as shown in figure [2.7](#Figure2.7). In `sum-odd-squares`, we begin with an enumerator, which generates a ''signal'' consisting of the leaves of a given tree. This signal is passed through a filter, which eliminates all but the odd elements. The resulting signal is in turn passed through a map, which is a ''transducer'' that applies the `square` procedure to each element. The output of the map is then fed to an accumulator, which combines the elements using `+`, starting from an initial 0. The plan for `even-fibs` is analogous.<br>
信号处理工程师们可能会发现，这种过程可以很自然地用流过一些级联的处理步骤的信号的方式描述，其中的每个处理步骤实现程序方案中的一个部分，如图[2.7](#Figure2.7)所示。对于第一种情况`sum-odd-squares`,我们从一个枚举器开始，它产生出由给定的树的所有树叶组成“信号”。这一信号流过一个过滤器，所有不是奇数的数都被删除了。这样得到的信号又通过一个映射，这是一个“转换装置”,它将`square`过程应用于每个元素。这一映射的输出被馈入一个累积器，该装置用+将得到的所有元素组合起来，以初始的0开始。`even-fibs`的工作过程与此类似。

<div id="Figure2.7" markdown>

<figure markdown>
  ![](../assets/Fig2.7.svg)
  <figcaption>Figure 2.7:  The signal-flow plans for the procedures `sum-odd-squares` (top) and `even-fibs` (bottom) reveal the commonality between the two programs.</figcaption>
</figure>

</div>

Unfortunately, the two procedure definitions above fail to exhibit this signal-flow structure. For instance, if we examine the `sum-odd-squares` procedure, we find that the enumeration is implemented partly by the `null?` and `pair?` tests and partly by the tree-recursive structure of the procedure. Similarly, the accumulation is found partly in the tests and partly in the addition used in the recursion. In general, there are no distinct parts of either procedure that correspond to the elements in the signal-flow description. Our two procedures decompose the computations in a different way, spreading the enumeration over the program and mingling it with the map, the filter, and the accumulation. If we could organize our programs to make the signal-flow structure manifest in the procedures we write, this would increase the conceptual clarity of the resulting code.<br>
遗憾的是，上面的两个过程定义并没有展现出这种信号流结构。譬如说，如果仔细考察`sum-odd-squares`过程，就会发现其中的枚举工作部分地由检查`null?`和`pair?`实现，部分地由过程的树形递归结构实现。与此类似，在那些检查中也可以看到一部分累积工作，另一部分是用在递归中的加法。一般而言，在这两个过程里，没有一个部分正好对应于信号流描述中的某一要素。我们的两个过程采用不同的方式分解了这个计算，将枚举工作散布在程序中各处，并将它与映射、过滤器和累积器混在一起。如果我们能够重新组织这一程序，使得信号流结构明显表现在写出的过程中，将会大大提高结果代码的清晰性。

## Sequence Operations(序列操作)
The key to organizing programs so as to more clearly reflect the signal-flow structure is to concentrate on the ''signals'' that flow from one stage in the process to the next. If we represent these signals as lists, then we can use list operations to implement the processing at each of the stages. For instance, we can implement the mapping stages of the signal-flow diagrams using the map procedure from section [2.2.1]:<br>
要组织好这些程序，使之能够更清晰地反应上面信号流的结构，最关键的一点就是将注意力集中在处理过程中从一个步骤流向下一个步骤的“信号”。如果我们用一些表来表示这些信号，那么就可以利用表操作实现每一步骤的处理。举例来说，我们可以用[2.2.1]节的map过程实现信号流图中的映射步骤：

```
(map square (list 1 2 3 4 5))
(1 4 9 16 25)
```

Filtering a sequence to select only those elements that satisfy a given predicate is accomplished by<br>
过滤一个序列，也就是选出其中满足某个给定谓词的元素，可以按下面方式做：

```
(define (filter predicate sequence)
  (cond ((null? sequence) nil)
        ((predicate (car sequence))
         (cons (car sequence)
               (filter predicate (cdr sequence))))
        (else (filter predicate (cdr sequence)))))
```

For example,<br>
例如，

```
(filter odd? (list 1 2 3 4 5))
(1 3 5)
```

Accumulations can be implemented by<br>
累积工作可以实现如下：

```
(define (accumulate op initial sequence)
  (if (null? sequence)
      initial
      (op (car sequence)
          (accumulate op initial (cdr sequence)))))
(accumulate + 0 (list 1 2 3 4 5))
15
(accumulate * 1 (list 1 2 3 4 5))
120
(accumulate cons nil (list 1 2 3 4 5))
(1 2 3 4 5)
```

All that remains to implement signal-flow diagrams is to enumerate the sequence of elements to be processed. For `even-fibs`, we need to generate the sequence of integers in a given range, which we can do as follows:<br>
剩下的就是实现有关的信号流图，枚举出需要处理的数据序列。对于`even-fibs`,我们需要生成出一个给定区间里的整数序列，这一序列可以如下做出：

```
(define (enumerate-interval low high)
  (if (> low high)
      nil
      (cons low (enumerate-interval (+ low 1) high))))
(enumerate-interval 2 7)
(2 3 4 5 6 7)
```

To enumerate the leaves of a tree, we can use[^1]<br>
要枚举出一棵树的所有树叶，则可以用[^1]

```
(define (enumerate-tree tree)
  (cond ((null? tree) nil)
        ((not (pair? tree)) (list tree))
        (else (append (enumerate-tree (car tree))
                      (enumerate-tree (cdr tree))))))
(enumerate-tree (list 1 (list 2 (list 3 4)) 5))
(1 2 3 4 5)
```

Now we can reformulate `sum-odd-squares` and `even-fibs` as in the signal-flow diagrams. For `sum-odd-squares`, we enumerate the sequence of leaves of the tree, filter this to keep only the odd numbers in the sequence, square each element, and sum the results:<br>
现在，我们就可以像上面的信号流图那样重新构造`sum-odd-squares`和`even-fibs`了。对于`sum-odd-squares`,我们需要枚举一棵树的树叶序列，过滤它，只留下序列中的奇数，求每个元素的平方，而后加起得到的结果：

```
(define (sum-odd-squares tree)
  (accumulate +
              0
              (map square
                   (filter odd?
                           (enumerate-tree tree)))))
```

For `even-fibs`, we enumerate the integers from 0 to ${n}$, generate the Fibonacci number for each of these integers, filter the resulting sequence to keep only the even elements, and accumulate the results into a list:<br>
对于`even-fibs`,我们需要枚举出从0到 ${n}$ 的所有整数，对某个整数生成相应的斐波那契数，通过过滤只留下其中的偶数，并将结果积累在一个表里：

```
(define (even-fibs n)
  (accumulate cons
              nil
              (filter even?
                      (map fib
                           (enumerate-interval 0 n)))))
```

The value of expressing programs as sequence operations is that this helps us make program designs that are modular, that is, designs that are constructed by combining relatively independent pieces. We can encourage modular design by providing a library of standard components together with a conventional interface for connecting the components in flexible ways.<br>
将程序表示为一些针对序列的操作，这样做的价值就在于能帮助我们得到模块化的程序设计，也就是说，得到由一些比较独立的片段的组合构成的设计。通过提供一个标准部件的库，并使这些部件都有着一些能以各种灵活方式相互连接的约定界面，将能进一步推动人们去做模块化的设计。

Modular construction is a powerful strategy for controlling complexity in engineering design. In real signal-processing applications, for example, designers regularly build systems by cascading elements selected from standardized families of filters and transducers. Similarly, sequence operations provide a library of standard program elements that we can mix and match. For instance, we can reuse pieces from the `sum-odd-squares` and `even-fibs` procedures in a program that constructs a list of the squares of the first ${n + 1}$ Fibonacci numbers:<br>
在工程设计中，模块化结构是控制复杂性的一种威力强大的策略。举例来说，在真实的信号处理应用中，设计者通常总是从标准化的过滤器和变换装置族中选出一些东西，通过级联的方式构造出各种系统。与此类似，序列操作也形成了一个可以混合和匹配使用的标准的程序元素库。例如，我们可以在另一个构造前 ${n + 1}$ 个斐波那契数的平方的程序里，使用取自过程`sum-odd-squares`和`even-fibs`的片段：

```
(define (list-fib-squares n)
  (accumulate cons
              nil
              (map square
                   (map fib
                        (enumerate-interval 0 n)))))
(list-fib-squares 10)
(0 1 1 4 9 25 64 169 441 1156 3025)
```

We can rearrange the pieces and use them in computing the product of the odd integers in a sequence:<br>
我们也可以重新安排有关的各个片段，将它们用在产生一个序列中所有奇数的平方之乘积的计算里：

```
(define (product-of-squares-of-odd-elements sequence)
  (accumulate *
              1
              (map square
                   (filter odd? sequence))))
(product-of-squares-of-odd-elements (list 1 2 3 4 5))
225
```

We can also formulate conventional data-processing applications in terms of sequence operations. Suppose we have a sequence of personnel records and we want to find the salary of the highest-paid programmer. Assume that we have a selector salary that returns the salary of a record, and a predicate `programmer?` that tests if a record is for a programmer. Then we can write<br>
我们同样可以采用序列操作的方式，重新去形式化各种常规的数据处理应用。假定有一个人事记录的序列，现在希望找出其中薪水最高的程序员的工资数额。假定现在有一个选择函数salary返回记录中的工资数，另有谓词`programmer?`检查某个记录是不是程序员，此时我们就可以写：

```
(define (salary-of-highest-paid-programmer records)
  (accumulate max
              0
              (map salary
                   (filter programmer? records))))
```

These examples give just a hint of the vast range of operations that can be expressed as sequence operations.[^2]<br>
这些例子给了我们一些启发，范围广大的许多操作都可以表述为序列操作.[^2]

Sequences, implemented here as lists, serve as a conventional interface that permits us to combine processing modules. Additionally, when we uniformly represent structures as sequences, we have localized the data-structure dependencies in our programs to a small number of sequence operations. By changing these, we can experiment with alternative representations of sequences, while leaving the overall design of our programs intact. We will exploit this capability in section [3.5], when we generalize the sequence-processing paradigm to admit infinite sequences.<br>
在这里，用表实现的序列被作为一种方便的界面，我们可以利用这种界面去组合起各种处理模块。进一步说，如果以序列作为所用的统一表示结构，我们就能将程序对于数据结构的依赖性局限到不多的几个序列操作上。通过修改这些操作，就可以在序列的不同表示之间转换，并保持程序的整个设计不变。在[3.5]节里还要继续探索这方面的能力，那时将把序列处理的范型推广到无穷序列。

<div id="Exercise2.33" markdown>

Exercise 2.33.  Fill in the missing expressions to complete the following definitions of some basic list-manipulation operations as accumulations:<br>
练习2.33 请填充下面缺失的表达式，完成将一些基本的表操作看作累积的定义：

```
(define (map p sequence)
  (accumulate (lambda (x y) <??>) nil sequence))
(define (append seq1 seq2)
  (accumulate cons <??> <??>))
(define (length sequence)
  (accumulate <??> 0 sequence))
```
</div>

<div id="Exercise2.34" markdown>

Exercise 2.34.  Evaluating a polynomial in ${x}$ at a given value of ${x}$ can be formulated as an accumulation. We evaluate the polynomial<br>
练习2.34 对于 ${x}$ 的某个给定值，求出一个多项式在 ${x}$ 的值，也可以形式化为一种累积。假定需要求下面多项式的值：

$$ a_n x^n + a_{n-1} x^{n-1} + \dots + a_1 x + a_0 $$

using a well-known algorithm called *Horner's rule*, which structures the computation as<br>
采用著名的Horner规则，可以构造出下面的计算：

$$ (\dots (a_n x + a_{n-1}) x + \dots + a_1) x + a_0. $$

In other words, we start with ${a_n}$, multiply by ${x}$, add ${a_{n-1}}$, multiply by ${x}$, and so on, until we reach ${a_0}$.[^3] Fill in the following template to produce a procedure that evaluates a polynomial using Horner's rule. Assume that the coefficients of the polynomial are arranged in a sequence, from ${a_0}$ through ${a_n}$.<br>
换句话说，我们可以从 ${a_n}$ 开始，乘以 ${x}$,再加上 ${a_{n-1}}$,乘以x,如此下去，直到处理完 ${a_0}$ [^3]。请填充下面的模板，做出一个利用Horner规则求多项式值的过程。假定多项式的系数安排在一个序列里，从 ${a_0}$ 直至 ${a_n}$。

```
(define (horner-eval x coefficient-sequence)
  (accumulate (lambda (this-coeff higher-terms) <??>)
              0
              coefficient-sequence))
```

For example, to compute ${1 + 3x + 5x^3 + x^5}$ at ${x = 2}$ you would evaluate<br>
例如，为了计算 ${1 + 3x + 5x^3 + x^5}$ 在 ${x = 2}$ 的值，你需要求值：

```
(horner-eval 2 (list 1 3 0 5 0 1))
```

</div>

<div id="Exercise2.35" markdown>

Exercise 2.35.  Redefine count-leaves from section [2.2.2] as an accumulation:<br>
练习2.35 将[2.2.2]节的`count-leaves`重新定义为一个累积：

```
(define (count-leaves t)
  (accumulate <??> <??> (map <??> <??>)))
```

</div>

<div id="Exercise2.36" markdown>

Exercise 2.36.  The procedure `accumulate-n` is similar to `accumulate` except that it takes as its third argument a sequence of sequences, which are all assumed to have the same number of elements. It applies the designated accumulation procedure to combine all the first elements of the sequences, all the second elements of the sequences, and so on, and returns a sequence of the results. For instance, if `s` is a sequence containing four sequences, `((1 2 3) (4 5 6) (7 8 9) (10 11 12))`, then the value of `(accumulate-n + 0 s)` should be the sequence `(22 26 30)`. Fill in the missing expressions in the following definition of `accumulate-n`:<br>
练习2.36  过程`accumulate-n`与`accumulate`类似，除了它的第三个参数是一个序列的序列，假定其中每个序列的元素个数相同。它用指定的累积过程去组合起所有序列的第一个元素，而后是所有序列的第二个元素，并如此做下去，返回得到的所有结果的序列。例如，如果`s`是包含着4个序列的序列`((1 2 3) (4 5 6) (7 8 9) (10 11 12))`的值就应该是序列`(22 26 30)`。请填充下面`accumulate-n`定义中所缺失的表达式：

```
(define (accumulate-n op init seqs)
  (if (null? (car seqs))
      nil
      (cons (accumulate op init <??>)
            (accumulate-n op init <??>))))
```
</div>

<div id="Exercise2.37" markdown>

Exercise 2.37.  Suppose we represent vectors ${v = (v_i)}$ as sequences of numbers, and matrices ${m = (m_{ij})}$ as sequences of vectors (the rows of the matrix). For example, the matrix<br>
练习2.37 假定我们将向量 ${v = (v_i)}$ 表示为数的序列，将矩阵 ${m = (m_{ij})}$ 表示为向量(矩阵行)的序列。例如，矩阵：

$$ 
\left(\matrix{	1 & 2 & 3 & 4 \cr
		4 & 5 & 6 & 6 \cr
		6 & 7 & 8 & 9 \cr }\right) $$

is represented as the sequence `((1 2 3 4) (4 5 6 6) (6 7 8 9))`. With this representation, we can use sequence operations to concisely express the basic matrix and vector operations. These operations (which are described in any book on matrix algebra) are the following:<br>
用序列`((1 2 3 4) (4 5 6 6) (6 7 8 9))`表示。对于这种表示，我们可以用序列操作简洁地表达基本的矩阵与向量运算。这些运算(任何有关矩阵代数的书里都有描述)如下：

$$ 
\begin{aligned}
\text{(dot-product v w)}& \text{ returns the sum}\Sigma_i v_i w_i;  \\
\text{(matrix-*-vector m v)}& \text{ returns the vector t},  &\text{ where }t_i=\Sigma_j m_{ij}v_j; \\
\text{(matrix-*-matrix m n)}& \text{ returns the matrix p},  &\text{ where }p_{ij}=\Sigma_k m_{ik}n_{kj}; \\
\text{(transpose m)}& \text{ returns the matrix n},  &\text{ where }n_{ij}=m_{ji}.
\end{aligned}
 $$

We can define the dot product as[^4]<br>
我们可以将点积(dot product)定义为[^4]

```
(define (dot-product v w)
  (accumulate + 0 (map * v w)))
```

Fill in the missing expressions in the following procedures for computing the other matrix operations. (The procedure `accumulate-n` is defined in exercise [2.36](#Exercise2.36).)<br>
请填充下面过程里缺失的表达式，它们计算出其他的矩阵运算结果(过程`accumulate-n`在练习[2.36](#Exercise2.36)中定义)。

```
(define (matrix-*-vector m v)
  (map <??> m))
(define (transpose mat)
  (accumulate-n <??> <??> mat))
(define (matrix-*-matrix m n)
  (let ((cols (transpose n)))
    (map <??> m)))
```
</div>

<div id="Exercise2.38" markdown>

Exercise 2.38.  The `accumulate` procedure is also known as `fold-right`, because it combines the first element of the sequence with the result of combining all the elements to the right. There is also a `fold-left`, which is similar to `fold-right`, except that it combines elements working in the opposite direction:<br>
练习2.38  过程`accumulate`也称为`fold-right`,因为它将序列的第一个元素组合到右边所有元素的组合结果上。也有一个`fold-left`,它与`fold-right`类似，但却是按照相反方向去操作各个元素：

```
(define (fold-left op initial sequence)
  (define (iter result rest)
    (if (null? rest)
        result
        (iter (op result (car rest))
              (cdr rest))))
  (iter initial sequence))
```

What are the values of<br>
下面表达式的值是什么?

```
(fold-right / 1 (list 1 2 3))
(fold-left / 1 (list 1 2 3))
(fold-right list nil (list 1 2 3))
(fold-left list nil (list 1 2 3))
```

Give a property that `op` should satisfy to guarantee that `fold-right` and `fold-left` will produce the same values for any sequence.<br>
如果要求用某个`op`时保证`fold-right`和`fold-left`对任何序列都产生同样的结果，请给出op应该满足的性质。

</div>

<div id="Exercise2.39" markdown>

Exercise 2.39.   Complete the following definitions of `reverse` (exercise [2.18]) in terms of `fold-right` and `fold-left` from exercise [2.38](#Exercise2.38):<br>
练习2.39  基于练习[2.38](#Exercise2.38)的`fold-right`和`fold-left`完成`reverse`(练习[2.18])下面的定义：

```
(define (reverse sequence)
  (fold-right (lambda (x y) <??>) nil sequence))
(define (reverse sequence)
  (fold-left (lambda (x y) <??>) nil sequence))
```
</div>

## Nested Mappings(嵌套映射)
We can extend the sequence paradigm to include many computations that are commonly expressed using nested loops.[^5] Consider this problem: Given a positive integer ${n}$, find all ordered pairs of distinct positive integers ${i}$ and ${j}$, where ${1 \le j < i \le n}$, such that ${i + j}$ is prime. For example, if ${n}$ is 6, then the pairs are the following:<br>
我们可以扩充序列范型，将许多通常用嵌套循环表述的计算也包含进来[^5]。现在考虑下面的问题：给定了自然数 ${n}$,找出所有不同的有序对 ${i}$ 和 ${j}$,其中 ${1 \le j < i \le n}$,使得 ${i + j}$ 是素数。例如，假定 ${n}$ 是6,满足条件的序对就是：

$$
\begin{array}{c|cccccccc}i&2&3&4&4&5&6&6\\ j&1&2&1&3&2&1&5\\ \hline i+j&3&5&5&7&7&7&11\end{array}
$$

A natural way to organize this computation is to generate the sequence of all ordered pairs of positive integers less than or equal to ${n}$, filter to select those pairs whose sum is prime, and then, for each pair ${(i, j)}$ that passes through the filter, produce the triple ${(i, j, i + j)}$.<br>
完成这一计算的一种很自然的组织方式：首先生成出所有小于等于 ${n}$ 的正自然数的有序对；而后通过过滤，得到那些和为素数的有序对；最后对每个通过了过滤的序对 ${(i, j)}$,产生出一个三元组 ${(i, j, i + j)}$。

Here is a way to generate the sequence of pairs: For each integer ${i \le n}$, enumerate the integers ${j < i}$, and for each such ${i}$ and ${j}$ generate the pair ${(i, j)}$. In terms of sequence operations, we map along the sequence `(enumerate-interval 1 n)`. For each ${i}$ in this sequence, we map along the sequence `(enumerate-interval 1 (- i 1))`. For each ${j}$ in this latter sequence, we generate the pair `(list i j)`. This gives us a sequence of pairs for each ${i}$. Combining all the sequences for all the ${i}$ (by accumulating with `append`) produces the required sequence of pairs:[^6]<br>
这里是生成有序对的序列的一种方式：对于每个整数 ${i \le n}$,枚举出所有的整数 ${j < i}$,并对每一对 ${i}$ 和 ${j}$ 生成序对 ${(i, j)}$。用序列操作的方式说，我们要对序列`(enumerate-interval 1 n)`做一次映射。对于这个序列里的每个 ${i}$,我们都要对序列`(enumerate-interval 1 (- i 1))`做映射。对于后一序列中的每个 ${j}$,我们生成出序对`(list i j)`。这样就对每个 ${i}$ 得到了一个序对的序列。将针对所有i的序列组合到一起(用append累积起来),就能产生出所需的序对序列了[^6]。


```
(accumulate append
            nil
            (map (lambda (i)
                   (map (lambda (j) (list i j))
                        (enumerate-interval 1 (- i 1))))
                 (enumerate-interval 1 n)))
```

The combination of mapping and accumulating with `append` is so common in this sort of program that we will isolate it as a separate procedure:<br>
由于在这类程序里常要用到映射，并用`append`做累积，我们将它独立出来定义为一个过程：

```
(define (flatmap proc seq)
  (accumulate append nil (map proc seq)))
```

Now filter this sequence of pairs to find those whose sum is prime. The filter predicate is called for each element of the sequence; its argument is a pair and it must extract the integers from the pair. Thus, the predicate to apply to each element in the sequence is<br>
现在可以过滤这一序对的序列，找出那些和为素数的序对。对序列里的每个元素调用过滤谓词。由于这个谓词的参数是一个序对，所以它必须将两个整数从序对里提取出来。这样，作用到序列中每个元素上的谓词就是：

```
(define (prime-sum? pair)
  (prime? (+ (car pair) (cadr pair))))
```

Finally, generate the sequence of results by mapping over the filtered pairs using the following procedure, which constructs a triple consisting of the two elements of the pair along with their sum:<br>
最后还要生成出结果的序列，为此只需将下面过程映射到通过过滤后的序对上，对每个有序对里的两个元素，这一过程生成出一个包含了它们的和的三元组：

```
(define (make-pair-sum pair)
  (list (car pair) (cadr pair) (+ (car pair) (cadr pair))))
```

Combining all these steps yields the complete procedure:<br>
将所有这些组合到一起，就得到了完整的过程：

```
(define (prime-sum-pairs n)
  (map make-pair-sum
       (filter prime-sum?
               (flatmap
                (lambda (i)
                  (map (lambda (j) (list i j))
                       (enumerate-interval 1 (- i 1))))
                (enumerate-interval 1 n)))))
```

Nested mappings are also useful for sequences other than those that enumerate intervals. Suppose we wish to generate all the permutations of a set ${S;}$ that is, all the ways of ordering the items in the set. For instance, the permutations of ${\{1, 2, 3\}}$ are ${\{1, 2, 3\}}$, ${\{1, 3, 2\}}$, ${\{2, 1, 3\}}$, ${\{2, 3, 1\}}$, ${\{3, 1, 2\}}$, and ${\{3, 2, 1\}}$. Here is a plan for generating the permutations of ${S}$: For each item ${x}$ in ${S}$, recursively generate the sequence of permutations of ${S - x}$,[^7] and adjoin ${x}$ to the front of each one. This yields, for each ${x}$ in ${S}$, the sequence of permutations of ${S}$ that begin with ${x}$. Combining these sequences for all ${x}$ gives all the permutations of ${S}$:[^8]<br>
嵌套的映射不仅能用于枚举这种区间，也可用于其他序列。假设我们希望生成集合 ${S}$ 的所有排列，也就是说，生成这一集合的元素的所有可能排序方式。例如，${\{1, 2, 3\}}$ 的所有排列是 ${\{1, 2, 3\}}$, ${\{1, 3, 2\}}$,${\{2, 1, 3\}}$,${\{2, 3, 1\}}$,${\{3, 1, 2\}}$ 和 ${\{3, 2, 1\}}$。这里是生成 ${S}$ 所有排列的序列的一种方案：对于 ${S}$ 里的每个 ${x}$,递归地生成 ${S - x}$ 的所有排列的序列[^7],而后将 ${x}$ 加到每个序列的前面。这样就能对 ${S}$ 里的每个 ${x}$,产生出了 ${S}$ 的所有以 ${x}$ 开头的排列。将对所有 ${x}$ 的序列组合起来，就可以得到 ${S}$ 的所有排列。

```
(define (permutations s)
  (if (null? s)                    ; empty set?
      (list nil)                   ; sequence containing empty set
      (flatmap (lambda (x)
                 (map (lambda (p) (cons x p))
                      (permutations (remove x s))))
               s)))
```

Notice how this strategy reduces the problem of generating permutations of S to the problem of generating the permutations of sets with fewer elements than ${S}$. In the terminal case, we work our way down to the empty list, which represents a set of no elements. For this, we generate `(list nil)`, which is a sequence with one item, namely the set with no elements. The `remove` procedure used in `permutations` returns all the items in a given sequence except for a given item. This can be expressed as a simple filter:<br>
请注意这里所用的策略，看看它如何将生成S的所有排列的问题，归结为生成元素少于 ${S}$ 的集合的所有排列的问题。在终极情况中我们将达到空表，它表示没有元素的集合。对此我们生成出的就是 `(list nil)`,这是一个只包含一个元素的序列，其中是一个没有元素的集合。在`permutations`过程中所用的remove过程返回除指定项之外的所有元素，它可以简单地用一个过滤器表示：

```
(define (remove item sequence)
  (filter (lambda (x) (not (= x item)))
          sequence))
```

<div id="Exercise2.40" markdown>

Exercise 2.40.  Define a procedure `unique-pairs` that, given an integer ${n}$, generates the sequence of pairs ${(i, j)}$ with ${1 \le j < i \le n}$. Use `unique-pairs` to simplify the definition of `prime-sum-pairs` given above.<br>
练习2.40请定义过程`unique-pairs`,给它整数 ${n}$,它产生出序对 ${(i, j)}$,其中 ${1 \le j < i \le n}$。请用`unique-pairs`去简化上面`prime-sum-pairs`的定义。

</div>

<div id="Exercise2.41" markdown>

Exercise 2.41.  Write a procedure to find all ordered triples of distinct positive integers ${i}$, ${j}$, and ${k}$ less than or equal to a given integer ${n}$ that sum to a given integer ${s}$.<br>
练习2.41  请写出一个过程，它能产生出所有小于等于给定整数 ${n}$ 的正的相异整数 ${i}$、${j}$ 和 ${k}$ 的有序三元组，使每个三元组的三个元之和等于给定的整数 ${s}$。

</div>

<div id="Exercise2.42" markdown>

Exercise 2.42.  

<div id="Figure2.8" markdown>

<figure markdown>
  ![](../assets/Fig2.8.svg)
  <figcaption>Figure 2.8:  A solution to the eight-queens puzzle.</figcaption>
</figure>

</div>

The ''eight-queens puzzle'' asks how to place eight queens on a chessboard so that no queen is in check from any other (i.e., no two queens are in the same row, column, or diagonal). One possible solution is shown in figure [2.8](#Figure2.8). One way to solve the puzzle is to work across the board, placing a queen in each column. Once we have placed ${k - 1}$ queens, we must place the $k^{\mathrm{th}}$ queen in a position where it does not check any of the queens already on the board. We can formulate this approach recursively: Assume that we have already generated the sequence of all possible ways to place ${k - 1}$ queens in the first ${k - 1}$ columns of the board. For each of these ways, generate an extended set of positions by placing a queen in each row of the $k^{\mathrm{th}}$ column. Now filter these, keeping only the positions for which the queen in the $k^{\mathrm{th}}$ column is safe with respect to the other queens. This produces the sequence of all ways to place k queens in the first ${k}$ columns. By continuing this process, we will produce not only one solution, but all solutions to the puzzle.<br>
练习2.42  “八皇后谜题”问的是怎样将八个皇后摆在国际象棋盘上，使得任意一个皇后都不能攻击另一个皇后(也就是说，任意两个皇后都不在同一行、同一列或者同一对角线上)。一个可能的解如图[2.8](#Figure2.8)所示。解决这一谜题的一种方法按一个方向处理棋盘，每次在每一列里放一个皇后。如果现在已经放好了 ${k - 1}$ 个皇后，第 ${k}$ 个皇后就必须放在不会被已在棋盘上的任何皇后攻击的位置上。我们可以递归地描述这一过程：假定我们已经生成了在棋盘的前 ${k - 1}$ 列中放置 ${k - 1}$ 个皇后的所有可能方式，现在需要的就是对于其中的每种方式，生成出将下一个皇后放在第 ${k}$ 列中每一行的扩充集合。而后过滤它们，只留下能使位于第 ${k}$ 列的皇后与其他皇后相安无事的那些扩充。这样就能产生出将 ${k}$ 个皇后放置在前 ${k}$ 列的所有格局的序列。继续这一过程，我们将能产生出这一谜题的所有解，而不是一个解。

We implement this solution as a procedure `queens`, which returns a sequence of all solutions to the problem of placing ${n}$ queens on an ${n \times n}$ chessboard. `Queens` has an internal procedure `queen-cols` that returns the sequence of all ways to place queens in the first ${k}$ columns of the board.<br>
将这一解法实现为一个过程`queens`,令它返回在 ${n \times n}$ 棋盘上放 ${n}$ 个皇后的所有解的序列。`queens`内部的过程`queen-cols`,返回在棋盘的前k列中放皇后的所有格局的序列。

```
(define (queens board-size)
  (define (queen-cols k)  
    (if (= k 0)
        (list empty-board)
        (filter
         (lambda (positions) (safe? k positions))
         (flatmap
          (lambda (rest-of-queens)
            (map (lambda (new-row)
                   (adjoin-position new-row k rest-of-queens))
                 (enumerate-interval 1 board-size)))
          (queen-cols (- k 1))))))
  (queen-cols board-size))
```

In this procedure `rest-of-queens` is a way to place ${k - 1}$ queens in the first ${k - 1}$ columns, and `new-row` is a proposed row in which to place the queen for the $k^{\mathrm{th}}$ column. Complete the program by implementing the representation for sets of board positions, including the procedure `adjoin-position`, which adjoins a new row-column position to a set of positions, and `empty-board`, which represents an empty set of positions. You must also write the procedure `safe?`, which determines for a set of positions, whether the queen in the kth column is safe with respect to the others. (Note that we need only check whether the new queen is safe -- the other queens are already guaranteed safe with respect to each other.)<br>
这个过程里的`rest-of-queens`是在前 ${k - 1}$ 列放置 ${k - 1}$ 个皇后的一种方式，`new-row`是在第k列放置所考虑的行编号。请完成这一程序，为此需要实现一种棋盘格局集合的表示方式；还要实现过程`adjoin-position`,它将一个新的行列格局加入一个格局集合；`empty-board`,它表示空的格局集合。你还需要写出过程`safe?`,它能确定在一个格局中，在第k列的皇后相对于其他列的皇后是否为安全的(请注意，我们只需检查新皇后是否安全——其他皇后已经保证相安无事了)。

</div>

<div id="Exercise2.43" markdown>

Exercise 2.43.  Louis Reasoner is having a terrible time doing exercise [2.42](#Exercise2.42). His `queens` procedure seems to work, but it runs extremely slowly. (Louis never does manage to wait long enough for it to solve even the ${6\times6}$ case.) When Louis asks Eva Lu Ator for help, she points out that he has interchanged the order of the nested mappings in the `flatmap`, writing it as<br>
练习2.43 Louis Reasoner在做练习[2.42](#Exercise2.42)时遇到了麻烦，他的`queens`过程看起来能行，但却运行得极慢(Louis居然无法忍耐到它解出 ${6\times6} 棋盘的问题)。当Louis请Eva Lu Ator帮忙时，她指出他在`flatmap`里交换了嵌套映射的顺序，将它写成了：

```
(flatmap
 (lambda (new-row)
   (map (lambda (rest-of-queens)
          (adjoin-position new-row k rest-of-queens))
        (queen-cols (- k 1))))
 (enumerate-interval 1 board-size))
```

Explain why this interchange makes the program run slowly. Estimate how long it will take Louis's program to solve the eight-queens puzzle, assuming that the program in exercise [2.42](#Exercise2.42) solves the puzzle in time ${T}$.<br>
请解释一下，为什么这样交换顺序会使程序运行得非常慢。估计一下，用Louis的程序去解决八皇后问题大约需要多少时间，假定练习[2.42](#Exercise2.42)中的程序需用时间 ${T}$ 求解这一难题。

</div>

[^1]: 
    This is, in fact, precisely the `fringe` procedure from exercise [2.28]. Here we've renamed it to emphasize that it is part of a family of general sequence-manipulation procedures.<br>
    这实际上就是练[2.28]的过程`fringe`,在这里给它另取一个名字，是为了强调它是一般性的序列操控过程族的一个组成部分。

[^2]:
    Richard Waters (1979) developed a program that automatically analyzes traditional Fortran programs, viewing them in terms of maps, filters, and accumulations. He found that fully 90 percent of the code in the Fortran Scientific Subroutine Package fits neatly into this paradigm. One of the reasons for the success of Lisp as a programming language is that lists provide a standard medium for expressing ordered collections so that they can be manipulated using higher-order operations. The programming language APL owes much of its power and appeal to a similar choice. In APL all data are represented as arrays, and there is a universal and convenient set of generic operators for all sorts of array operations.<br>
    Richard Waters(1979)开发了一个能自动分析传统的Fortan程序，并用映射、过滤器和累积器的观点去观察它们的程序。他发现，在Fortan Scienifie Subrouine Package (Fortan科学计算子程序包)里，足足有90%的代码可以很好地纳入这一风范之中。作为程序设计语言，Lisp取得成功的一个原因，就在于它用表作为表述有序汇集的一种标准媒介，并使它们可以通过高阶操作来处理。程序设计语言APL的威力和形式也来自另一种类似选择。在APL里，所有的数据都是数组，而且为各种类型的通用数组操作提供了一集具有普遍性，使用方便的运算符。

[^3]:
    According to Knuth (1981), this rule was formulated by W. G. Horner early in the nineteenth century, but the method was actually used by Newton over a hundred years earlier. Horner's rule evaluates the polynomial using fewer additions and multiplications than does the straightforward method of first computing ${a_n x^n}$, then adding ${a_{n-1}x^{n-1}}$, and so on. In fact, it is possible to prove that any algorithm for evaluating arbitrary polynomials must use at least as many additions and multiplications as does Horner's rule, and thus Horner's rule is an optimal algorithm for polynomial evaluation. This was proved (for the number of additions) by A. M. Ostrowski in a 1954 paper that essentially founded the modern study of optimal algorithms. The analogous statement for multiplications was proved by V. Y. Pan in 1966. The book by Borodin and Munro (1975) provides an overview of these and other results about optimal algorithms.<br>
    根据Knuth(1981),这一规则是W.G.Horner在19世纪早期提出的，但这一方法在100多年前就已经被牛顿实际使用了。Horner规则在求值多项式时所用的加法和乘法次数少于直接方法，即那种先计算出 ${a_n x^n}$,而后加上 ${a_{n-1}x^{n-1}}$，并这样做下去的方法。事实上，可以证明，任何多项式的求值算法至少需要做Hormer规则那么多次加法和乘法，因此，Horner规则就是多项式求值的最优算法。这一论断由A.M.Ostrowski在1954年的一篇文章中(对加法)证明，这也是现代最优算法研究的开创性工作。关于乘法的类似论断由V.Y.Pan在1966年证明。Borodin和Munro的著作(1975)里有对这些工作的概述和有关最优算法的其他一些结果。

[^4]:
    This definition uses the extended version of `map` described in footnote [12].<br>
    这一定义里使用了脚注[12]中描述的扩充的map。

[^5]:
    This approach to nested mappings was shown to us by David Turner, whose languages KRC and Miranda provide elegant formalisms for dealing with these constructs. The examples in this section (see also exercise [2.42]) are adapted from Turner 1981. In section [3.5.3], we'll see how this approach generalizes to infinite sequences.<br>
    有关嵌套映射的这种方式是由David Tumer展现给我们的，他的语言KRC和Miranda为处理这些结构提供了很优美的形式。本节里的例子(也请看练习[2.42])取自Turner 1981。[3.5.3]节还要将这一途径推广到处理无穷序列的情况。

[^6]:
    We're representing a pair here as a list of two elements rather than as a Lisp pair. Thus, the ''pair'' (i,j) is represented as `(list i j)`, not `(cons i j)`.<br>
    这里的序对被表示为两个元素的表，没有直接采用Lisp的序对，因此，这里所谓的“序对”(i,j)就是`(list i j)`,而不是`(cons i j)`.

[^7]:
    The set ${S - x}$ is the set of all elements of ${S}$, excluding ${x}$.<br>
    集合 ${S - x}$ 里包括了集合 ${S}$ 中除 ${x}$ 之外的所有元素。

[^8]:
    Semicolons in Scheme code are used to introduce comments. Everything from the semicolon to the end of the line is ignored by the interpreter. In this book we don't use many comments; we try to make our programs self-documenting by using descriptive names.<br>
    在Scheme代码中，分号用于引进注释。从分号开始直至行尾的所有东西都将被解释器忽略掉。在本书里使用的注释并不多，我们主要是希望通过使用有意义的名字使程序具有自解释性。

[1.3]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_3/

[2.2.2]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_2/2.2.2

[2.2.1]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_2/2.2.1

[2.28]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_2/2.2.2/#Exercise2.28

[3.5]: {{ config.extra.homepage_sicp }}/chapter_3/chapter_3_5/

[12]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_2/2.2.4/#Footnote12

[2.18]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_2/2.2.1/#Exercise2.18

[2.42]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_2/2.2.3/#Exercise2.42

[3.5.3]: {{ config.extra.homepage_sicp }}/chapter_3/chapter_3_5/3.5.3