---
comments: true
---

# 2.2.1  Representing Sequences

<div id="Figure2.4" markdown>

<figure markdown>
  ![](../assets/Fig2.4.svg)
  <figcaption>Figure 2.4:  The sequence 1, 2, 3, 4 represented as a chain of pairs.</figcaption>
</figure>

</div>

One of the useful structures we can build with pairs is a *sequence* -- an ordered collection of data objects. There are, of course, many ways to represent sequences in terms of pairs. One particularly straightforward representation is illustrated in figure [2.4](#Figure2.4), where the sequence 1, 2, 3, 4 is represented as a chain of pairs. The `car` of each pair is the corresponding item in the chain, and the `cdr` of the pair is the next pair in the chain. The `cdr` of the final pair signals the end of the sequence by pointing to a distinguished value that is not a pair, represented in box-and-pointer diagrams as a diagonal line and in programs as the value of the variable `nil`. The entire sequence is constructed by nested `cons` operations:<br>
利用序对可以构造出的一类有用结构是序列——一批数据对象的一种有序汇集。显然，采用序对表示序列的方式很多，一种最直接的表示方式如[2.4](#Figure2.4)所示，其中用一个序对的链条表示出序列1,2,3,4,在这里，每个序对的`car`部分对应于这个链中的条目，`cdr`则是链中下一个序对。最后的一个序对的`cdr`用一个能辨明不是序对的值表示，标明序列的结束，在盒子指针图中用一条对角线表示，在程序里用变量`nil`的值。整个序列可以通过嵌套的`cons`操作构造起来：

```
(cons 1
      (cons 2
            (cons 3
                  (cons 4 nil))))
```

Such a sequence of pairs, formed by nested `cons`es, is called a *list*, and Scheme provides a primitive called `list` to help in constructing lists.[^1] The above sequence could be produced by `(list 1 2 3 4)`. In general,<br>
通过嵌套的`cons`形成的这样一个序对的序列称为一个表，Scheme为方便表的构造，提供了一个基本操作`list`[^1],上面序列也可以通过`(list 1234)`产生。一般说：

```
(list <a1> <a2> ... <an>)
```

is equivalent to<br>
等价于

```
(cons <a1> (cons <a2> (cons ... (cons <an> nil) ...)))
```

Lisp systems conventionally print lists by printing the sequence of elements, enclosed in parentheses. Thus, the data object in figure [2.4](#Figure2.4) is printed as `(1 2 3 4)`:<br>
Lisp系统通常用元素序列的形式打印出表，外面用括号括起。按照这种方式，图[2.4](#Figure2.4)里的数据对象就将打印为`(1234)`:

```
(define one-through-four (list 1 2 3 4))

one-through-four
(1 2 3 4)
```

Be careful not to confuse the expression `(list 1 2 3 4)` with the list `(1 2 3 4)`, which is the result obtained when the expression is evaluated. Attempting to evaluate the expression `(1 2 3 4)` will signal an error when the interpreter tries to apply the procedure `1` to arguments `2, 3`, and `4`.<br>
请当心，不要将表达式`(list 1234)`和表`(1234)`搞混了。后面这个表是对前面表达式求值得到的结果。如果想去求值表达式`(1234)`,解释器就会试图将过程`1`应用于参数`2`、`3`和`4`,这时会发出一个出错信号。

We can think of `car` as selecting the first item in the list, and of `cdr` as selecting the sublist consisting of all but the first item. Nested applications of `car` and `cdr` can be used to extract the second, third, and subsequent items in the list.[^2] The constructor `cons` makes a list like the original one, but with an additional item at the beginning.<br>
我们可以将`car`看作选取表的第一项的操作，将`cdr`看作是选取表中除去第一项之后剩下的所有项形成的子表。`car`和`cdr`的嵌套应用可以取出一个表里的第二、第三以及后面的各项[^2]。构造符`cons`可用于构造表，它在原有的表前面增加一个元素：

```
(car one-through-four)
1

(cdr one-through-four)
(2 3 4)
(car (cdr one-through-four))
2

(cons 10 one-through-four)
(10 1 2 3 4)

(cons 5 one-through-four)
(5 1 2 3 4)
```

The value of `nil`, used to terminate the chain of pairs, can be thought of as a sequence of no elements, the *empty list*. The word *nil* is a contraction of the Latin word *nihil*, which means ''nothing.''[^3]<br>
nil的值用于表示序对的链结束，它也可以当作一个不包含任何元素的序列，空表。单词“nil”是拉丁词汇“nihil”的缩写，这个拉丁词汇表示“什么也没有”[^3]。

## List operations(表操作)
The use of pairs to represent sequences of elements as lists is accompanied by conventional programming techniques for manipulating lists by successively ''cdring down'' the lists. For example, the procedure `list-ref` takes as arguments a list and a number ${n}$ and returns the $n^{\mathrm{th}}$ item of the list. It is customary to number the elements of the list beginning with 0. The method for computing `list-ref` is the following:<br>
利用序对将元素的序列表示为表之后，我们就可以使用常规的程序设计技术，通过顺序“向下cdr”表的方式完成对表的各种操作了。例如，下面的过程`list-ref`的实际参数是一个表和一个数n,它返回这个表中的第n个项。这里人们习惯令表元素的编号从0开始。计算`list-ref`的方法如下：


- For n = 0, `list-ref` should return the `car` of the list.<br>
对n=0,`list-ref`应返回表的`car`。

- Otherwise, `list-ref` should return the ${(n - 1)}$ st item of the `cdr` of the list.<br>
否则，`list-ref`返回表的`cdr`的第 ${(n - 1)}$ 个项。

```
(define (list-ref items n)
  (if (= n 0)
      (car items)
      (list-ref (cdr items) (- n 1))))
(define squares (list 1 4 9 16 25))

(list-ref squares 3)
16
```

Often we `cdr` down the whole list. To aid in this, Scheme includes a primitive predicate `null?`, which tests whether its argument is the empty list. The procedure `length`, which returns the number of items in a list, illustrates this typical pattern of use:<br>
我们经常要向下`cdr`整个的表，为了帮助做好这件事，Scheme包含一个基本操作`nul1?`,用于检查参数是不是空表。返回表中项数的过程`length`可以说明这一典型应用模式：

```
(define (length items)
  (if (null? items)
      0
      (+ 1 (length (cdr items)))))
(define odds (list 1 3 5 7))

(length odds)
4
```

The `length` procedure implements a simple recursive plan. The reduction step is:<br>
过程`length`实现一种简单的递归方案，其中的递归步骤是：

- The `length` of any list is 1 plus the `length` of the `cdr` of the list.<br>
任意一个表的length就是这个表的cdr的length加一。<br>
This is applied successively until we reach the base case:<br>
顺序地这样应用，直至达到了基础情况：
- The `length` of the empty list is 0.<br>
空表的length是0。

We could also compute length in an iterative style:<br>
我们也可以用一种迭代方式来计算length:

```
(define (length items)
  (define (length-iter a count)
    (if (null? a)
        count
        (length-iter (cdr a) (+ 1 count))))
  (length-iter items 0))
```

Another conventional programming technique is to ''cons up'' an answer list while `cdr`ing down a list, as in the procedure `append`, which takes two lists as arguments and combines their elements to make a new list:<br>
另一常用程序设计技术是在向下`cdr`一个表的过程中“向上cons”出一个结果表，例如过程`append`,它以两个表为参数，用它们的元素组合成一个新表：

```
(append squares odds)
(1 4 9 16 25 1 3 5 7)

(append odds squares)
(1 3 5 7 1 4 9 16 25)
```

`Append` is also implemented using a recursive plan. To `append` lists `list1` and `list2`, do the following:<br>
`append`也是用一种递归方案实现的。要得到表`list1`和`list2`的`append`,按如下方式做：

- If `list1` is the empty list, then the result is just `list2`.<br>
如果list1是空表，结果就是list2。

- Otherwise, `append` the `cdr` of `list1` and `list2`, and `cons` the `car` of `list1` onto the result:<br>
否则应先做出`list1`的`cdr`和`list2`的`append`,而后再将`list1`的`car`通过`cons`加到结果的前面：

```
(define (append list1 list2)
  (if (null? list1)
      list2
      (cons (car list1) (append (cdr list1) list2))))
```

<div id="Exercise2.17" markdown>

Exercise 2.17.  Define a procedure last-pair that returns the list that contains only the last element of a given (nonempty) list:<br>
练习2.17  请定义出过程`last-pair`,它返回只包含给定(非空)表里最后一个元素的表：

```
(last-pair (list 23 72 149 34))
(34)
```

</div>

<div id="Exercise2.18" markdown>

Exercise 2.18.  Define a procedure `reverse` that takes a list as argument and returns a list of the same elements in reverse order:<br>
练习2.18  请定义出过程`reverse`,它以一个表为参数，返回的表中所包含的元素与参数表相同，但排列顺序与参数表相反：

```
(reverse (list 1 4 9 16 25))
(25 16 9 4 1)
```

</div>

<div id="Exercise2.19" markdown>

Exercise 2.19.  Consider the change-counting program of section [1.2.2]. It would be nice to be able to easily change the currency used by the program, so that we could compute the number of ways to change a British pound, for example. As the program is written, the knowledge of the currency is distributed partly into the procedure `first-denomination` and partly into the procedure `count-change` (which knows that there are five kinds of U.S. coins). It would be nicer to be able to supply a list of coins to be used for making change.<br>
练习2.19  请考虑[1.2.2]节的兑换零钱方式计数程序。如果能够轻而易举地改变程序里所用的兑换币种就更好了。譬如说，那样我们就能计算出1英镑的不同兑换方式的数目。在写前面那个程序时，有关币种的知识中有一部分出现在过程`first-denomination`里，另一部分出现在过程里`count-change`(它知道有5种U.S.硬币)。如果能够用一个表来提供可用于兑换的硬币就更好了。

We want to rewrite the procedure `cc` so that its second argument is a list of the values of the coins to use rather than an integer specifying which coins to use. We could then have lists that defined each kind of currency:<br>
我们希望重写出过程`cc`,使其第二个参数是一个可用硬币的币值表，而不是一个指定可用硬币种类的整数。而后我们就可以针对各种货币定义出一些表：

```
(define us-coins (list 50 25 10 5 1))
(define uk-coins (list 100 50 20 10 5 2 1 0.5))
```

We could then call `cc` as follows:<br>
然后我们就可以通过如下方式调用`cc`:

```
(cc 100 us-coins)
292
```

To do this will require changing the program `cc` somewhat. It will still have the same form, but it will access its second argument differently, as follows:<br>
为了做到这件事，我们需要对程序`cc`做一些修改。它仍然具有同样的形式，但将以不同的方
式访问自己的第二个参数，如下面所示：

```
(define (cc amount coin-values)
  (cond ((= amount 0) 1)
        ((or (< amount 0) (no-more? coin-values)) 0)
        (else
         (+ (cc amount
                (except-first-denomination coin-values))
            (cc (- amount
                   (first-denomination coin-values))
                coin-values)))))
```

Define the procedures `first-denomination`, `except-first-denomination`, and `no-more?` in terms of primitive operations on list structures. Does the order of the list `coin-values` affect the answer produced by `cc?` Why or why not?<br>
请基于表结构上的基本操作，定义出过程`first-denomination`、`except-first-denomination`和`no-more?`。表`coin-values`的排列顺序会影响`cc`给出的回答吗?为什么?

</div>

<div id="Exercise2.20" markdown>

Exercise 2.20.  The procedures `+`, `*`, and `list` take arbitrary numbers of arguments. One way to define such procedures is to use `define` with *dotted-tail notation*. In a procedure definition, a parameter list that has a dot before the last parameter name indicates that, when the procedure is called, the initial parameters (if any) will have as values the initial arguments, as usual, but the final parameter's value will be a *list* of any remaining arguments. For instance, given the definition<br>
练习2.20  过程`+`、`*`和`list`可以取任意个数的实际参数。定义这类过程的一种方式是采用一种带点尾部记法形式的`define`。在一个过程定义中，如果在形式参数表的最后一个参数之前有一个点号，那就表明，当这一过程被实际调用时，前面各个形式参数(如果有的话)将以前面的各个实际参数为值，与平常一样。但最后一个形式参数将以所有剩下的实际参数的表为值。例如，假若我们定义了：

```
(define (f x y . z) <body>)
```

the procedure `f` can be called with two or more arguments. If we evaluate<br>
过程`f`就可以用两个以上的参数调用。如果求值：

```
(f 1 2 3 4 5 6)
```

then in the body of `f`, `x` will be 1, `y` will be 2, and `z` will be the list `(3 4 5 6)`. Given the definition<br>
那么在`f`的体里，`x`将是1,`y`将是2,而`z`将是表`(3 4 5 6)`。给了定义：

```
(define (g . w) <body>)
```

the procedure `g` can be called with zero or more arguments. If we evaluate<br>
过程`g`可以用0个或多个参数调用。如果求值：

```
(g 1 2 3 4 5 6)
```

then in the body of `g`, `w` will be the list `(1 2 3 4 5 6)`.[^4]<br>
那么在`g`的体里，`w`将是表`(1 2 3 4 5 6)`[^4]

Use this notation to write a procedure `same-parity` that takes one or more integers and returns a list of all the arguments that have the same even-odd parity as the first argument. For example,<br>
请采用这种记法形式写出过程`same-parity`,它以一个或者多个整数为参数，返回所有
与其第一个参数有着同样奇偶性的参数形成的表。例如：

```
(same-parity 1 2 3 4 5 6 7)
(1 3 5 7)

(same-parity 2 3 4 5 6 7)
(2 4 6)
```

</div>

## Mapping over lists(对表的映射)
One extremely useful operation is to apply some transformation to each element in a list and generate the list of results. For instance, the following procedure scales each number in a list by a given factor:<br>
一个特别有用的操作是将某种变换应用于一个表的所有元素，得到所有结果构成的表。举例来说，下面过程将一个表里的所有元素按给定因子做一次缩放：

```
(define (scale-list items factor)
  (if (null? items)
      nil
      (cons (* (car items) factor)
            (scale-list (cdr items) factor))))
(scale-list (list 1 2 3 4 5) 10)
(10 20 30 40 50)
```

We can abstract this general idea and capture it as a common pattern expressed as a higher-order procedure, just as in section [1.3]. The higher-order procedure here is called `map. Map` takes as arguments a procedure of one argument and a list, and returns a list of the results produced by applying the procedure to each element in the list:[^5]<br>
我们可以抽象出这一具有一般性的想法，将其中的公共模式表述为一个高阶过程，就像
[1.3]节里所做的那样。这一高阶过程称为`map`,它有一个过程参数和一个表参数，返回将这一过程应用于表中各个元素得到的结果形成的表[^5]。

```
(define (map proc items)
  (if (null? items)
      nil
      (cons (proc (car items))
            (map proc (cdr items)))))
(map abs (list -10 2.5 -11.6 17))
(10 2.5 11.6 17)
(map (lambda (x) (* x x))
     (list 1 2 3 4))
(1 4 9 16)
```

Now we can give a new definition of `scale-list` in terms of `map`:<br>
现在我们可以用`map`给出`scale-list`的一个新定义：

```
(define (scale-list items factor)
  (map (lambda (x) (* x factor))
       items))
```

`Map` is an important construct, not only because it captures a common pattern, but because it establishes a higher level of abstraction in dealing with lists. In the original definition of `scale-list`, the recursive structure of the program draws attention to the element-by-element processing of the list. Defining `scale-list` in terms of `map` suppresses that level of detail and emphasizes that scaling transforms a list of elements to a list of results. The difference between the two definitions is not that the computer is performing a different process (it isn't) but that we think about the process differently. In effect, `map` helps establish an abstraction barrier that isolates the implementation of procedures that transform lists from the details of how the elements of the list are extracted and combined. Like the barriers shown in figure [2.1], this abstraction gives us the flexibility to change the low-level details of how sequences are implemented, while preserving the conceptual framework of operations that transform sequences to sequences. Section [2.2.3] expands on this use of sequences as a framework for organizing programs.<br>
`map`是一种很重要的结构，不仅因为它代表了一种公共模式，而且因为它建立起了一种处理表的高层抽象。在`scale-list`原来的定义里，程序的递归结构将人的注意力吸引到对于表中逐个元素的处理上。通过`map`定义`scale-list`抑制了这种细节层面上的情况，强调的是从元素表到结果表的一个缩放变换。这两种定义形式之间的差异，并不在于计算机会执行不同的计算过程(其实不会),而在于我们对这同一个过程的不同思考方式。从作用上看，`map`帮我们建起了一层抽象屏障，将实现表变换的过程的实现，与如何提取表中元素以及组合结果的细节隔离开。与图[2.1]里所示的屏障类似，这种抽象也提供了新的灵活性，使我们有可能在保持从序列到序列的变换操作框架的同时，改变序列实现的低层细节。[2.2.3]节将把序列的这种使用方式扩展为一种组织程序的框架。


<div id="Exercise2.21" markdown>

Exercise 2.21.  The procedure `square-list` takes a list of numbers as argument and returns a list of the squares of those numbers.<br>
练习2.21  过程`square-list`以一个数值表为参数，返回每个数的平方构成的表：

```
(square-list (list 1 2 3 4))
(1 4 9 16)
```

Here are two different definitions of `square-list`. Complete both of them by filling in the missing expressions:<br>
下面是`square-list`的两个定义，请填充其中缺少的表达式以完成它们：

```
(define (square-list items)
  (if (null? items)
      nil
      (cons <??> <??>)))
(define (square-list items)
  (map <??> <??>))
```

</div>

<div id="Exercise2.22" markdown>

Exercise 2.22.  Louis Reasoner tries to rewrite the first `square-list` procedure of exercise [2.21](#Exercise2.21) so that it evolves an iterative process:<br>
练习2.22 Louis Reasoner试图重写练习[2.21](#Exercise2.21)的第一个`square-list`过程，希望使它能生成一个迭代计算过程：

```
(define (square-list items)
  (define (iter things answer)
    (if (null? things)
        answer
        (iter (cdr things) 
              (cons (square (car things))
                    answer))))
  (iter items nil))
```

Unfortunately, defining `square-list` this way produces the answer list in the reverse order of the one desired. Why?<br>
但是很不幸，在按这种方式定义出的`square-list`产生出的结果表中，元素的顺序正好与我们所需要的相反。为什么?

Louis then tries to fix his bug by interchanging the arguments to `cons`:<br>
Louis又试着修正其程序，交换了`cons`的参数：

```
(define (square-list items)
  (define (iter things answer)
    (if (null? things)
        answer
        (iter (cdr things)
              (cons answer
                    (square (car things))))))
  (iter items nil))
```

This doesn't work either. Explain.<br>
但还是不行。请解释为什么。

</div>

<div id="Exercise2.23" markdown>

Exercise 2.23.  The procedure `for-each` is similar to `map`. It takes as arguments a procedure and a list of elements. However, rather than forming a list of the results, `for-each` just applies the procedure to each of the elements in turn, from left to right. The values returned by applying the procedure to the elements are not used at all `-- for-each` is used with procedures that perform an action, such as printing. For example,<br>
练习2.23  过程`for-each`与`map`类似，它以一个过程和一个元素表为参数，但它并不返回结果的表，只是将这一过程从左到右应用于各个元素，将过程应用于元素得到的值都丢掉不用。`for-each`通常用于那些执行了某些动作的过程，如打印等。看下面例子：

```
(for-each (lambda (x) (newline) (display x))
          (list 57 321 88))
57
321
88
```

The value returned by the call to `for-each` (not illustrated above) can be something arbitrary, such as true. Give an implementation of `for-each`.<br>
由`for-each`的调用返回的值(上面没有显示)可以是某种任意的东西，例如逻辑值真。请
给出一个`for-each`的实现。

</div>

[^1]:
    In this book, we use list to mean a chain of pairs terminated by the end-of-list marker. In contrast, the term `list structure` refers to any data structure made out of pairs, not just to lists.<br>
    在这本书里，我们用术语表专指那些有表尾结束标记的序对的链。与此相对应，用术语表结构指所有的由序对构造起来的数据结构，而不仅是表。

[^2]:
    Since nested applications of `car` and `cdr` are cumbersome to write, Lisp dialects provide abbreviations for them -- for instance,<br>
    因为嵌套地应用`car`和`cdr`也会感到很麻烦，所以许多Lisp方言都提供了它们的缩写形式，例如：

    ```
    (cadr ⟨arg⟩) = (car (cdr ⟨arg⟩))
    ```

    The names of all such procedures start with `c` and end with `r`. Each `a` between them stands for a `car` operation and each `d` for a `cdr` operation, to be applied in the same order in which they appear in the name. The names `car` and `cdr` persist because simple combinations like `cadr` are pronounceable.<br>
    所有这类过程的名字都以`c`开头，以工结束，其中每个`a`表示一个`car`操作，每个`d`表示一个`cdr`操作，按照它们在名字中出现的顺序应用。读`car`和`cdr`的方式则继续保留，因为像`cadr`这样的简单组合还是可以发音的。

[^3]:
    It’s remarkable how much energy in the standardization of Lisp dialects has been dissipated in arguments that are literally over nothing: Should nil be an ordinary name? Should the value of nil be a symbol? Should it be a list? Should it be a pair? In Scheme, nil is an ordinary name, which we use in this section as a variable whose value is the end-of-list marker (just as true is an ordinary variable that has a true value). Other dialects of Lisp, including Common Lisp, treat nil as a special symbol. The authors of this book, who have endured too many language standardization brawls, would like to avoid the entire issue. Once we have introduced quotation in [2.3], we will denote the empty list as '() and dispense with the variable nil entirely.<br>
    值得提出的是，在Lisp方言的标准化方面，人们已经令人气馁地将许许多多精力花在一些毫无意义的字面问题的争论上：nil应该是个普通的名字吗?nil的值应该算是一个符号吗?它应该算是一个表吗?它应该算一个序对吗?在Scheme里nil是个普通的名字，在本节里被我们用作一个变量，其值就是表尾标记(正如true是个普通变量，具有真的值一样)。Lisp的其他方言，包括Common Lisp,都将nil作为一个特殊符号，本书的作者参加过许多语言标准化方面的无益口角，真是希望完全避免这些东西。到[2.3]节引进了引号之后，我们就将一直用'()表示空表，完全抛弃变量nil。


[^4]:
    To define `f` and `g` using `lambda` we would write<br>
    用`lambda`方式定义`f`和`g`,应该写：
    ```
    (define f (lambda (x y . z) <body>))
    (define g (lambda w <body>))
    ```
[^5]:
    Scheme standardly provides a `map` procedure that is more general than the one described here. This more general `map` takes a procedure of n arguments, together with n lists, and applies the procedure to all the first elements of the lists, all the second elements of the lists, and so on, returning a list of the results. For example:<br>
    Scheme标准提供了一个`map`过程，它比这里描述的过程更具一般性。这个更一般的`map`以一个取个参数的过程和n个表为参数，将这个过程应用于所有表的第一个元素，而后应用于它们的第二个元素，如此下去，最后返回所有结果的表。例如：

    ```
    (map + (list 1 2 3) (list 40 50 60) (list 700 800 900))
    (741 852 963)

    (map (lambda (x y) (+ x (* 2 y)))
        (list 1 2 3)
        (list 4 5 6))
    (9 12 15)
    ```

[1.2.2]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_2/1.2.2/

[1.3]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_3/

[2.1]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_1/2.1.2/#Figure2.1

[2.2.3]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_3/2.2.3

[2.3]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_3/