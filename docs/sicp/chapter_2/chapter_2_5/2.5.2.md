---
comments: true
---

# 2.5.2  Combining Data of Different Types(不同类型数据的组合)
We have seen how to define a unified arithmetic system that encompasses ordinary numbers, complex numbers, rational numbers, and any other type of number we might decide to invent, but we have ignored an important issue. The operations we have defined so far treat the different data types as being completely independent. Thus, there are separate packages for adding, say, two ordinary numbers, or two complex numbers. What we have not yet considered is the fact that it is meaningful to define operations that cross the type boundaries, such as the addition of a complex number to an ordinary number. We have gone to great pains to introduce barriers between parts of our programs so that they can be developed and understood separately. We would like to introduce the cross-type operations in some carefully controlled way, so that we can support them without seriously violating our module boundaries.<br />
前面已经看到了如何定义出一个统一的算术系统，其中包含常规的数、复数和有理数，以及我们希望发明的任何其他数值类型。但在那里也忽略了一个重要的问题。我们至今定义的所有运算，都把不同数据类型看作相互完全分离的东西，也就是说，这里有几个完全分离的程序包，它们分别完成两个常规的数，或者两个复数的加法。我们至今还没有考虑的问题是下面事实：定义出能够跨过类型界限的操作也很有意义，譬如完成一个复数和一个常规数的加法。在前面，我们一直煞费苦心地在程序的各个部分之间引进了屏障，以使它们能够分别开发和分别理解。现在却又要引进跨类型的操作。当然，我们必须以一种经过精心考虑的可控方式去做这件事情，以使我们在支持这种操作的同时又没有严重地损害模块间的分界。

One way to handle cross-type operations is to design a different procedure for each possible combination of types for which the operation is valid. For example, we could extend the complex-number package so that it provides a procedure for adding complex numbers to ordinary numbers and installs this in the table using the tag `(complex scheme-number)`:[^1]<br />
处理跨类型操作的一种方式，就是为每一种类型组合的合法运算设计一个特定过程。例如，我们可以扩充复数包，使它能提供一个过程用于加起一个复数和一个常规的数，并用标志`(complex scheme-number)`将它安装到表格里[^1]:

```
;; to be included in the complex package
(define (add-complex-to-schemenum z x)
  (make-from-real-imag (+ (real-part z) x)
                       (imag-part z)))
(put 'add '(complex scheme-number)
     (lambda (z x) (tag (add-complex-to-schemenum z x))))
```

This technique works, but it is cumbersome. With such a system, the cost of introducing a new type is not just the construction of the package of procedures for that type but also the construction and installation of the procedures that implement the cross-type operations. This can easily be much more code than is needed to define the operations on the type itself. The method also undermines our ability to combine separate packages additively, or least to limit the extent to which the implementors of the individual packages need to take account of other packages. For instance, in the example above, it seems reasonable that handling mixed operations on complex numbers and ordinary numbers should be the responsibility of the complex-number package. Combining rational numbers and complex numbers, however, might be done by the complex package, by the rational package, or by some third package that uses operations extracted from these two packages. Formulating coherent policies on the division of responsibility among packages can be an overwhelming task in designing systems with many packages and many cross-type operations.<br />
这一技术确实可以用，但也非常麻烦。对于这样的一个系统，引进一个新类型的代价就不仅仅需要构造出针对这一类型的所有过程的包，还需要构造并安装好所有实现跨类型操作的过程。后一件事所需要的代码很容易就会超过定义类型本身所需的那些操作。这种方法也损害了以添加方式组合独立开发的程序包的能力，至少给独立程序包的实现者增加了一些限制，要求他们在对独立程序包工作时，必须同时关注其他的程序包。比如，在上面例子里，如果要处理复数和常规数的混合运算，将其看作复数包的责任是合理的。然而，有关有理数和复数的组合工作却存在许多选择，完全可以由复数包、有理数包，或者由另外的，使用了从前面两个包中取出的操作的第三个包完成。在设计包含许多程序包和许多跨类型操作的系统时，要想规划好一套统一的策略，分清各种包之间的责任，很容易变成非常复杂的任务。

## Coercion(强制)
In the general situation of completely unrelated operations acting on completely unrelated types, implementing explicit cross-type operations, cumbersome though it may be, is the best that one can hope for. Fortunately, we can usually do better by taking advantage of additional structure that may be latent in our type system. Often the different data types are not completely independent, and there may be ways by which objects of one type may be viewed as being of another type. This process is called coercion. For example, if we are asked to arithmetically combine an ordinary number with a complex number, we can view the ordinary number as a complex number whose imaginary part is zero. This transforms the problem to that of combining two complex numbers, which can be handled in the ordinary way by the complex-arithmetic package.<br />
最一般的情况是需要处理针对一批完全无关的类型的一批完全无关的操作，直接实现跨类型操作很可能就是解决问题的最好方式了，当然，这样做起来确实比较麻烦。幸运的是，我们常常可以利用潜藏在类型系统之中的一些额外结构，将事情做得更好些。不同的数据类型通常都不是完全相互无关的，常常存在一些方式，使我们可以把一种类型的对象看作另一种类型的对象。这种过程就称为强制。举例来说，如果现在需要做常规数值与复数的混合算术，我们就可以将常规数值看成是虚部为0的复数。这样就把问题转换为两个复数的运算问题，可以由复数包以正常的方式处理了。

In general, we can implement this idea by designing coercion procedures that transform an object of one type into an equivalent object of another type. Here is a typical coercion procedure, which transforms a given ordinary number to a complex number with that real part and zero imaginary part:<br />
一般而言，要实现这一想法，我们可以设计出一些强制过程，它们能把一个类型的对象转换到另一类型的等价对象。下面是一个典型的强制过程，它将给定的常规数值转换为一个复数，其中的实部为原来的数而虚部是0:

```
(define (scheme-number->complex n)
  (make-complex-from-real-imag (contents n) 0))
```

We install these coercion procedures in a special coercion table, indexed under the names of the two types:<br />
我们将这些强制过程安装到一个特殊的强制表格中，用两个类型的名字作为索引：

```
(put-coercion 'scheme-number 'complex scheme-number->complex)
```

(We assume that there are `put-coercion` and `get-coercion` procedures available for manipulating this table.) Generally some of the slots in the table will be empty, because it is not generally possible to coerce an arbitrary data object of each type into all other types. For example, there is no way to coerce an arbitrary complex number to an ordinary number, so there will be no general `complex->scheme-number` procedure included in the table.<br />
(这里假定了存在着用于操纵这个表格的`put-coercion`和`get-coercion`过程。)一般而言，这一表格里的某些格子将是空的，因为将任何数据对象转换到另一个类型并不是都能做的。例如并不存在某种将任意复数转换为常规数值的方式，因此，这个表格中就不应包括一般性的`complex->scheme-number`过程。

Once the coercion table has been set up, we can handle coercion in a uniform manner by modifying the `apply-generic` procedure of section [2.4.3]. When asked to apply an operation, we first check whether the operation is defined for the arguments' types, just as before. If so, we dispatch to the procedure found in the operation-and-type table. Otherwise, we try coercion. For simplicity, we consider only the case where there are two arguments[^2]. We check the coercion table to see if objects of the first type can be coerced to the second type. If so, we coerce the first argument and try the operation again. If objects of the first type cannot in general be coerced to the second type, we try the coercion the other way around to see if there is a way to coerce the second argument to the type of the first argument. Finally, if there is no known way to coerce either type to the other type, we give up. Here is the procedure:<br />
一旦将上述转换表格装配好，我们就可以修改2.4.3节的apply-generic过程，得到一种处理强制的统一方法。在要求应用一个操作时，我们将首先检查是否存在针对实际参数类型的操作定义，就像前面一样。如果存在，那么就将任务分派到由操作-类型表格中找出的相应过程去，否则就去做强制。为了简化讨论，这里只考虑两个参数的情况[^2]。我们检查强制表格，查看其中第一个参数类型的对象能否转换到第二个参数的类型。如果可以，那就对第一个参数做强制后再试验操作。如果第一个参数类型的对象不能强制到第二个类型，那么就试验另一种方式，看看能否从第二个参数的类型转换到第一个参数的类型。最后，如果不存在从一个类型到另一类型的强制，那么就只能放弃了。下面是这个过程：


```
(define (apply-generic op . args)
  (let ((type-tags (map type-tag args)))
    (let ((proc (get op type-tags)))
      (if proc
          (apply proc (map contents args))
          (if (= (length args) 2)
              (let ((type1 (car type-tags))
                    (type2 (cadr type-tags))
                    (a1 (car args))
                    (a2 (cadr args)))
                (let ((t1->t2 (get-coercion type1 type2))
                      (t2->t1 (get-coercion type2 type1)))
                  (cond (t1->t2
                         (apply-generic op (t1->t2 a1) a2))
                        (t2->t1
                         (apply-generic op a1 (t2->t1 a2)))
                        (else
                         (error "No method for these types"
                                (list op type-tags))))))
              (error "No method for these types"
                     (list op type-tags)))))))
```

This coercion scheme has many advantages over the method of defining explicit cross-type operations, as outlined above. Although we still need to write coercion procedures to relate the types (possibly ${n^2}$ procedures for a system with ${n}$ types), we need to write only one procedure for each pair of types rather than a different procedure for each collection of types and each generic operation[^3]. What we are counting on here is the fact that the appropriate transformation between types depends only on the types themselves, not on the operation to be applied.<br />
与显式定义的跨类型操作相比，这种强制模式有许多优越性。就像在上面已经说过的。虽然我们仍然需要写出一些与各种类型有关的强制过程(对于 ${n}$ 个类型的系统可能需要 ${n^2}$ 个过程),但是却只需要为每一对类型写一个过程，而不是为每对类型和每个通用型操作写一个过程[^3]。能够这样做的基础就是，类型之间的适当转换只依赖于类型本身，而不依赖于所实际应用的操作。

On the other hand, there may be applications for which our coercion scheme is not general enough. Even when neither of the objects to be combined can be converted to the type of the other it may still be possible to perform the operation by converting both objects to a third type. In order to deal with such complexity and still preserve modularity in our programs, it is usually necessary to build systems that take advantage of still further structure in the relations among types, as we discuss next.<br />
在另一方面，也可能存在一些应用，对于它们而言我们的强制模式还不足够一般。即使需要运算的两种类型的对象都不能转换到另一种类型，也完全可能在将这两种类型的对象都转换到第三种类型后执行这一运算。为了处理这种复杂性，同时又能维持我们系统的模块性，通常就需要在建立系统时利用类型之间的进一步结构，有关情况见下面的讨论。

## Hierarchies of types(类型的层次结构)
The coercion scheme presented above relied on the existence of natural relations between pairs of types. Often there is more ''global'' structure in how the different types relate to each other. For instance, suppose we are building a generic arithmetic system to handle integers, rational numbers, real numbers, and complex numbers. In such a system, it is quite natural to regard an integer as a special kind of rational number, which is in turn a special kind of real number, which is in turn a special kind of complex number. What we actually have is a so-called *hierarchy of types*, in which, for example, integers are a *subtype* of rational numbers (i.e., any operation that can be applied to a rational number can automatically be applied to an integer). Conversely, we say that rational numbers form a supertype of integers. The particular hierarchy we have here is of a very simple kind, in which each type has at most one supertype and at most one subtype. Such a structure, called a *tower*, is illustrated in figure [2.25].<br />
上面给出的强制模式，依赖于一对对类型之间存在着某种自然的关系。在实际中，还常常存在着不同类型间相互关系的更“全局性”的结构。例如，假定我们想要构造出一个通用型的算术系统，处理整数、有理数、实数、复数。在这样的一个系统里，一种很自然的做法是把整数看作是一类特殊的有理数，而有理数又是一类特殊的实数，实数转而又是一类特殊的复数。这样，我们实际有的就是一个所谓的类型的层次结构，在其中，(例如)整数是有理数的子类型(也就是说，任何可以应用于有理数的操作都可以应用于整数)。与此相对应，人们也说有理数形成了整数的一个超类型。在这个例子里所看到的类型层次结构是最简单的一种，其中一个类型只有至多一个超类型和至多一个子类型。这样的结构称为一个类型塔，如图[2.25]所示。


<div id="Figure2.25" markdown>

<figure markdown>
  ![](../assets/Fig2.25.svg)
  <figcaption>Figure 2.25:  A tower of types.</figcaption>
</figure>
</div>

If we have a tower structure, then we can greatly simplify the problem of adding a new type to the hierarchy, for we need only specify how the new type is embedded in the next supertype above it and how it is the supertype of the type below it. For example, if we want to add an integer to a complex number, we need not explicitly define a special coercion procedure `integer->complex`. Instead, we define how an integer can be transformed into a rational number, how a rational number is transformed into a real number, and how a real number is transformed into a complex number. We then allow the system to transform the integer into a complex number through these steps and then add the two complex numbers.<br />
如果我们面对的是一个塔结构，那么将一个新类型加入层次结构的问题就可能极大地简化了，因为需要做的所有事情，也就是刻画清楚这一新类型将如何嵌入正好位于它之上的超类型，以及它如何作为下面一个类型的超类型。举例说，如果我们希望做一个整数和一个复数的加法，那么并不需要明确定义一个特殊强制函数integer->complex。相反，我们可以定义如何将整数转换到有理数，如何将有理数转换到实数，以及如何将实数转换到复数。而后让系统通过这些步骤将该整数转换到复数，在此之后再做两个复数的加法。

We can redesign our `apply-generic` procedure in the following way: For each type, we need to supply a `raise` procedure, which ''raises'' objects of that type one level in the tower. Then when the system is required to operate on objects of different types it can successively raise the lower types until all the objects are at the same level in the tower. (Exercises [2.83] and  [2.84] concern the details of implementing such a strategy.)<br />
我们可以按照下面的方式重新设计那个`apply-generic`过程。对于每个类型，都需要提供一个raise过程，它将这一类型的对象“提升”到塔中更高一层的类型。此后，当系统遇到需要对两个不同类型的运算时，它就可以逐步提升较低的类型，直至所有对象都达到了塔的同一个层次(练习[2.83]和练习[2.84]关注的就是实现这种策略的一些细节)。

Another advantage of a tower is that we can easily implement the notion that every type ''inherits'' all operations defined on a supertype. For instance, if we do not supply a special procedure for finding the real part of an integer, we should nevertheless expect that `real-part` will be defined for integers by virtue of the fact that integers are a subtype of complex numbers. In a tower, we can arrange for this to happen in a uniform way by modifying `apply-generic`. If the required operation is not directly defined for the type of the object given, we raise the object to its supertype and try again. We thus crawl up the tower, transforming our argument as we go, until we either find a level at which the desired operation can be performed or hit the top (in which case we give up).<br />
类型塔的另一优点，在于使我们很容易实现一种概念：每个类型能够“继承”其超类型中定义的所有操作。举例说，如果我们没有为找出整数的实部提供一个特定过程，但也完全可能期望`real-part`过程对整数有定义，因为事实上整数是复数的一个子类型。对于类型塔的情况，我们可以通过修改`apply-generic`过程，以一种统一的方式安排好这些事情。如果所需操作在给定对象的类型中没有明确定义，那么就将这个对象提升到它的超类型并再次检查。在向塔顶攀登的过程中，我们也不断转换有关的参数，直至在某个层次上找到了所需的操作而后去执行它，或者已经到达了塔顶(此时就只能放弃了)。

Yet another advantage of a tower over a more general hierarchy is that it gives us a simple way to ''lower'' a data object to the simplest representation. For example, if we add ${2 + 3i}$ to ${4 - 3i}$, it would be nice to obtain the answer as the integer 6 rather than as the complex number ${6 + 0i}$. Exercise [2.85] discusses a way to implement such a lowering operation. (The trick is that we need a general way to distinguish those objects that can be lowered, such as ${6 + 0i}$, from those that cannot, such as ${6 + 2i}$.)<br />
与其他层次结构相比，塔形结构的另一优点是它使我们有一种简单的方式去“下降”一个数据对象，使之达到最简单的表示形式。例如，如果现在做了 ${2 + 3i}$ 和 ${4 - 3i}$ 的加法，如果结果是整数6而不是复数 ${6 + 0i}$ 当然就更好了。练习[2.85]讨论了实现这种下降操作的一种方式。这里的技巧在于需要有一种一般性的方式，分辨出哪些是可以下降的对象(例如 ${6 + 0i}$ ),哪些是不能下降的对象(例如 ${6 + 2i}$)。

<div id="Figure2.26" markdown>

<figure markdown>
  ![](../assets/Fig2.26.svg)
  <figcaption>Figure 2.26:  Relations among types of geometric figures.</figcaption>
</figure>
</div>

## Inadequacies of hierarchies(层次结构的不足)
If the data types in our system can be naturally arranged in a tower, this greatly simplifies the problems of dealing with generic operations on different types, as we have seen. Unfortunately, this is usually not the case. Figure 2.26 illustrates a more complex arrangement of mixed types, this one showing relations among different types of geometric figures. We see that, in general, a type may have more than one subtype. Triangles and quadrilaterals, for instance, are both subtypes of polygons. In addition, a type may have more than one supertype. For example, an isosceles right triangle may be regarded either as an isosceles triangle or as a right triangle. This multiple-supertypes issue is particularly thorny, since it means that there is no unique way to ''raise'' a type in the hierarchy. Finding the ''correct'' supertype in which to apply an operation to an object may involve considerable searching through the entire type network on the part of a procedure such as `apply-generic`. Since there generally are multiple subtypes for a type, there is a similar problem in coercing a value ''down'' the type hierarchy. Dealing with large numbers of interrelated types while still preserving modularity in the design of large systems is very difficult, and is an area of much current research.[^4]<br />
如果在一个系统里，有关的数据类型可以自然地安排为一个塔形，那么正如在前面已经看到的，处理不同类型上通用型操作的问题将能得到极大的简化。遗憾的是，事情通常都不是这样。图2-26展示的是类型之间关系的一种更复杂情况，其中显示出的是表示几何图形的各种类型之间的关系。从这个图里可以看到，一般而言，一个类型可能有多于一个子类型，例如三角形和四边形都是多边形的子类型。此外，一个类型也可能有多于一个超类型，例如，等腰直角三角形可以看作是等腰三角形，又可以看作是直角三角形。这种存在多重超类型的问题特别令人棘手，因为这就意味着，并不存在一种唯一方式在层次结构中去“提升”一个类型。当我们需要将一个操作应用于一个对象时，为此而找出“正确”超类型的工作(例如，这就是`apply-generic`这类过程中的一部分)可能涉及到对整个类型网络的大范围搜索。由于一般说一个类型存在着多个子类型，需要在类型层次结构中“下降”一个值时也会遇到类似的问题。在设计大型系统时，处理好一大批相互有关的类型而同时又能保持模块性，这是一个非常困难的问题，也是当前正在继续研究的一个领域[^4]。

<div id="Exercise2.81" markdown>

Exercise 2.81. Louis Reasoner has noticed that `apply-generic` may try to coerce the arguments to each other's type even if they already have the same type. Therefore, he reasons, we need to put procedures in the coercion table to "coerce" arguments of each type to their own type. For example, in addition to the `scheme-number->complex` coercion shown above, he would do:<br />
练习2.81  Louis Reasoner注意到，甚至在两个参数的类型实际相同的情况下，`apply-generic`也可能试图去做参数间的类型强制。由此他推论说，需要在强制表格中加入一些过程，以将每个类型的参数“强制”到它们自己的类型。例如，除了上面给出的`scheme-number->complex`强制之外，他觉得应该有：

```
(define (scheme-number->scheme-number n) n)
(define (complex->complex z) z)
(put-coercion 'scheme-number 'scheme-number
              scheme-number->scheme-number)
(put-coercion 'complex 'complex complex->complex)
```

a. With Louis's coercion procedures installed, what happens if `apply-generic` is called with two arguments of type `scheme-number` or two arguments of type `complex` for an operation that is not found in the table for those types? For example, assume that we've defined a generic exponentiation operation:<br />
a. 如果安装了Louis的强制过程，如果在调用`apply-generic`时各参数的类型都为`scheme-number`或者类型都为`complex`,而在表格中又找不到相应的操作，这时会出现什么情况?例如，假定我们定义了一个通用型的求幂运算：

```
(define (exp x y) (apply-generic 'exp x y))
```

and have put a procedure for exponentiation in the Scheme-number package but not in any other package:<br />
并在Scheme数值包里放入了一个求幂过程，但其他程序包里都没有：

```
;; following added to Scheme-number package
(put 'exp '(scheme-number scheme-number)
     (lambda (x y) (tag (expt x y)))) ; using primitive expt
```

What happens if we call `exp` with two complex numbers as arguments?<br />
如果对两个复数调用`exp`会出现什么情况?

b. Is Louis correct that something had to be done about coercion with arguments of the same type, or does `apply-generic` work correctly as is?<br />
b. Louis真的纠正了有关同样类型参数的强制问题吗?`apply-generic`还能像原来那样正确工作吗?

c. Modify `apply-generic` so that it doesn't try coercion if the two arguments have the same type.<br />
c. 请修改apply-generic,使之不会试着去强制两个同样类型的参数。
</div>

<div id="Exercise2.82" markdown>

Exercise 2.82.  Show how to generalize `apply-generic` to handle coercion in the general case of multiple arguments. One strategy is to attempt to coerce all the arguments to the type of the first argument, then to the type of the second argument, and so on. Give an example of a situation where this strategy (and likewise the two-argument version given above) is not sufficiently general. (Hint: Consider the case where there are some suitable mixed-type operations present in the table that will not be tried.)<br />
练习2.82  请阐述一种方法，设法推广`apply-generic`,以便处理多个参数的一般性情况下的强制问题。一种可能策略是试着将所有参数都强制到第一个参数的类型，而后试著强制到第二个参数的类型，并如此试下去。请给出一个例子说明这种策略还不够一般(就像上面对两个参数的情况给出的例子那样)。(提示：请考虑一些情况，其中表格里某些合用的操作将不会被考虑。)

</div>

<div id="Exercise2.83" markdown>

Exercise 2.83.  Suppose you are designing a generic arithmetic system for dealing with the tower of types shown in figure [2.25]: integer, rational, real, complex. For each type (except complex), design a procedure that raises objects of that type one level in the tower. Show how to install a generic `raise` operation that will work for each type (except complex).<br />
练习2.83  假定你正在设计一个通用型的算术包，处理图[2.25]所示的类型塔，包括整数、有理数、实数和复数。请为每个类型(除复数外)设计一个过程，它能将该类型的对象提升到塔中的上面一层。请说明如何安装一个通用的raise操作，使之能对各个类型工作(除复数之外)。
</div>

<div id="Exercise2.84" markdown>

Exercise 2.84.  Using the `raise` operation of exercise [2.83], modify the `apply-generic` procedure so that it coerces its arguments to have the same type by the method of successive raising, as discussed in this section. You will need to devise a way to test which of two types is higher in the tower. Do this in a manner that is ''compatible'' with the rest of the system and will not lead to problems in adding new levels to the tower.<br />
练习2.84 利用练习[2.83]的raise操作修改`apply-generic`过程，使它能通过逐层提升的方式将参数强制到同样的类型，正如本节中讨论的。你将需要安排一种方式，去检查两个类型中哪个更高。请以一种能与系统中其他部分“相容”,而且又不会影响向塔中加入新层次的方式完成这一工作。
</div>

<div id="Exercise2.85" markdown>

Exercise 2.85.  This section mentioned a method for ''simplifying'' a data object by lowering it in the tower of types as far as possible. Design a procedure `drop` that accomplishes this for the tower described in exercise [2.83]. The key is to decide, in some general way, whether an object can be lowered. For example, the complex number ${1.5 + 0i}$ can be lowered as far as `real`, the complex number ${1 + 0i}$ can be lowered as far as `integer`, and the complex number ${2 + 3i}$ cannot be lowered at all. Here is a plan for determining whether an object can be lowered: Begin by defining a generic operation `project` that ''pushes'' an object down in the tower. For example, projecting a complex number would involve throwing away the imaginary part. Then a number can be dropped if, when we `project` it and `raise` the result back to the type we started with, we end up with something equal to what we started with. Show how to implement this idea in detail, by writing a `drop` procedure that drops an object as far as possible. You will need to design the various projection operations[^5] and install `project` as a generic operation in the system. You will also need to make use of a generic equality predicate, such as described in exercise [2.79]. Finally, use `drop` to rewrite `apply-generic` from exercise [2.84] so that it ''simplifies'' its answers.<br />
练习2.85  本节中提到了“简化”数据对象表示的一种方法，就是使之在类型塔中尽可能地下降。请设计一个过程`drop(下落)`,使它能在如练习[2.83]所描述的类型塔中完成这一工作。这里的关键是以某种一般性的方式，判断一个数据对象能否下降。举例来说，复数 ${1.5 + 0i}$ 至多可以下降到`real`,复数 ${1 + 0i}$ 至多可以下降到`integer`,而复数 ${2 + 3i}$ 就根本无法下降。现在提出一种确定一个对象能否下降的计划：首先定义一个运算`project(投影)`,它将一个对象“压”到塔的下面一层。例如，投影一个复数就是丢掉其虚部。这样，一个数能够向下落，如果我们首先`project`它而后将得到的结果`raise`到开始的类型，最终得到的东西与开始的东西相等。请阐述实现这一想法的具体细节，并写出一个`drop`过程，使它可以将一个对象尽可能地下落。你将需要设计各种各样的投影函数[^5],并需要把`project`安装为系统里的一个通用型操作。你还需要使用一个通用型的相等谓词，例如练习[2.79]所描述的。最后，请利用`drop`重写练习[2.84]的`apply-generic`,使之可以“简化”其结果。

</div>

<div id="Exercise2.86" markdown>

Exercise 2.86.  Suppose we want to handle complex numbers whose real parts, imaginary parts, magnitudes, and angles can be either ordinary numbers, rational numbers, or other numbers we might wish to add to the system. Describe and implement the changes to the system needed to accommodate this. You will have to define operations such as `sine` and `cosine` that are generic over ordinary numbers and rational numbers<br />
练习2.86  假定我们希望处理一些复数，它们的实部、虚部、模和幅角都可以是常规数值、有理数，或者我们希望加入系统的任何其他数值类型。请描述和实现系统需要做的各种修改，以满足这一需要。你应设法将例如`sine`和`cosine`一类的运算也定义为在常规数和有理数上的通用运算。

</div>

[^1]:
    We also have to supply an almost identical procedure to handle the types `(scheme-number complex)`.<br />
    我们还需要另一个几乎相同的过程去处理类型`(scheme-number complex)`。

[^2]:
    See exercise [2.82] for generalizations.<br />
    有关推广见练习[2.82],

[^3]:
    If we are clever, we can usually get by with fewer than ${n^2}$ coercion procedures. For instance, if we know how to convert from type 1 to type 2 and from type 2 to type 3, then we can use this knowledge to convert from type 1 to type 3. This can greatly decrease the number of coercion procedures we need to supply explicitly when we add a new type to the system. If we are willing to build the required amount of sophistication into our system, we can have it search the ''graph'' of relations among types and automatically generate those coercion procedures that can be inferred from the ones that are supplied explicitly.<br />
    如果做得更聪明些，常常不需要写出 ${n^2}$ 那么多个强制过程。例如，如果知道如何从类型1转换到类型2,以及如何从类型2转换到类型3,那么也就可以利用这些知识从类型1转换到类型3。达将大大减少在向系统中加人新类型时需要显式提供的转换过程的个数。如果真的希望，也完全可以将这种复杂方式做到系统里，让系统去查找类型之间的关系“图”,而后自动地通过显式提供的强制过程，生成其他能够推导出的强制过程。

[^4]:
    This statement, which also appears in the first edition of this book, is just as true now as it was when we wrote it twelve years ago. Developing a useful, general framework for expressing the relations among different types of entities (what philosophers call ''ontology'') seems intractably difficult. The main difference between the confusion that existed ten years ago and the confusion that exists now is that now a variety of inadequate ontological theories have been embodied in a plethora of correspondingly inadequate programming languages. For example, much of the complexity of object-oriented programming languages -- and the subtle and confusing differences among contemporary object-oriented languages -- centers on the treatment of generic operations on interrelated types. Our own discussion of computational objects in chapter 3 avoids these issues entirely. Readers familiar with object-oriented programming will notice that we have much to say in chapter 3 about local state, but we do not even mention ''classes'' or ''inheritance.'' In fact, we suspect that these problems cannot be adequately addressed in terms of computer-language design alone, without also drawing on work in knowledge representation and automated reasoning.<br />
    这句话也出现在本书的第1版里，它在现在就像20年前写出时一样的正确。开发出一种有用的，具有一般意义的框架，以描述不同类型的对象之间的关系(这在哲学中称为“本体论”),看来是一件极其困难的工作。在10年前存在的混乱和今天存在的混乱之间的主要差异在于，今天已经有了一批各式各样的并不合适的本体理论，它们已经被嵌入到数量过多而又先天不足的各种程序设计语言里。举例来说，面向对象语言的大部分复杂性——以及当前各种面向对象语言之间细微的而且使人迷惑的差异——的核心，就是对类型之间通用型操作的处理。我们在第3章有关计算性对象的讨论中完全避免了这些问题。熟悉面向对象程序设计的读者将会注意到，在第3章里关于局部状态说了许多东西，但是却根本没有提到“类”或者“继承”。事实上，我们的猜想是，如果没有知识表示和自动推理工作的帮助，这些问题是无法仅仅通过计算机语言设计的方式合理处理的。


[^5]: 
    A real number can be projected to an integer using the round primitive, which returns the closest integer to its argument.<br />
    实数可以用基本过程round投射到整数，它返回最接近参数的整数值。

[2.82]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_5/2.5.2/#Exercise2.82

[2.4.3]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_4/2.4.3/

[2.25]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_5/2.5.2/#Figure2.25

[2.83]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_5/2.5.2/#Exercise2.83

[2.85]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_5/2.5.2/#Exercise2.85

[2.84]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_5/2.5.2/#Exercise2.84

[2.79]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_5/2.5.1/#Exercise2.79

