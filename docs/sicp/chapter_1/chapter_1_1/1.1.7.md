---
comments: true
---

# 1.1.7  Example: Square Roots by Newton's Method（实例：采用牛顿法求平方根）

Procedures, as introduced above, are much like ordinary mathematical functions. They specify a value that is determined by one or more parameters. But there is an important difference between mathematical functions and computer procedures. Procedures must be effective.<br>
上面介绍的过程都很像常规的数学函数，它们描述的是如何根据一个或者几个参数去确定一个值。然而，在数学的函数和计算机的过程之间有一个重要差异，那就是，这一过程还必须是有效可行的。

As a case in point, consider the problem of computing square roots. We can define the square-root function as<br>
作为目前情况下的一个实例，现在我们来考虑求平方根的问题。我们可以将平方根函数定义为：

$$\sqrt{x}\;\; = {\rm\;\; the\;\;} y 
{\rm\;\; such\;\; that\;\;} y \ge 0 {\rm\;\; and\;\;} y^2 = x.$$

This describes a perfectly legitimate mathematical function. We could use it to recognize whether one number is the square root of another, or to derive facts about square roots in general. On the other hand, the definition does not describe a procedure. Indeed, it tells us almost nothing about how to actually find the square root of a given number. It will not help matters to rephrase this definition in pseudo-Lisp:<br>
这就描述出了一个完全正统的数学函数，我们可以利用它去判断某个数是否为另一个数的平方根，或根据上面叙述，推导出一些有关平方根的一般性事实。然而，在另一方面，这一定义并没有描述一个计算过程，因为它确实没有告诉我们，在给定了一个数之后，如何实际地找到这个数的平方根。即使将这个定义用类似Lisp的形式重写一遍也完全无济于事：

```
(define (sqrt x)
  (the y (and (>= y 0)
              (= (square y) x))))
```

This only begs the question.<br>
这只不过是重新提出了原来的问题。

The contrast between function and procedure is a reflection of the general distinction between describing properties of things and describing how to do things, or, as it is sometimes referred to, the distinction between declarative knowledge and imperative knowledge. In mathematics we are usually concerned with declarative (what is) descriptions, whereas in computer science we are usually concerned with imperative (how to) descriptions.<br>
函数与过程之间的矛盾，不过是在描述一件事情的特征，与描述如何去做这件事情之间的普遍性差异的一个具体反映。换一种说法，人们有时也将它说成是说明性的知识与行动性的知识之间的差异。在数学里，人们通常关心的是说明性的描述(是什么)，而在计算机科学里，人们则通常关心行动性的描述(怎么做)。

How does one compute square roots? The most common way is to use Newton's method of successive approximations, which says that whenever we have a guess y for the value of the square root of a number x, we can perform a simple manipulation to get a better guess (one closer to the actual square root) by averaging y with x/y For example, we can compute the square root of 2 as follows. Suppose our initial guess is 1:<br>
计算机如何算出平方根呢？最常用的就是牛顿的逐步逼进方法。这一方法告诉我们，如果对x的平方根的值有了一个猜测y, 那么就可以通过执行一个简单操作去得到一个更好的猜测：只需要求出y和x/y的平均值(它更接近实际的平方根值)。例如，可以用这种方式去计算2的平方根，假定初始值是1:

| Guess  |     Quotient      |           Average            |
| :----: | :---------------: | :--------------------------: |
|   1    |    (2/1)  = 2     |      ((2 + 1)/2)  = 1.5      |
|  1.5   |  (2/1.5)=1.3333   |  ((1.3333 + 1.5)/2)=1.4167   |
| 1.4167 | (2/1.4167)=1.4118 | ((1.4167 + 1.4118)/2)=1.4142 |
| 1.4142 |        ...        |             ...              |


Continuing this process, we obtain better and better approximations to the square root.<br>
继续这一计算过程，我们就能得到对2的平方根的越来越好的近似值。

Now let's formalize the process in terms of procedures. We start with a value for the radicand (the number whose square root we are trying to compute) and a value for the guess. If the guess is good enough for our purposes, we are done; if not, we must repeat the process with an improved guess. We write this basic strategy as a procedure:<br>
现在，让我们设法用过程的语言来描述这一计算过程。开始时，我们有了被开方数的值(现在需要做的就是算出它的平方根)和一个猜测值。如果猜测值已经足够好了，有关工作也就完成了。如若不然，那么就需要重复上述计算过程去改进猜测值。我们可以将这一基本策略写成下面的过程：

```
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x)
                 x)))
```

A guess is improved by averaging it with the quotient of the radicand and the old guess:<br>
改进猜测的方式就是求出它与被开方数除以上一个猜测的平均值：

```
(define (improve guess x)
  (average guess (/ x guess)))
```

where<br>
其中

```
(define (average x y)
  (/ (+ x y) 2))
```

We also have to say what we mean by ``good enough.'' The following will do for illustration, but it is not really a very good test. (See exercise 1.7.) The idea is to improve the answer until it is close enough so that its square differs from the radicand by less than a predetermined tolerance (here 0.001):<br>
我们还必须说明什么叫做“足够好”。下面的做法只是为了说明问题，它确实不是一个很好的检测方法(参见[练习1.7](#Exercise1.7))。这里的想法是，不断改进答案直至它足够接近平方根，使得其平方与被开方数之差小于某个事先确定的误差值(这里用的是0.001)

```
(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))
```

Finally, we need a way to get started. For instance, we can always guess that the square root of any number is 1<br>
最后还需要一种方式来启动整个工作。例如，我们可以总用1作为对任何数的初始猜测值

```
(define (sqrt x)
  (sqrt-iter 1.0 x))
```

If we type these definitions to the interpreter, we can use sqrt just as we can use any procedure:<br>
如果把这些定义都送给解释器，我们就可以使用sqrt了，就像可以使用其他过程一样：

```
(sqrt 9)
3.00009155413138
(sqrt (+ 100 37))
11.704699917758145
(sqrt (+ (sqrt 2) (sqrt 3)))
1.7739279023207892
(square (sqrt 1000))
1000.000369924366
```

The sqrt program also illustrates that the simple procedural language we have introduced so far is sufficient for writing any purely numerical program that one could write in, say, C or Pascal. This might seem surprising, since we have not included in our language any iterative (looping) constructs that direct the computer to do something over and over again. Sqrt-iter, on the other hand, demonstrates how iteration can be accomplished using no special construct other than the ordinary ability to call a procedure<br>
这个sqrt 程序也说明，在用于写纯粹的数值计算程序时，至今已介绍的简单程序设计语言已经足以写出可以在其他语言(例如C或者Pascal)中写出的任何东西了。这看起来很让人吃惊，因为这一语言中甚至还没有包括任何迭代结构(循环),它们用于指挥计算机去一遍遍地做某些事情。而在另一方面，sqrt-iter展示了如何不用特殊的迭代结构来实现迭代，其中只需要使用常规的过程调用能力.

Exercise 1.6.  Alyssa P. Hacker doesn't see why if needs to be provided as a special form. ``Why can't I just define it as an ordinary procedure in terms of cond?'' she asks. Alyssa's friend Eva Lu Ator claims this can indeed be done, and she defines a new version of if:<br>
练习1.6  Alyssa P.Hacker看不出为什么需要将if提供为一种特殊形式，她问：“为什么我不能直接通过cond将它定义为一个常规过程呢?”Alyssa的朋友Eva Lu Ator断言确实可以这样做，并定义了if 的一个新版本：

```
(define (new-if predicate then-clause else-clause)
  (cond (predicate then-clause)
        (else else-clause)))
```

Eva demonstrates the program for Alyssa:<br>
Eva 给Alyssa演示她的程序：

```
(new-if (= 2 3) 0 5)
5

(new-if (= 1 1) 0 5)
0
```

Delighted, Alyssa uses new-if to rewrite the square-root program:<br>
她很高兴地用自己的new-if重写了求平方根的程序：

```
(define (sqrt-iter guess x)
  (new-if (good-enough? guess x)
          guess
          (sqrt-iter (improve guess x)
                     x)))
```

What happens when Alyssa attempts to use this to compute square roots? Explain.<br>
当Alyssa试着用这个过程去计算平方根时会发生什么事情呢?请给出解释。

<div id='Exercise1.7'>
Exercise 1.7.  The good-enough? test used in computing square roots will not be very effective for finding the square roots of very small numbers. Also, in real computers, arithmetic operations are almost always performed with limited precision. This makes our test inadequate for very large numbers. Explain these statements, with examples showing how the test fails for small and large numbers. An alternative strategy for implementing good-enough? is to watch how guess changes from one iteration to the next and to stop when the change is a very small fraction of the guess. Design a square-root procedure that uses this kind of end test. Does this work better for small and large numbers?<br>
练习1.7 对于确定很小的数的平方根而言，在计算平方根中使用的检测good-enough?是很不好的。还有，在现实的计算机里，算术运算总是以一定的有限精度进行的。这也会使我们的检测不适合非常大的数的计算。请解释上述论断，用例子说明对很小和很大的数，这种检测都可能失败。实现good-enough?的另一种策略是监视猜测值在从一次迭代到下一次的变化情况，当改变值相对于猜测值的比率很小时就结束。请设计一个采用这种终止测试方式的平方根过程。对于很大和很小的数，这一方式都能工作吗?
</div>

Exercise 1.8.  Newton's method for cube roots is based on the fact that if y is an approximation to the cube root of x, then a better approximation is given by the value<br>
练习1.8  求立方根的牛顿法基于如下事实，如果y是x的立方根的一个近似值，那么下式将给出一个更好的近似值：

$$
{\frac{x}{y^2} + 2y\over 3}
$$

Use this formula to implement a cube-root procedure analogous to the square-root procedure. (In section [1.3.4] we will see how to implement Newton's method in general as an abstraction of these square-root and cube-root procedures.)<br>
请利用这一公式实现一个类似平方根过程的求立方根的过程。(在[1.3.4]节里，我们将看到如何实现一般性的牛顿法，作为这些求平方根和立方根过程的抽象。)

[1.3.4]: ../chapter_1_3/1.3.4.md

