---
comments: true
---

# 1.1.6 条件表达式和谓词

The expressive power of the class of procedures that we can define at this point is very limited, because we have no way to make tests and to perform different operations depending on the result of a test. For instance, we cannot define a procedure that computes the absolute value of a number by testing whether the number is positive, negative, or zero and taking different actions in the different cases according to the rule</br>
至此我们能定义出的过程类的表达能力还非常有限，因为还没办法去做某些检测，而后依据检测的结果去确定做不同的操作。例如，我们还无法定义一个过程，使它能计算出一个 数的绝对值。完成此事需要先检查一个数是正的、负的或者零，而后依据遇到的不同情况，按照下面规则采取不同的动作：

$$
|x| = \left\{\eqalign{     \hfill   x   &\quad{\rm if}\quad x > 0, \cr
                           \hfill   0   &\quad{\rm if}\quad x = 0, \cr
                                   -x   &\quad{\rm if}\quad x < 0. \cr}\right.
$$

This construct is called a case analysis, and there is a special form in Lisp for notating such a case analysis. It is called cond (which stands for ''conditional''), and it is used as follows:</br>
这种结构称为一个分情况分析，在Lisp里有着一种针对这类分情况分析的特殊形式。称为cond(表示“条件”)，其使用形式如下：

```
(define (abs x)
  (cond ((> x 0) x)
        ((= x 0) 0)
        ((< x 0) (- x))))
```

The general form of a conditional expression is</br>
条件表达式的一般性形式为：

```
(cond (<p1> <e1>)
      (<p2> <e2>)
      ...
      (<pn> <en>))
```

consisting of the symbol cond followed by parenthesized pairs of expressions`(<p> <e>)`called clauses. The first expression in each pair is a predicate -- that is, an expression whose value is interpreted as either true or false</br>
这里首先包含了一个符号cond，在它之后跟着一些称为子句的用括号括起来的表达式对偶`(<p> <e>)`。在每个对偶中的第一个表达式是一个谓词，也就是说，这是一个表达式，它的值将被解释为真或者假[^1]

Conditional expressions are evaluated as follows. The predicate `<p1>` is evaluated first. If its value is false, then `<p2>` is evaluated. If `<p2>`'s value is also false, then `<p3>` is evaluated. This process continues until a predicate is found whose value is true, in which case the interpreter returns the value of the corresponding consequent expression `<e>` of the clause as the value of the conditional expression. If none of the `<p>`'s is found to be true, the value of the cond is undefined.<br>
条件表达式的求值方式如下：首先求值谓词`<p1>`, 如果它的值是false,  那么就去求值`<p2>`, 如果`<p2>`的值是false, 那么就去求值`<p3>`。 这一过程将继续做下去，直到发现了某个谓词的值为真为止。此时解释器就返回相应子句中的序列表达式`<e>`的值，以这个值作为整个条件表达式的值。如果无法找到值为真的`<p>`，cond的值就没有定义。

The word predicate is used for procedures that return true or false, as well as for expressions that evaluate to true or false. The absolute-value procedure abs makes use of the primitive predicates >, <, and =.18 These take two numbers as arguments and test whether the first number is, respectively, greater than, less than, or equal to the second number, returning true or false accordingly.</br>
我们用术语谓词指那些返回真或假的过程，也指那种能求出真或者假的值的表达式。求绝对值的过程abs使用了基本谓词>、<和=18，这几个谓词都以两个数为参数，分别检查第一个数是否大于、小于或者等于第二个数，并据此分别返回真或者假。

Another way to write the absolute-value procedure is</br>
写绝对值函数的另一种方式是：

```
(define (abs x)
  (cond ((< x 0) (- x))
        (else x)))
```

which could be expressed in English as ''If x is less than zero return - x; otherwise return x.'' Else is a special symbol that can be used in place of the `<p>` in the final clause of a cond. This causes the cond to return as its value the value of the corresponding `<e>` whenever all previous clauses have been bypassed. In fact, any expression that always evaluates to a true value could be used as the `<p>` here.</br>
用自然语言来说，就是“如果x小于0就返回-x, 否则就返回x”。else是一个特殊符号，可以用在cond 的最后一个子句中`<p>`的位置，这样做时，如果该cond前面的所有子句都被跳过， 它就会返回最后子句中`<e>`的值。事实上，所有永远都求出真值的表达式都可以用在这个`<p>`的位置上。

Here is yet another way to write the absolute-value procedure:</br>
下面是又一种写绝对值函数的方式：

```
(define (abs x)
  (if (< x 0)
      (- x)
      x))
```

This uses the special form if, a restricted type of conditional that can be used when there are precisely two cases in the case analysis. The general form of an if expression is</br>
这里采用的是特殊形式if，它是条件表达式的一种受限形式，适用于分情况分析中只有两个情况的需要。if表达式的一般形式是：

```
(if <predicate> <consequent> <alternative>)
```

To evaluate an if expression, the interpreter starts by evaluating the `<predicate>` part of the expression. If the `<predicate>` evaluates to a true value, the interpreter then evaluates the `<consequent>` and returns its value. Otherwise it evaluates the `<alternative>` and returns its value.</br>
在求值一个if表达式时，解释器从求值其`<predicate>`部分开始，如果`<predicate>`得到真值，解释器就去求值`<consequent>`并返回其值，否则它就去求值`<alternative>`并返回其值。

In addition to primitive predicates such as <, =, and >, there are logical composition operations, which enable us to construct compound predicates. The three most frequently used are these:</br>
除了一批基本谓词如<、=和>之外，还有一些逻辑复合运算符，利用它们可以构造出各种复合谓词。最常用的三个复合运算符是：

```
(and <e1> ... <en>)
```

The interpreter evaluates the expressions `<e>` one at a time, in left-to-right order. If any `<e>` evaluates to false, the value of the and expression is false, and the rest of the `<e>`'s are not evaluated. If all `<e>`'s evaluate to true values, the value of the and expression is the value of the last one.</br>
解释器将从左到右一个个地求值`<e>`, 如果某个`<e>`求值得到假，这一and表达式的值就是假，后面的那些`<e>`也不再求值了。如果前面所有的`<e>`都求出真值，这一and表达式的值就是最后那个`<e>`的值。

```
(or <e1> ... <en>)
```

The interpreter evaluates the expressions `<e>` one at a time, in left-to-right order. If any `<e>` evaluates to a true value, that value is returned as the value of the or expression, and the rest of the `<e>`'s are not evaluated. If all `<e>`'s evaluate to false, the value of the or expression is false.<br>
解释器将从左到右一个个地求值`<e>`, 如果某个`<e>`求值得到真，or表达式就以这个表达式的值作为值，后面的那些`<e>`也不再求值了。如果所有的`<e>`都求出假值，这一or表达式的值就是假。

```
(not <e>)
```

The value of a not expression is true when the expression <e> evaluates to false, and false otherwise.</br>
如果`<e>`求出的值是假，not表达式的值就是真；否则其值为假。

Notice that and and or are special forms, not procedures, because the subexpressions are not necessarily all evaluated. Not is an ordinary procedure.</br>
注意，and和or都是特殊形式而不是普通的过程，因为它们的子表达式不一定都求值。not则是一个普通的过程。

As an example of how these are used, the condition that a number x be in the range 5 < x < 10 may be expressed as</br>
作为使用这些逻辑复合运算符的例子，数x的值位于区间`5 < x < 10`之中的条件可以写为：

```
(and (> x 5) (< x 10))
```

As another example, we can define a predicate to test whether one number is greater than or equal to another as</br>
作为另一个例子，下面定义了一个谓词，它检测某个数是否大于或者等于另一个数：

```
(define (>= x y)
  (or (> x y) (= x y)))
```

或者也可以定义为：

```
(define (>= x y)
  (not (< x y)))
```

Exercise 1.1.  Below is a sequence of expressions. What is the result printed by the interpreter in response to each expression? Assume that the sequence is to be evaluated in the order in which it is presented.</br>
练习1.1 下面是一系列表达式，对于每个表达式，解释器将输出什么结果?假定这一系列表达式是按照给出的顺序逐个求值的。

```
10
(+ 5 3 4)
(- 9 1)
(/ 6 2)
(+ (* 2 4) (- 4 6))
(define a 3)
(define b (+ a 1))
(+ a b (* a b))
(= a b)
(if (and (> b a) (< b (* a b)))
    b
    a)
(cond ((= a 4) 6)
      ((= b 4) (+ 6 7 a))
      (else 25))
(+ 2 (if (> b a) b a))
(* (cond ((> a b) a)
         ((< a b) b)
         (else -1))
   (+ a 1))
```

Exercise 1.2.  Translate the following expression into prefix form</br>
练习1.2 请将下面表达式变换为前缀形式：

$${5 + 4 + (2 - (3 - (6 + {4\over5})))\over3(6 - 2)(2 - 7)}\,.$$

Exercise 1.3.  Define a procedure that takes three numbers as arguments and returns the sum of the squares of the two larger numbers.</br>
练习1.3 请定义一个过程，它以三个数为参数，返回其中较大的两个数之和。

Exercise 1.4.  Observe that our model of evaluation allows for combinations whose operators are compound expressions. Use this observation to describe the behavior of the following procedure:</br>
练习1.4 请仔细考察上面给出的允许运算符为复合表达式的组合式的求值模型，根据对这一模型的认识描述下面过程的行为：

```
(define (a-plus-abs-b a b)
  ((if (> b 0) + -) a b))
```

Exercise 1.5.  Ben Bitdiddle has invented a test to determine whether the interpreter he is faced with is using applicative-order evaluation or normal-order evaluation. He defines the following two procedures:</br>
练习1.5 Ben Bitdiddle发明了一种检测方法，能够确定解释器究竟采用哪种序求值，是采用应用序，还是采用正则序。他定义了下面两个过程：

```
(define (p) (p))

(define (test x y)
  (if (= x 0)
      0
      y))
```

而后他求值下面的表达式：

```
(test 0 (p))
```

What behavior will Ben observe with an interpreter that uses applicative-order evaluation? What behavior will he observe with an interpreter that uses normal-order evaluation? Explain your answer. (Assume that the evaluation rule for the special form if is the same whether the interpreter is using normal or applicative order: The predicate expression is evaluated first, and the result determines whether to evaluate the consequent or the alternative expression.)</br>
如果某个解释器采用的是应用序求值，Ben会看到什么样的情况？如果解释器采用正则序求值，他又会看到什么情况？请对你的回答做出解释。(无论采用正则序或者应用序，假定特殊形式 if 的求值规则总是一样的。其中的谓词部分先行求值，根据其结果确定随后求值的子表达式部分。)

[^1]: ''Interpreted as either true or false'' means this: In Scheme, there are two distinguished values that are denoted by the constants #t and #f. When the interpreter checks a predicate's value, it interprets #f as false. Any other value is treated as true. (Thus, providing #t is logically unnecessary, but it is convenient.) In this book we will use names true and false, which are associated with the values #t and #f respectively.</br>
“解释为真或者假”的意思如下：在Scheme里存在这两个特殊的值，它们分别用常量#t和#f表示。当解释器检查一个谓词的值时，它将#f解释为假，而将所有其它的值都作为真（这样，提供#t在逻辑上就是不必要的，只是为了方便）。在本书中将使用true和false, 令它们分别关联于#t和#f。
