---
comments: true
---

# 1.1.4  复合过程

我们已经看到了Lisp里的某些元素，它们必然也会出现在任何一种强有力的程序设计语言里。这些东西包括：

- 数和算术运算是基本的数据和过程。

- 组合式的嵌套提供了一种组织起多个操作的方法。

- 定义是一种受限的抽象手段，它为名字关联相应的值。

现在我们来学习过程定义，这是一种威力更加强大的抽象技术，通过它可以为复合操作提供名字，而后就可以将这样的操作作为一个单元使用了。

现在我们要考察如何表述 **“平方”** 的想法。我们可能想说 **“求某个东西的平方，就是用它自身去乘以它自身"**。在这个语言里，这件事情应该表述为：

```
(define (square x) (* x x))
```

可以按如下方式理解这一描述:

```
(define (square  x)        (*         x     x))
```

| (    | define |  (   | square |    x     |  )   |  (   |  *   |  x   |   x    |  )   |  )   |
| ---- | :----: | :--: | :----: | :------: | :--: | :--: | :--: | :--: | :----: | :--: | :--: |
|      |   去   |      |  平方  | 某个东西 |      |      | 乘以 |  它  | 它自身 |      |      |
                                  
 
这样我们就有了一个复合过程，给它取的名字是square。这一过程表示的是将一个东西乘以它自身的操作。被乘的东西也给定了一个局部名字x, 它扮演着与自然语言里代词同样的角色。求值这一定义的结果是创建起一个复合过程，并将它关联于名字 **square**

过程定义的一般形式是：

```
(define (<name> <formal parameters>) <body>)
```

其中 **<name>** 是一个符号，过程定义将在环境中关联于这个符号。 **<formal parameters> (形式参数)** 是一些名字，它们用在过程体中，用于表示过程应用时与它们对应的各个实际参数  **<body>** 是一个表达式，在应用这一过程时，这一表达式中的形式参数将用与之对应的实际参 数取代，对这样取代后的表达式的求值，产生出这个过程应用的值。**<name>** 和 **<formal parameters>** 被放在一对括号里，成为一组，就像实际调用被定义过程时的写法。

定义好square  之后，我们就可以使用它了：

```
(square 21)
441

(square (+ 2 5))
49

(square (square 3))
81
```

我们还可以用 **square** 作为基本构件去定义其他过程。例如， x^2+y^2 可以表述为：

```
(+ (square x) (square y))
```

现在我们很容易定义一个过程sum-of-squares,给它两个数作为实际参数，让它产生这两个数的平方和：

```
(define (sum-of-squares x y)
  (+ (square x) (square y)))

(sum-of-squares 3 4)
25
```

现在我们又可以用sum-of-squares作为构件，进一步去构造其他过程：

```
(define (f a)
  (sum-of-squares (+ a 1) (* a 2)))

(f 5)
136
```

复合过程的使用方式与基本过程完全一样。实际上，如果人们只看上面 **sum-of-squares** 的定义，根本就无法分辨出 **square** 究竟是(像 **+** 和 **\*** 那样)直接做在解释器里呢，还是被定义为一个复合过程。