---
comments: true
---

# 1.2.2 Tree Recursion(树形递归)
Another common pattern of computation is called tree recursion. As an example, consider computing the sequence of Fibonacci numbers, in which each number is the sum of the preceding two:<br>
另一种常见计算模式称为树形递归。作为例子，现在考虑斐波那契 (Fibonacci) 数序列的计算，这一序列中的每个数都是前面两个数之和：

```
0, 1, 1, 2, 3, 5, 8, 13, 21, ...
```

In general, the Fibonacci numbers can be defined by the rule<br>
一般说，斐波那契数由下面规则定义：

$$ {\rm Fib}(n) = 
\cases{	0 &if $\quad n=0$, \cr
	1 &if $\quad n=1$, \cr
	{\rm Fib}(n-1) + {\rm Fib}(n-2) \quad &otherwise. \cr} $$

We can immediately translate this definition into a recursive procedure for computing Fibonacci numbers:<br>
我们马上就可以将这个定义翻译为一个计算斐波那契数的递归过程：

```
(define (fib n)
  (cond ((= n 0) 0)
        ((= n 1) 1)
        (else (+ (fib (- n 1))
                 (fib (- n 2))))))
```

Consider the pattern of this computation. To compute (fib 5), we compute (fib 4) and (fib 3). To compute (fib 4), we compute (fib 3) and (fib 2). In general, the evolved process looks like a tree, as shown in Figure 1.5. Notice that the branches split into two at each level (except at the bottom); this reflects the fact that the fib procedure calls itself twice each time it is invoked.<br>
考虑这一计算的模式。为了计算(fib 5),我们需要计算出(fib 4)和(fib 3)。而为了计算(fib 4),又需要计算(fib 3)和(fib 2)。一般而言，这一展开过程看起来像一棵树，如图1-5所示。请注意，这里的每层分裂为两个分支(除了最下面),反映出对fib过程的每个调用中两次递归调用自身的事实。

<figure markdown>
  ![](../assets/figure_1.5.svg)
  <figcaption>Figure 1.5: The tree-recursive process generated in computing (fib 5).</figcaption>
  <figcaption>图1-5 计算(fib 5)中产生的树形递归计算过程.</figcaption>
</figure>


This procedure is instructive as a prototypical tree recursion, but it is a terrible way to compute Fibonacci numbers because it does so much redundant computation. Notice in Figure 1.5 that the entire computation of (fib 3)—almost half the work—is duplicated. In fact, it is not hard to show that the number of times the procedure will compute (fib 1) or (fib 0) (the number of leaves in the above tree, in general) is precisely Fib(n+1). To get an idea of how bad this is, one can show that the value of Fib(n) grows exponentially with n. More precisely (see Exercise 1.13), Fib(n) is the closest integer to ${\varphi^n \big/ \sqrt{5}}$, where<br>
上面过程作为典型的树形递归具有教育意义，但它却是一种很糟的计算斐波那契数的方法，因为它做了太多的冗余计算。在图1-5中，求 (fib 3) 差不多是这里的一半工作，这计算整个地重复做了两次。事实上，不难证明，在这一过程中，计算 (fib 1) 和 (fib 0) 的次数(一般说，也就是上面树里树叶的个数)正好是Fib(n+1) 。 要领会这种情况有多么糟 糕，我们可以证明Fib(n)值的增长相对于n是指数的。更准确地说(见练习1.13),Fib(n)就是最接近 ${\varphi^n \big/ \sqrt{5}}$ 的整数，其中：

$$\varphi = {1 + \sqrt{5}\over2} \approx 1.6180 $$

is the golden ratio, which satisfies the equation<br>
就是黄金分割的值，它满足方程：

$$\varphi^2 = \varphi + 1. $$

Thus, the process uses a number of steps that grows exponentially with the input. On the other hand, the space required grows only linearly with the input, because we need keep track only of which nodes are above us in the tree at any point in the computation. In general, the number of steps required by a tree-recursive process will be proportional to the number of nodes in the tree, while the space required will be proportional to the maximum depth of the tree.<br>
这样，该过程所用的计算步骤数将随着输入增长而指数性地增长。在另一方面，其空间需求只是随着输入增长而线性增长，因为，在计算中的每一点，我们都只需保存树中在此之上的结点的轨迹。 一般说，树形递归计算过程里所需的步骤数将正比于树中的结点数，其空间需求正比于树的最大深度。

We can also formulate an iterative process for computing the Fibonacci numbers. The idea is to use a pair of integers a and b, initialized to Fib(1) = 1 and Fib(0) = 0, and to repeatedly apply the simultaneous transformations<br>
我们也可以规划出一种计算斐波那契数的迭代计算过程，其基本想法就是用一对整数a和b, 将它们分别初始化为Fib(1)=1和Fib(0)=0, 而后反复地同时使用下面变换规则：

$$\eqalign{a &\gets a + b, \cr 
b &\gets a. \cr}$$

It is not hard to show that, after applying this transformation n times, a and b will be equal, respectively, to Fib(n+1) and Fib(n). Thus, we can compute Fibonacci numbers iteratively using the procedure<br>
不难证明，在n次应用了这些变换后，a和b将分别等于Fib(n+1)和Fib(n)。因此，我们可以用下面过程，以迭代方式计算斐波那契数：

```
(define (fib n) 
  (fib-iter 1 0 n))

(define (fib-iter a b count)
  (if (= count 0)
      b
      (fib-iter (+ a b) a (- count 1))))
```

This second method for computing Fib(n) is a linear iteration. The difference in number of steps required by the two methods—one linear in n, one growing as fast as Fib(n) itself—is enormous, even for small inputs.<br>
计算Fib(n)的这种方法是一个线性迭代。这两种方法在计算中所需的步骤上差异巨大 —— 后一方法相对于n为线性的，前一个的增长像Fib(n)一样快，即使不大的输入也可能造成很大的差异。

One should not conclude from this that tree-recursive processes are useless. When we consider processes that operate on hierarchically structured data rather than numbers, we will find that tree recursion is a natural and powerful tool.32 But even in numerical operations, tree-recursive processes can be useful in helping us to understand and design programs. For instance, although the first fib procedure is much less efficient than the second one, it is more straightforward, being little more than a translation into Lisp of the definition of the Fibonacci sequence. To formulate the iterative algorithm required noticing that the computation could be recast as an iteration with three state variables.<br>
但是我们也不应做出结论，说树形递归计算过程根本没有用。当我们考虑的是在层次结构性的数据上操作，而不是对数操作时，将会发现树形递归计算过程是一种自然的、威力强大的工具。即使是对于数的计算，树形递归计算过程也可能帮助我们理解和设计程序。以计算斐波那契数的程序为例，虽然第一个fib过程远比第二个低效，但它却更加直截了当，基本上就是将斐波那契序列的定义直接翻译为Lisp语言。而要规划出那个迭代过程，则需要注意到，这一计算过程可以重新塑造为一个采用三个状态变量的迭代。

## Example: Counting change(实例：换零钱方式的统计)
It takes only a bit of cleverness to come up with the iterative Fibonacci algorithm. In contrast, consider the following problem: How many different ways can we make change of $1.00, given half-dollars, quarters, dimes, nickels, and pennies? More generally, can we write a procedure to compute the number of ways to change any given amount of money?<br>
要想得到一个迭代的斐波那契算法需要一点点智慧。与此相对应，现在考虑下面的问题：给了半美元、四分之一美元、10美分、5美分和1美分的硬币，将1美元换成零钱，一共有多少种不同方式? 更一般的问题是，给定了任意数量的现金，我们能写出一个程序，计算出所有换零钱方式的种数吗?

This problem has a simple solution as a recursive procedure. Suppose we think of the types of coins available as arranged in some order. Then the following relation holds:<br>
采用递归过程，这一问题有一种很简单的解法。假定我们所考虑的可用硬币类型种类排了某种顺序，于是就有下面的关系：

The number of ways to change amount a using n kinds of coins equals<br>
将总数为a的现金换成n种硬币的不同方式的数目等于

- the number of ways to change amount a using all but the first kind of coin, plus<br>
将现金数a换成除第一种硬币之外的所有其他硬币的不同方式数目，加上 

- the number of ways to change amount a−d using all n kinds of coins, where d is the denomination of the first kind of coin.<br>
将现金数a-d换成所有种类的硬币的不同方式数目，其中的d是第一种硬币的币值。

To see why this is true, observe that the ways to make change can be divided into two groups: those that do not use any of the first kind of coin, and those that do. Therefore, the total number of ways to make change for some amount is equal to the number of ways to make change for the amount without using any of the first kind of coin, plus the number of ways to make change assuming that we do use the first kind of coin. But the latter number is equal to the number of ways to make change for the amount that remains after using a coin of the first kind.<br>
要问为什么这一说法是对的，请注意这里将换零钱分成两组时所采用的方式，第一组里都没有使用第一种硬币，而第二组里都使用了第一种硬币。显然，换成零钱的全部方式的数目，就等于完全不用第一种硬币的方式的数目，加上用了第一种硬币的换零钱方式的数目。而后一个数目也就等于去掉一个第一种硬币值后，剩下的现金数的换零钱方式数目。

Thus, we can recursively reduce the problem of changing a given amount to the problem of changing smaller amounts using fewer kinds of coins. Consider this reduction rule carefully, and convince yourself that we can use it to describe an algorithm if we specify the following degenerate cases:<br>
这样就可以将某个给定现金数的换零钱方式的问题，递归地归约为对更少现金数或者更少种类硬币的同一个问题。仔细考虑上面的归约规则，设法使你确信，如果采用下面方式处理退化情况，我们就能利用上面规则写出一个算法来:

- If a is exactly 0, we should count that as 1 way to make change.<br>
如果a就是0,应该算作是有1种换零钱的方式。 

- If a is less than 0, we should count that as 0 ways to make change.<br>
如果a小于0,应该算作是有0种换零钱的方式。 

- If n is 0, we should count that as 0 ways to make change.<br>
如果n是0,应该算作是有0种换零钱的方式。

We can easily translate this description into a recursive procedure:
我们很容易将这些描述翻译为一个递归过程：

```
(define (count-change amount)
  (cc amount 5))

(define (cc amount kinds-of-coins)
  (cond ((= amount 0) 1)
        ((or (< amount 0) 
             (= kinds-of-coins 0)) 
         0)
        (else 
         (+ (cc amount (- kinds-of-coins 1))
            (cc (- amount (first-denomination 
                           kinds-of-coins))
                kinds-of-coins)))))

(define (first-denomination kinds-of-coins)
  (cond ((= kinds-of-coins 1) 1)
        ((= kinds-of-coins 2) 5)
        ((= kinds-of-coins 3) 10)
        ((= kinds-of-coins 4) 25)
        ((= kinds-of-coins 5) 50)))
```

(The first-denomination procedure takes as input the number of kinds of coins available and returns the denomination of the first kind. Here we are thinking of the coins as arranged in order from largest to smallest, but any order would do as well.) We can now answer our original question about changing a dollar:<br>
(过程first-denomination以可用的硬币种数作为输入，返回第一种硬币的币值。这里认为硬币已经从最大到最小排列好了，其实采用任何顺序都可以)。我们现在就能回答开始的问题了，下面是换1美元硬币的不同方式数目：

```
(count-change 100)
292
```

Count-change generates a tree-recursive process with redundancies similar to those in our first implementation of fib. (It will take quite a while for that 292 to be computed.) On the other hand, it is not obvious how to design a better algorithm for computing the result, and we leave this problem as a challenge. The observation that a tree-recursive process may be highly inefficient but often easy to specify and understand has led people to propose that one could get the best of both worlds by designing a “smart compiler” that could transform tree-recursive procedures into more efficient procedures that compute the same result<br>
count-change产生出一个树形的递归计算过程，其中的冗余计算与前面fib的第一种实现类似(它计算出292需要一点时间)。在另一方面，要想设计出一个更好的算法，使之能算出同样结果，就不那么明显了。我们将这一 问题留给读者作为一个挑战。人们认识到，树形递归计算过程有可能极其低效，但常常很容易描述和理解，这就导致人们提出了一个建议：希望能利用世界上的这两个最好的东西。人们希望能设计出一种“灵巧编译器”,使之能将一个树形递归的过程翻译为一个能计算出同样结果的更有效的过程

Exercise 1.11: A function f is defined by the rule that f(n)=n if n<3 and f(n)=f(n−1)+2f(n−2)+3f(n−3) if n≥3. Write a procedure that computes f by means of a recursive process. Write a procedure that computes f by means of an iterative process.<br>
练习1.11 函数f由如下的规则定义：如果n<3,  那么f(n)=n; 如果n≥3, 那么f(n)=f(n-1)+2f(n-2)+3f(n-3)。请写一个采用递归计算过程计算f 的过程。再写一个采用迭代计算过程计算f的过程。

Exercise 1.12: The following pattern of numbers is called Pascal’s triangle.<br>
练习1.12 下面数值模式称为帕斯卡三角形：

```
         1
       1   1
     1   2   1
   1   3   3   1
 1   4   6   4   1
       . . .
```
The numbers at the edge of the triangle are all 1, and each number inside the triangle is the sum of the two numbers above it.35 Write a procedure that computes elements of Pascal’s triangle by means of a recursive process.<br>
三角形边界上的数都是1,内部的每个数是位于它上面的两个数之和。请写一个过程，它采用递归计算过程计算出帕斯卡三角形。

Exercise 1.13: Prove that Fib(n) is the closest integer to ${\varphi^n \big/ \sqrt{5}}$, where ${\varphi = (1 + \sqrt{5}) \big/ 2}.$  Hint: Let ${\psi = (1 - \sqrt{5}) \big/ 2}$. Use induction and the definition of the Fibonacci numbers (see 1.2.2) to prove that Fib(n) = ${(\varphi^n - \psi^n) \big/ \sqrt{5}}$.<br>
练习 1.13: 证明 Fib(n) 是最接近 ${\varphi^n \big/ \sqrt{5}}$ 的整数, 其中 ${\varphi = (1 + \sqrt{5}) \big/ 2}.$  提示: 利用归纳法和斐波那契数的定义（见1.2.2节），证明Fib(n) = ${(\varphi^n - \psi^n) \big/ \sqrt{5}}$.