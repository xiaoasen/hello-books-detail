---
comments: true
---

# 1.2.4 Exponentiation

Consider the problem of computing the exponential of a given number. We would like a procedure that takes as arguments a base ${b}$ and a positive integer exponent ${n}$ and computes ${b^n}$. One way to do this is via the recursive definition<br>
现在考虑对一个给定的数计算乘幂的问题，我们希望这一过程的参数是一个基数 ${b}$ 和一个整数的指数 ${n}$ 以及计算出 ${b^n}$. 做这件事的一种方式是通过下面这个递归定义： 

$$\eqalign{b^n &= b\cdot b^{n-1}, \cr 
b^0 &= 1, \cr} $$

which translates readily into the procedure<br>
它可以直接翻译为如下过程：

```
(define (expt b n)
  (if (= n 0) 
      1 
      (* b (expt b (- n 1)))))
```

This is a linear recursive process, which requires ${\Theta(n)}$ steps and ${\Theta(n)}$ space. Just as with factorial, we can readily formulate an equivalent linear iteration:<br>
这是一个线性的递归计算过程，需要 ${\Theta(n)}$ 步和 ${\Theta(n)}$ 空间。就像阶乘一样，我们很容易将其形式化为一个等价的线性迭代：

```
(define (expt b n) 
  (expt-iter b n 1))

(define (expt-iter b counter product)
  (if (= counter 0)
      product
      (expt-iter b
                 (- counter 1)
                 (* b product))))
```

This version requires ${\Theta(n)}$ steps and ${\Theta(1)}$ space.<br>
这一版需要 ${\Theta(n)}$ 步和 ${\Theta(1)}$ 空间。

We can compute exponentials in fewer steps by using successive squaring. For instance, rather than computing ${b^8}$ as<br>
我们可以通过连续求平方，以更少的步骤完成乘幂计算。例如，不是采用下面这样的方式算 ${b^8}$ ：

$$ b\cdot (b\cdot (b\cdot (b\cdot (b\cdot (b\cdot (b\cdot b))))))\,, $$

we can compute it using three multiplications:<br>
而是用三次乘法算出它来：

$$\eqalign{	b^2 &= b\cdot b, \cr
		b^4 &= b^2\cdot b^2, \cr
		b^8 &= b^4\cdot b^4. \cr} $$

This method works fine for exponents that are powers of 2. We can also take advantage of successive squaring in computing exponentials in general if we use the rule<br>
这一方法对于指数为2的乘幂都可以用。如果采用下面规则，我们就可以借助于连续求平方，去完成一般的乘幂计算：

$$\eqalign{b^n &= (b^{n / 2})^2 \qquad \;\, {\rm if\;} n {\rm\; is\; even}, \cr
b^n &= b\cdot b^{n-1} \qquad {\rm if\;} n {\rm\; is\; odd}. \cr} $$

We can express this method as a procedure:<br>
这一方法可以定义为如下的过程：

```
(define (fast-expt b n)
  (cond ((= n 0) 
         1)
        ((even? n) 
         (square (fast-expt b (/ n 2))))
        (else 
         (* b (fast-expt b (- n 1))))))
```

where the predicate to test whether an integer is even is defined in terms of the primitive procedure `remainder` by<br>
其中检测一个整数是否偶数的谓词可以基于基本过程`remainder`定义：

```
(define (even? n)
  (= (remainder n 2) 0))
```

The process evolved by `fast-expt` grows logarithmically with ${n}$ in both space and number of steps.  To see this, observe that computing ${b^{2n}}$ using `fast-expt` requires only one more multiplication than computing ${b^n}$.  The size of the exponent we can compute therefore doubles (approximately) with every new multiplication we are allowed.  Thus, the number of multiplications required for an exponent of ${n}$ grows about as fast as the logarithm of ${n}$ to the base 2.  The process has ${\Theta(\log n)}$ growth.[^1]<br>
由`fast-expt`演化出的计算过程，在空间和步数上相对于 ${n}$ 都是对数的。要看到这些情况，请注意，在用`fast-expt`计算 ${b^{2n}}$ 时，只需要比计算 ${b^n}$ 多做一次乘法。每做一次新的乘法，能够计算的指数值（大约）增大一倍。这样，计算指数 ${n}$ 所需要的乘法次数的增长大约就是以2为底的 ${n}$ 的对数值，这一计算过程增长的阶为 ${\Theta(\log n)}$ [^1]

The difference between ${\Theta(\log n)}$ growth and ${\Theta(n)}$ growth becomes striking as ${n}$ becomes large. For example, `fast-expt` for ${n}$ = 1000 requires only 14 multiplications[^2] It is also possible to use the idea of successive squaring to devise an iterative algorithm that computes exponentials with a logarithmic number of steps (see [Exercise1.16](#Exercise1.16)), although, as is often the case with iterative algorithms, this is not written down so straightforwardly as the recursive algorithm[^3]<br>
随着 ${n}$ 变大，${\Theta(\log n)}$ 增长与 ${\Theta(n)}$ 增长之间的差异也会变得非常明显。例如，对 ${n}$ = 1000, `fast-expt`只需要14次乘法[^2]。我们也可能采用连续求平方的想法，设计出一个具有对数步数的计算乘幂的迭代算法（见[练习1.16](#Exercise1.16)）。就像迭代算法的常见情况一样，写出这一算法就不像对递归算法那样直截了当了[^3]


<div id='Exercise1.16' markdown>

Exercise 1.16: Design a procedure that evolves an iterative exponentiation process that uses successive squaring and uses a logarithmic number of steps, as does `fast-expt`. (Hint: Using the observation that ${(b^{n / 2})^2 = (b^2)^{n / 2}}$, keep, along with the exponent ${n}$ and the base ${b}$, an additional state variable ${a}$, and define the state transformation in such a way that the product ${ab^n}$ is unchanged from state to state. At the beginning of the process ${a}$ is taken to be 1, and the answer is given by the value of ${a}$ at the end of the process. In general, the technique of defining an invariant quantity that remains unchanged from state to state is a powerful way to think about the design of iterative algorithms.)<br>
练习1.16 请定义一个过程，它能产生出一个按照迭代方式的求幂计算过程，其中使用一系列的求平方，就像一样`fast-expt`只用对数个步骤那样。（提示：请利用关系 ${(b^{n / 2})^2 = (b^2)^{n / 2}}$，除了指数 ${n}$ 和基数 ${b}$ 之外，还应维持一个附加的状态变量 ${a}$，并定义好状态变换，使得从一个状态转到另一个状态时乘积 ${ab^n}$ 不变。在计算过程开始时令 ${a}$ 取值1，并用计算过程结束时 ${a}$ 的值作为回答。一般来说，定义一个不变量，要求它在状态之间保持不变，这一技术是思考迭代算法设计问题时的一种非常强有力的方法。）
</div>



<div id='Exercise1.17' markdown>

Exercise 1.17: The exponentiation algorithms in this section are based on performing exponentiation by means of repeated multiplication. In a similar way, one can perform integer multiplication by means of repeated addition. The following multiplication procedure (in which it is assumed that our language can only add, not multiply) is analogous to the `expt` procedure:<br>
练习1.17：本节里的求幂算法的基础就是通过反复做乘法去求乘幂。与此类似，也可以通过反复做加法的方式求出乘积。下面的乘积过程与`expt`过程类似（其中假定我们的语言只有加法而没有乘法）：

```
(define (* a b)
  (if (= b 0)
      0
      (+ a (* a (- b 1)))))
```
      
This algorithm takes a number of steps that is linear in `b`. Now suppose we include, together with addition, operations `double`, which doubles an integer, and `halve`, which divides an (even) integer by 2. Using these, design a multiplication procedure analogous to `fast-expt` that uses a logarithmic number of steps.<br>
这一算法具有相对于`b`的线性步数。现在假定除了加法之外还有运算`double`,它能求出一个整数的两倍，还有`halve`，它将一个（偶数）除以2.请用这些运算设计一个类似`fast-expt`的求乘积过程，使之只用对数的计算步数。
</div>


Exercise 1.18: Using the results of [Exercise 1.16](#Exercise1.16) and [Exercise 1.17](#Exercise1.17), devise a procedure that generates an iterative process for multiplying two integers in terms of adding, doubling, and halving and uses a logarithmic number of steps[^4]<br>
练习1.18：利用[练习1.16](#Exercise1.16) and [练习1.17](#Exercise1.17)的结果设计一个过程，它能产生出一个基于加、加倍和折半运算的迭代计算过程，只用对数的步数就能求出两个整数的乘积[^4]

Exercise 1.19: There is a clever algorithm for computing the Fibonacci numbers in a logarithmic number of steps. Recall the transformation of the state variables ${a}$ and ${b}$ in the `fib-iter` process of [1.2.2]:${a \gets a + b}$ and ${b \gets a}$. Call this transformation ${T}$, and observe that applying ${T}$ over and over again ${n}$ times, starting with 1 and 0, produces the pair Fib(${n+1}$) and Fib(${n}$). In other words, the Fibonacci numbers are produced by applying ${T^n}$, the the $n^{\mathrm{th}}$ power of the transformation ${T}$, starting with the pair (1, 0). Now consider ${T}$ to be the special case of ${p=0}$ and ${q=1}$ in a family of transformations ${T_{pq}}$, where ${T_{pq}}$ transforms the pair ${(a, b)}$ according to ${a \gets bq + aq + ap}$ and ${b \gets bp + aq}$. Show that if we apply such a transformation ${T_{pq}}$ twice, the effect is the same as using a single transformation ${T_{p'q'}}$ of the same form, and compute ${p'}$ and ${q'}$ in terms of ${p}$ and ${q}$.  This gives us an explicit way to square these transformations, and thus we can compute ${T^n}$ using successive squaring, as in the `fast-expt` procedure. Put this all together to complete the following procedure, which runs in a logarithmic number of steps[^5]<br>
练习1.19：存在着一种以对数步数求出斐波那契数的巧妙算法。请回忆[1.2.2]节`fib-iter`计算过程中状态变量 ${a}$ 和 ${b}$ 的变换规则，${a \gets a + b}$ 和 ${b \gets a}$，现在将这种变换称为 ${T}$ 变换。通过观察可以发现，从1和0开始将 ${T}$ 反复应用 ${n}$ 次，将产生出一对数 Fib(${n+1}$) 和 Fib(${n}$)。换句话说，斐波那契数可以通过将 ${T^n}$ (变换 ${T}$ 的 ${n}$ 次方)应用于对偶(1, 0)而产生出来。现在将 ${T}$ 看做是变换族 ${T_{pq}}$ 中 ${p=0}$ 且 ${q=1}$ 的特殊情况，其中 ${T_{pq}}$ 是对于对偶 ${(a, b)}$ 按照 ${a \gets bq + aq + ap}$ 和 ${b \gets bp + aq}$ 规则的变换。请证明，如果我们应用变换 ${T_{pq}}$ 两次，其效果等同于应用同样形式的一次变换 ${T_{p'q'}}$，其中的 ${p'}$ 和 ${q'}$ 可以由 ${p}$ 和 ${q}$ 计算出来。这就指明了一条求出这种变换的平方的路径，使我们可以通过连续求平方的方式去计算 ${T^n}$，就像`fast-expt` 过程里所做的那样。将所有这些集中到一起，就形成了下面的过程，其运行只需要对数的步数[^5]

```
(define (fib n)
  (fib-iter 1 0 0 1 n))

(define (fib-iter a b p q count)
  (cond ((= count 0) 
         b)
        ((even? count)
         (fib-iter a
                   b
                   ⟨??⟩  ;compute p'
                   ⟨??⟩  ;compute q'
                   (/ count 2)))
        (else 
         (fib-iter (+ (* b q) 
                      (* a q) 
                      (* a p))
                   (+ (* b p) 
                      (* a q))
                   p
                   q
                   (- count 1)))))
```

[^1]: More precisely, the number of multiplications required is equal to 1 less than the log base 2 of ${n}$ plus the number of ones in the binary representation of ${n}$. This total is always less than twice the log base 2 of ${n}$.  The arbitrary constants ${k_1}$ and ${k_2}$ in the definition of order notation imply that, for a logarithmic process, the base to which logarithms are taken does not matter, so all such processes are described as ${\Theta(\log n)}$.<br>
更准确地说，这里所需乘法地次数等于 ${n}$ 的以2为底的对数值，再加上 ${n}$ 的二进制表示中1的个数减1.这个值总小于 ${n}$ 的以2为底的对数值的两倍。对于对数的计算过程而言，在阶记法定义中的任意常量 ${k_1}$ 和 ${k_2}$，意味着对数的底并没有关系。因此这种过程被描述为 ${\Theta(\log n)}$.

[^2]: You may wonder why anyone would care about raising numbers to the 1000th power.  See [1.2.6].<br>
你可能奇怪什么人会关心去求数的1000次乘幂。参看[1.2.6]节

[^3]: This is iterative algorithm is ancient. It appears in the Chandah-sutra by Áchárya Pingala, written before 200 B.C. See [Knuth 1981], section 4.6.3, for a full discussion and analysis of this and other methods of exponentiation.<br>
这一迭代算法也是一个古董，它出现在公园前200年之前Chandah-sutra所写的Áchárya Pingala里。有关求幂的这一算法和其它算法的完整讨论和分析，请参考[Knuth 1981]的4.6.3节

[^4]: This algorithm, which is sometimes known as the ''Russian peasant method'' of multiplication, is ancient.  Examples of its use are found in the Rhind Papyrus, one of the two oldest mathematical documents in existence, written about 1700 B.C. (and copied from an even older document) by an Egyptian scribe named A'h-mose.<br>
这一算法有时被称为乘法的“俄罗斯农民的方法”,它的历史也很悠久。使用它的实例可以在莱因德纸草书(Rhind Papyrus)中找到，这是现存最悠久的两份数学文献之一，由一位名为A'h-mose的埃及抄写人写于大约公元前1700年(而且是另一份年代更久远的文献的复制品)。

[^5]: This exercise was suggested to us by Joe Stoy, based on an example in @ref{Kaldewaij 1990}.<br>
这一练习是Joy Stoy给我们建议的，基于在Kaldewaij 1990的一个例子。

[1.2.6]: 1.2.6.md
[1.2.2]: 1.2.2.md
[Knuth 1981]: {{ config.extra.homepage_sicp }}/references/#ref_Knuth_1981
