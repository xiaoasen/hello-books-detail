---
comments: true
---

# 1.3.1  Procedures as Arguments(过程作为参数)

Consider the following three procedures. The first computes the sum of the integers from `a` through `b`:<br>
考虑下面的三个过程，第一个计算从`a`到`b`的各整数之和：

```
(define (sum-integers a b)
  (if (> a b)
      0
      (+ a (sum-integers (+ a 1) b))))
```

The second computes the sum of the cubes of the integers in the given range:<br>
第二个计算给定范围内的整数的立方之和：

```
(define (sum-cubes a b)
  (if (> a b)
      0
      (+ (cube a) (sum-cubes (+ a 1) b))))
```

The third computes the sum of a sequence of terms in the series<br>
第三个计算下面的序列之和：

$$ {1\over1\cdot 3} +  {1\over5\cdot 7} + {1\over9\cdot 11} + \dots, $$

which converges to ${\pi \big/ 8}$ (very slowly):[^1]<br>
它将(非常缓慢地)收敛到 ${\pi \big/ 8}$：[^1]

```
(define (pi-sum a b)
  (if (> a b)
      0
      (+ (/ 1.0 (* a (+ a 2))) (pi-sum (+ a 4) b))))
```

These three procedures clearly share a common underlying pattern. They are for the most part identical, differing only in the name of the procedure, the function of `a` used to compute the term to be added, and the function that provides the next value of `a`. We could generate each of the procedures by filling in slots in the same template:<br>
可以明显看出，这三个过程共享着一种公共的基础模式。它们的很大一部分是共同的，只在所用的过程名字上不一样：用于从`a`算出需要加的项的函数，还有用于提供下一个`a`值的函数。我们可以通过填充下面模板中的各空位，产生出上面的各个过程：

```
(define (<name> a b)
  (if (> a b)
      0
      (+ (<term> a)
         (<name> (<next> a) b))))
```

The presence of such a common pattern is strong evidence that there is a useful abstraction waiting to be brought to the surface. Indeed, mathematicians long ago identified the abstraction of summation of a series and invented ''sigma notation,'' for example<br>
这种公共模式的存在是一种很强的证据，说明这里实际上存在着一种很有用的抽象，在那里等着浮现出来。确实，数学家很早就认识到序列求和中的抽象模式，并提出了专门的“求和记法”,例如：

$$ \sum\limits_{n=a}^b f(n) = f(a) + \dots + f(b), $$

to express this concept. The power of sigma notation is that it allows mathematicians to deal with the concept of summation itself rather than only with particular sums -- for example, to formulate general results about sums that are independent of the particular series being summed.<br>
用于描述这一概念。求和记法的威力在于它使数学家能去处理求和的概念本身，而不只是某个特定的求和——例如，借助它去形式化某些并不依赖于特定求和序列的求和结果。

Similarly, as program designers, we would like our language to be powerful enough so that we can write a procedure that expresses the concept of summation itself rather than only procedures that compute particular sums. We can do so readily in our procedural language by taking the common template shown above and transforming the ''slots'' into formal parameters:<br>
与此类似，作为程序模式，我们也希望所用的语言足够强大，能用于写出一个过程，去表述求和的概念，而不是只能写计算特定求和的过程。我们确实可以在所用的过程语言中做到这些，只要按照上面给出的模式，将其中的“空位”翻译为形式参数：

```
(define (sum term a next b)
  (if (> a b)
      0
      (+ (term a)
         (sum term (next a) next b))))
```

Notice that sum takes as its arguments the lower and upper bounds `a` and `b` together with the procedures `term` and `next`. We can use `sum` just as we would any procedure. For example, we can use it (along with a procedure `inc` that increments its argument by 1) to define `sum-cubes`:<br>
请注意，sum仍然以作为下界和上界的参数`a`和`b`为参数，但是这里又增加了过程参数`term`和`next`。使用`sum`的方式与其他函数完全一样。例如，我们可以用它去定义`sum-cubes`(还需要一个过程`inc`,它得到参数值加一):

```
(define (inc n) (+ n 1))
(define (sum-cubes a b)
  (sum cube a inc b))
```

Using this, we can compute the sum of the cubes of the integers from 1 to 10:<br>
我们可以用这个过程算出从1到10的立方和：

```
(sum-cubes 1 10)
3025
```

With the aid of an identity procedure to compute the term, we can define `sum-integers` in terms of `sum`:<br>
利用一个恒等函数帮助算出项值，我们就可以基于`sum`定义出`sum-integers`:

```
(define (identity x) x)

(define (sum-integers a b)
  (sum identity a inc b))
```

Then we can add up the integers from 1 to 10:<br>
而后就可以求出从1到10的整数之和了：

```
(sum-integers 1 10)
55
```

We can also define `pi-sum` in the same way:[^2]<br>
我们也可以按同样方式定义pi-sum:[^2]

```
(define (pi-sum a b)
  (define (pi-term x)
    (/ 1.0 (* x (+ x 2))))
  (define (pi-next x)
    (+ x 4))
  (sum pi-term a pi-next b))
```

Using these procedures, we can compute an approximation to ${\pi}$:<br>
利用这一过程就能计算出 ${\pi}$ 的一个近似值了：

```
(* 8 (pi-sum 1 1000))
3.139592655589783
```

Once we have `sum`, we can use it as a building block in formulating further concepts. For instance, the definite integral of a function ${f}$ between the limits ${a}$ and ${b}$ can be approximated numerically using the formula<br>
一旦有了`sum`,我们就能用它作为基本构件，去形式化其他概念。例如，求出函数 ${f}$ 在范围 ${a}$ 和 ${b}$ 之间的定积分的近似值，可以用下面公式完成

$$\eqalign{ 
  {\int_a^b \!\!\! f} \, &= \left[\;f\!\left(a + {d\!x\over 2}\right) 
                  + f\!\left(a + d\!x + {d\!x\over 2}\right) \right. \cr
                 &+ \left. f\!\left(a + 2d\!x + {d\!x\over 2}\right) 
                  + \,\dots \;\right] \! d\!x \cr } $$

for small values of ${d\!x}$. We can express this directly as a procedure:<br>
其中的 ${d\!x}$ 是一个很小的值。我们可以将这个公式直接描述为一个过程：

```
(define (integral f a b dx)
  (define (add-dx x) (+ x dx))
  (* (sum f (+ a (/ dx 2.0)) add-dx b)
     dx))
(integral cube 0 1 0.01)
.24998750000000042
(integral cube 0 1 0.001)
.249999875000001
```

(The exact value of the integral of `cube` between 0 and 1 is 1/4.)<br>
(`cube`在0和1间积分的精确值是1/4。)

<div id="Exercise1.29" markdown>

Exercise 1.29.  Simpson's Rule is a more accurate method of numerical integration than the method illustrated above. Using Simpson's Rule, the integral of a function ${f}$ between ${a}$ and ${b}$ is approximated as<br>
练习1.29  辛普森规则是另一种比上面所用规则更精确的数值积分方法。采用辛普森规则，函数 ${f}$ 在范围 ${a}$ 和 ${b}$ 之间的定积分的近似值是：

$$ {h\over 3}(y_0 + 4\!y_1 + 2\!y_2 + 4\!y_3 + 2\!y_4 + \dots + 2\!y\!_{n-2} + 4\!y\!_{n-\!1} + y\!_n), $$

where ${h = (b - a) \big/\!n}$, for some even integer ${n}$, and ${y_k = f(a + kh)}$. (Increasing ${n}$ increases the accuracy of the approximation.) Define a procedure that takes as arguments ${f}$, ${a}$, ${b}$, and ${n}$ and returns the value of the integral, computed using Simpson's Rule. Use your procedure to integrate `cube` between 0 and 1 (with ${n = 100}$ and ${n = 1000}$), and compare the results to those of the `integral` procedure shown above.<br>
其中 ${h = (b - a) \big/\!n}$, ${n}$ 是某个偶数，而 ${y_k = f(a + kh)}$ (增大 ${n}$ 能提高近似值的精度)。请定义一个具有参数 ${f}$、${a}$、${b}$ 和 ${n}$,采用辛普森规则计算并返回积分值的过程。用你的函数求出`cube`在0和1之间的积分(用 ${n = 100}$ 和 ${n = 1000}$),并将得到的值与上面用`integral`过程所得到的结果比较。
</div>

<div id="Exercise1.30" markdown>

Exercise 1.30.  The `sum` procedure above generates a linear recursion. The procedure can be rewritten so that the sum is performed iteratively. Show how to do this by filling in the missing expressions in the following definition:<br>
练习1.30  上面的过程`sum`将产生出一个线性递归。我们可以重写该过程，使之能够迭代地执行。请说明应该怎样通过填充下面定义中缺少的表达式，完成这一工作。

```
(define (sum term a next b)
  (define (iter a result)
    (if <??>
        <??>
        (iter <??> <??>)))
  (iter <??> <??>))
```
</div>

<div id="Exercise1.31" markdown>

Exercise 1.31.  
a.  The `sum` procedure is only the simplest of a vast number of similar abstractions that can be captured as higher-order procedures.[^3] Write an analogous procedure called `product` that returns the product of the values of a function at points over a given range. Show how to define `factorial` in terms of `product`. Also use `product` to compute approximations to ${\pi}$ using the formula.[^4]<br>
练习1.31  
a. 过程`sum`是可以用高阶过程表示的大量类似抽象中最简单的一个"。请写出一个类似的称为`product`的过程，它返回在给定范围中各点的某个函数值的乘积。请说明如何用`product`定义`factorial`。另请按照下面公式计算 ${\pi}$ 的近似值.[^4]

$$ {\pi\over 4} = {2\cdot 4\cdot 4\cdot 6\cdot 6\cdot 8\cdot\dots\over 
		   3\cdot 3\cdot 5\cdot 5\cdot 7\cdot 7\cdot\dots}\,. $$

b.  If your `product` procedure generates a recursive process, write one that generates an iterative process. If it generates an iterative process, write one that generates a recursive process.<br>
b. 如果你的`product`过程生成的是一个递归计算过程，那么请写出一个生成迭代计算过程的过程。如果它生成一个迭代计算过程，请写一个生成递归计算过程的过程。
</div>

<div id="Exercise1.32" markdown>

Exercise 1.32.  
a. Show that `sum` and `product` (exercise [1.31](#Exercise1.31)) are both special cases of a still more general notion called `accumulate` that combines a collection of terms, using some general accumulation function:<br>
练习1.32  
a. 请说明，`sum`和`product`(练习[1.31](#Exercise1.31))都是另一称为`accumulate`的更一般概念的特殊情况，`accumulate`使用某些一般性的累积函数组合起一系列项：

```
(accumulate combiner null-value term a next b)
```

`Accumulate` takes as arguments the same term and range specifications as `sum` and `product`, together with a `combiner` procedure (of two arguments) that specifies how the current term is to be combined with the accumulation of the preceding terms and a `null-value` that specifies what base value to use when the terms run out. Write `accumulate` and show how `sum` and `product` can both be defined as simple calls to `accumulate`.<br>
`Accumulate`取的是与`sum`和`product`一样的项和范围描述参数，再加上一个(两个参数的)`combiner`过程，它描述如何将当前项与前面各项的积累结果组合起来，另外还有一个`null-value`参数，它描述在所有的项都用完时的基本值。请写出`accumulate`,并说明我们能怎样基于简单地调用`accumulate`,定义出`sum`和`product`来。

b. If your `accumulate` procedure generates a recursive process, write one that generates an iterative process. If it generates an iterative process, write one that generates a recursive process.<br>
b. 如果你的`accumulate`过程生成的是一个递归计算过程，那么请写出一个生成迭代计算过程的过程。如果它生成一个迭代计算过程，请写一个生成递归计算过程的过程。
</div>

<div id="Exercise1.33" markdown>

Exercise 1.33.  You can obtain an even more general version of `accumulate`(exercise [1.32](#Exercise1.32)) by introducing the notion of a `filter` on the terms to be combined. That is, combine only those terms derived from values in the range that satisfy a specified condition. The resulting `filtered-accumulate` abstraction takes the same arguments as accumulate, together with an additional predicate of one argument that specifies the filter. Write `filtered-accumulate` as a procedure. Show how to express the following using `filtered-accumulate`:<br>
练习1.33  你可以通过引进一个处理被组合项的过滤器(filter)概念，写出一个比accumulate(练习[1.32](#Exercise1.32))更一般的版本。也就是说，在计算过程中，只组合起由给定范围得到的项里的那些满足特定条件的项。这样得到的`filtered-accumulate`抽象取与上面累积过程同样的参数，再加上一个另外的描述有关过滤器的谓词参数。请写出`filtered-accumulate`作为一个过程，说明如何用`filtered-accumulate`表达以下内容：

a. the sum of the squares of the prime numbers in the interval ${a}$ to ${b}$ (assuming that you have a `prime?` predicate already written)<br>
a. 求出在区间 ${a}$ 到 ${b}$ 中所有素数之和(假定你已经写出了谓词`prime?`)。

b. the product of all the positive integers less than ${n}$ that are relatively prime to ${n}$ (i.e., all positive integers ${i < n}$ such that $( \mathrm{GCD}(i, n) = 1 )$).<br>
b. 小于 ${n}$ 的所有与 ${n}$ 互素的正整数(即所有满足 $( \mathrm{GCD}(i, n) = 1 )$ 的整数 ${i < n}$)之乘积。
</div>

[^1]: 
    This series, usually written in the equivalent form $( \frac{\pi}{4} = 1 - \frac{1}{3} + \frac{1}{5} - \frac{1}{7} + \dots )$, is due to Leibniz. We'll see how to use this as the basis for some fancy numerical tricks in section [3.5.3].<br>
    这一序列通常被写成与之等价的形式 $( \frac{\pi}{4} = 1 - \frac{1}{3} + \frac{1}{5} - \frac{1}{7} + \dots )$，这归功于菜布尼茨。我们将在[3.5.3]节看到如何用它作为某些数值技巧的基础。


[^2]: 
    Notice that we have used block structure (section [1.1.8]) to embed the definitions of `pi-next` and `pi-term` within `pi-sum`, since these procedures are unlikely to be useful for any other purpose. We will see how to get rid of them altogether in section [1.3.2].<br>
    注意，我们已经用([1.1.8]节介绍的)块结构将`pi-next`和`pi-term`嵌入`pi-sum`内部，因为这些函数不大可能用于其他地方。我们将在[1.3.2]节说明如何完全摆脱这种定义。

[^3]: 
    The intent of exercises [1.31](#Exercise1.31)-[1.33](#Exercise1.33) is to demonstrate the expressive power that is attained by using an appropriate abstraction to consolidate many seemingly disparate operations. However, though accumulation and filtering are elegant ideas, our hands are somewhat tied in using them at this point since we do not yet have data structures to provide suitable means of combination for these abstractions. We will return to these ideas in section [2.2.3] when we show how to use sequences as interfaces for combining filters and accumulators to build even more powerful abstractions. We will see there how these methods really come into their own as a powerful and elegant approach to designing programs.<br>
    练习[1.31](#Exercise1.31)-[1.33](#Exercise1.33)的意图是阐释用一个适当的抽象去整理许多貌似毫无关系的操作，所获得的巨大表达能力。显然，虽然累积和过滤器都是很优美的想法，我们并没有急于将它们用在这里，因为我们还没有数据结构的思想，无法用它们去提供组合这些抽象的适当手段。我们将在[2.2.3]节回到这些思想，那时将说明如何用序列的概念作为界面，组合起过滤器和累积，去构造出更强大得多的抽象。我们将在那里看到，这些方法本身如何能成为设计程序的强有力而又非常优美的途径。


[^4]: 
    This formula was discovered by the seventeenth-century English mathematician John Wallis.<br>
    这一公式是由17世纪英国数学家John Wallis发现的。

[3.5.3]: {{ config.extra.homepage_sicp }}/chapter_3/chapter_3_5/3.5.3/
[1.1.8]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_1/1.1.8/
[1.3.2]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_3/1.3.2/
[2.2.3]: {{ config.extra.homepage_sicp }}/chapter_2/chapter_2_2/2.2.3/