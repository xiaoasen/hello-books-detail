---
comments: true
---

# 1.3.3  Procedures as General Methods(过程作为一般性的方法)

We introduced compound procedures in section [1.1.4] as a mechanism for abstracting patterns of numerical operations so as to make them independent of the particular numbers involved. With higher-order procedures, such as the `integral` procedure of section [1.3.1], we began to see a more powerful kind of abstraction: procedures used to express general methods of computation, independent of the particular functions involved. In this section we discuss two more elaborate examples -- general methods for finding zeros and fixed points of functions -- and show how these methods can be expressed directly as procedures.<br>
我们在[1.1.4]节里介绍了复合过程，是为了作为一种将若干操作的模式抽象出来的机制，使所描述的计算不再依赖于所涉及的特定的数值。有了高阶过程，例如[1.3.1]节的`integral`过程，我们开始看到一种威力更强大的抽象，它们也是一类方法，可用于表述计算的一般性过程，与其中所涉及的特定函数无关。本节将讨论两个更精细的实例——找出函数零点和不动点的一般性方法，并说明如何通过过程去直接描述这些方法。


## Finding roots of equations by the half-interval method(通过区间折半寻找方程的根)

The `half-interval method` is a simple but powerful technique for finding roots of an equation ${f(x) = 0}$, where ${f}$ is a continuous function. The idea is that, if we are given points ${a}$ and ${b}$ such that ${f(a) < 0 < f(b)}$, then ${f}$ must have at least one zero between ${a}$ and ${b}$. To locate a zero, let ${x}$ be the average of ${a}$ and ${b}$ and compute ${f(x)}$. If ${f(x) > 0}$, then ${f}$ must have a zero between ${a}$ and ${x}$. If ${f(x) < 0}$, then ${f}$ must have a zero between ${x}$ and ${b}$. Continuing in this way, we can identify smaller and smaller intervals on which ${f}$ must have a zero. When we reach a point where the interval is small enough, the process stops. Since the interval of uncertainty is reduced by half at each step of the process, the number of steps required grows as ${\Theta(\log(L \big/\! T))}$, where ${L}$ is the length of the original interval and ${T}$ is the error tolerance (that is, the size of the interval we will consider ''small enough''). Here is a procedure that implements this strategy:<br>
`区间折半方法`是寻找方程 ${f(x) = 0}$ 根的一种筒单而又强有力的方法，这里的 ${f}$ 是一个连续函数。这种方法的基本想法是，如果对于给定点 ${a}$ 和 ${b}$ 有 ${f(a) < 0 < f(b)}$,那么 ${f}$ 在 ${a}$ 和 ${b}$ 之间必然有一个零点。为了确定这个零点，令 ${f(x)}$ 是 ${a}$ 和 ${b}$ 的平均值并计算出 ${f}$。如果 ${f(x) > 0}$,那么在 ${a}$ 和 ${x}$ 之间必然有的一个 ${f}$ 的零点；如果 ${f(x) < 0}$,那么在 ${x}$ 和 ${b}$ 之间必然有的一个 ${f}$ 的零点。继续这样做下去，就能确定出越来越小的区间，且保证在其中必然有 ${f}$ 的一个零点。当区间“足够小”时，就结束这一计算过程了。因为这种不确定的区间在计算过程的每一步都缩小一半，所需步数的增长将是 ${\Theta(\log(L \big/\! T))}$,其中 ${T}$ 是区间的初始长度，${T}$ 是可容忍的误差(即认为“足够小”的区间的大小)。下面是一个实现了这一策略的过程：

```
(define (search f neg-point pos-point)
  (let ((midpoint (average neg-point pos-point)))
    (if (close-enough? neg-point pos-point)
        midpoint
        (let ((test-value (f midpoint)))
          (cond ((positive? test-value)
                 (search f neg-point midpoint))
                ((negative? test-value)
                 (search f midpoint pos-point))
                (else midpoint))))))
```

We assume that we are initially given the function ${f}$ together with points at which its values are negative and positive. We first compute the midpoint of the two given points. Next we check to see if the given interval is small enough, and if so we simply return the midpoint as our answer. Otherwise, we compute as a test value the value of ${f}$ at the midpoint. If the test value is positive, then we continue the process with a new interval running from the original negative point to the midpoint. If the test value is negative, we continue with the interval from the midpoint to the positive point. Finally, there is the possibility that the test value is 0, in which case the midpoint is itself the root we are searching for.<br>
假定开始时给定了函数 ${f}$,以及使它取值为负和为正的两个点。我们首先算出两个给定点的中点，而后检查给定区间是否已经足够小。如果是的话，就返回这一中点的值作为回答；否则就算出 ${f}$ 在这个中点的值。如果检查发现得到的这个值为正，那么就以从原来负点到中点的新区间继续下去；如果这个值为负，就以中点到原来为正的点为新区间并继续下去。还有，也存在着检测值恰好为0的可能性，这时中点就是我们所寻找的根。

To test whether the endpoints are ''close enough'' we can use a procedure similar to the one used in section [1.1.7] for computing square roots:[^1]<br>
为了检查两个端点是否“足够接近”,我们可以用一个过程，它与[1.1.7]节计算平方根时所用的那个过程很类似”:[^1]

```
(define (close-enough? x y)
  (< (abs (- x y)) 0.001))
```

`Search` is awkward to use directly, because we can accidentally give it points at which ${f}$ 's values do not have the required sign, in which case we get a wrong answer. Instead we will use `search` via the following procedure, which checks to see which of the endpoints has a negative function value and which has a positive value, and calls the `search` procedure accordingly. If the function has the same sign on the two given points, the half-interval method cannot be used, in which case the procedure signals an error.[^2]<br>
`search`很难直接去用，因为我们可能会偶然地给了它一对点，相应的 ${f}$ 值并不具有这个过程所需的正负号，这时就会得到错误的结果。让我们换一种方式，通过下面的过程去用`search`,这一过程检查是否某个点具有负的函数值，另一个点是正值，并根据具体情况去调用`search`过程。如果这一函数在两个给定点的值同号，那么就无法使用折半方法，在这种情况下过程发出错误信号。[^2]

```
(define (half-interval-method f a b)
  (let ((a-value (f a))
        (b-value (f b)))
    (cond ((and (negative? a-value) (positive? b-value))
           (search f a b))
          ((and (negative? b-value) (positive? a-value))
           (search f b a))
          (else
           (error "Values are not of opposite sign" a b)))))
```

The following example uses the half-interval method to approximate ${\pi}$ as the root between 2 and 4 of ${\sin x = 0}$:<br>
下面实例用折半方法求 ${\pi}$ 的近似值，它正好是 ${\sin x = 0}$ 在2和4之间的根：

```
(half-interval-method sin 2.0 4.0)
3.14111328125
```

Here is another example, using the half-interval method to search for a root of the equation ${x^3 - 2x - 3 = 0}$ between 1 and 2:<br>
这里是另一个例子，用折半方法找出 ${x^3 - 2x - 3 = 0}$ 在1和2之间的根：

```
(half-interval-method (lambda (x) (- (* x x x) (* 2 x) 3))
                      1.0
                      2.0)
1.89306640625
```

## Finding fixed points of functions(找出函数的不动点)

A number ${x}$ is called a *fixed point* of a function ${f}$ if ${x}$ satisfies the equation ${f(x) = x}$. For some functions ${f}$ we can locate a fixed point by beginning with an initial guess and applying ${f}$ repeatedly,<br>
数 ${x}$ 称为函数 ${f}$ 的不动点，如果 ${x}$ 满足方程 ${f(x) = x}$。对于某些函数，通过从某个初始猜测出发，反复地应用 ${f}$

$$ f(x),\quad f(f(x)),\quad f(f(f(x))), \quad\dots, $$

until the value does not change very much. Using this idea, we can devise a procedure `fixed-point` that takes as inputs a function and an initial guess and produces an approximation to a fixed point of the function. We apply the function repeatedly until we find two successive values whose difference is less than some prescribed tolerance:<br>
直到值的变化不大时，就可以找到它的一个不动点。根据这个思路，我们可以设计出一个过程`fixed-point`,它以一个函数和一个初始猜测为参数，产生出该函数的一个不动点的近似值。我们将反复应用这个函数，直至发现连续的两个值之差小于某个事先给定的容许值：

```
(define tolerance 0.00001)
(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
          next
          (try next))))
  (try first-guess))
```

For example, we can use this method to approximate the fixed point of the cosine function, starting with 1 as an initial approximation:[^3]<br>
例如，下面用这一方法求出的是余弦函数的不动点，其中用1作为初始近似值[^3]

```
(fixed-point cos 1.0)
.7390822985224023
```

Similarly, we can find a solution to the equation ${y = \sin y + \cos y}$:<br>
类似地，我们也可以找出方程 ${y = \sin y + \cos y}$ 的一个解：

```
(fixed-point (lambda (y) (+ (sin y) (cos y)))
             1.0)
1.2587315962971173
```

The fixed-point process is reminiscent of the process we used for finding square roots in section [1.1.7]. Both are based on the idea of repeatedly improving a guess until the result satisfies some criterion. In fact, we can readily formulate the square-root computation as a fixed-point search. Computing the square root of some number ${x}$ requires finding a ${y}$ such that ${y^2 = x}$. Putting this equation into the equivalent form ${y = x \big/\! y}$, we recognize that we are looking for a fixed point of the function[^4], ${y \mapsto x \big/\! y}$ and we can therefore try to compute square roots as<br>
这一不动点的计算过程使人回忆起[1.1.7]节里用于找平方根的计算过程。两者都是基于同样的想法：通过不断地改进猜测，直至结果满足某一评价准则为止。事实上，我们完全可以将平方根的计算形式化为一个寻找不动点的计算过程。计算某个数 ${x}$ 的平方根，就是要找到一个 ${y}$ 使得 ${y^2 = x}$。将这一等式变成另一个等价形式 ${y = x \big/\! y}$,就可以发现，这里要做的就是寻找函数 ${y \mapsto x \big/\! y}$ 的不动点[^4]。因此，可以用下面方式试着去计算平方根：

```
(define (sqrt x)
  (fixed-point (lambda (y) (/ x y))
               1.0))
```

Unfortunately, this fixed-point search does not converge. Consider an initial guess ${y_1}$. The next guess is ${y_2 = x \big/\! y_1}$ and the next guess is ${y_3 = x \big/\! y_2 = x \big/ (x \big/\! y_1) = y_1}$. This results in an infinite loop in which the two guesses ${y_1}$ and ${y_2}$ repeat over and over, oscillating about the answer.<br>
遗憾的是，这一不动点搜寻并不收敛。考虑某个初始猜测 ${y_1}$,下一个猜测将是 ${y_2 = x \big/\! y_1}$,而再下一个猜测是 ${s = x \big/\! y_2 = x \big/ (x \big/\! y_1) = y_1}$。结果是进入了一个无限循环，其中没完没了地反复出现两个猜测 ${y_1}$ 和 ${y_2}$,在答案的两边往复振荡。

One way to control such oscillations is to prevent the guesses from changing so much. Since the answer is always between our guess ${y}$ and ${x \big/\! y}$, we can make a new guess that is not as far from ${y}$ as ${x \big/\! y}$ by averaging ${y}$ with ${x \big/\! y}$, so that the next guess after ${y}$ is ${ {1\over2}(y + x \big/\! y) }$ instead of ${x \big/\! y}$. The process of making such a sequence of guesses is simply the process of looking for a fixed point of ${y \mapsto {1\over2}(y + x \big/\! y)}$:<br>
控制这类振荡的一种方法是不让有关的猜测变化太剧烈。因为实际答案总是在两个猜测 ${y}$ 和 ${x \big/\! y}$ 之间，我们可以做出一个猜测，使之不像 ${x \big/\! y}$ 那样远离 ${y}$,为此可以用 ${y}$ 和 ${x \big/\! y}$ 的平均值。这样，我们就取 ${y}$ 之后的下一个猜测值为 ${ {1\over2}(y + x \big/\! y) }$ 而不是 ${x \big/\! y}$。做出这种猜测序列的计算过程也就是搜寻 ${y \mapsto {1\over2}(y + x \big/\! y)}$ 的不动点：

```
(define (sqrt x)
  (fixed-point (lambda (y) (average y (/ x y)))
               1.0))
```

(Note that ${y = {1\over2}(y + x \big/\! y)}$ is a simple transformation of the equation ${y = x \big/\! y;}$; to derive it, add ${y}$ to both sides of the equation and divide by 2.)<br>
(请注意，${y = {1\over2}(y + x \big/\! y)}$ 是方程 ${y = x \big/\! y;}$ 经过简单变换的结果，导出它的方式是在方程两边都加 ${y}$, 然后将两边都除以2。)

With this modification, the square-root procedure works. In fact, if we unravel the definitions, we can see that the sequence of approximations to the square root generated here is precisely the same as the one generated by our original square-root procedure of section [1.1.7]. This approach of averaging successive approximations to a solution, a technique we that we call *average damping*, often aids the convergence of fixed-point searches.<br>
经过这一修改，平方根过程就能正常工作了。事实上，如果我们仔细分析这一定义，那么就可以看到，它在求平方根时产生的近似值序列，正好就是[1.1.7]节原来那个求平方根过程产生的序列。这种取逼进一个解的一系列值的平均值的方法，是一种称为平均阻尼的技术，它常常用在不动点搜寻中，作为帮助收敛的手段。

<div id="Exercise1.35" markdown>

Exercise 1.35.  Show that the golden ratio ${\varphi}$ (section [1.2.2]) is a fixed point of the transformation ${x \mapsto 1 + 1 \big/\! x}$, and use this fact to compute ${\varphi}$ by means of the fixed-point procedure.<br>
练习1.35  请证明黄金分割率 ${\varphi}$ ([1.2.2]节)是变换 ${x \mapsto 1 + 1 \big/\! x}$ 的不动点。请利用这一事实，通过过程`fixed-point`计算出 ${\varphi}$ 的值。
</div>

<div id="Exercise1.36" markdown>

Exercise 1.36.  Modify `fixed-point` so that it prints the sequence of approximations it generates, using the `newline` and `display` primitives shown in exercise [1.22]. Then find a solution to ${x^x = 1000}$ by finding a fixed point of ${x \mapsto \log(1000) \big/\! \log(x)}$. (Use Scheme's primitive log procedure, which computes natural logarithms.) Compare the number of steps this takes with and without average damping. (Note that you cannot start `fixed-point` with a guess of 1, as this would cause division by ${\log(1) = 0}$.)<br>
练习1.36  请修改`fixed-point`,使它能打印出计算中产生的近似值序列，用练习[1.22]展示的`newline`和`display`基本过程。而后通过找出 ${x \mapsto \log(1000) \big/\! \log(x)}$ 的不动点的方式，确定 ${x^x = 1000}$ 的一个根(请利用Scheme的基本过程1og,它计算自然对数值)。请比较一下采用平均阻尼和不用平均阻尼时的计算步数。(注意，你不能用猜测1去启动`fixed-point`,因为这将导致除以 ${\log(1) = 0}$。)
</div>


<div id="Exercise1.37" markdown>

Exercise 1.37.  
a. An infinite *continued fraction* is an expression of the form<br>
练习1.37 
a. 一个无穷连分式是一个如下形式的表达式：

$$ {f} = {N_1 \over {D_1 + {N_2 \over {D_2 + {N_3 \over D_3 + \dots}}}}}\,. $$

As an example, one can show that the infinite continued fraction expansion with the ${N_i}$ and the ${D_i}$ all equal to 1 produces ${1 \big/ \varphi}$, where ${\varphi}$ is the golden ratio (described in section [1.2.2]). One way to approximate an infinite continued fraction is to truncate the expansion after a given number of terms. Such a truncation -- a so-called *k-term finite continued fraction* -- has the form<br>
作为一个例子，我们可以证明在所有的 ${N_i}$ 和 ${D_i}$,都等于1时，这一无穷连分式产生出 ${1 \big/ \varphi}$,其中的 ${\varphi}$ 就是黄金分割率(见[1.2.2]节的描述)。逼进某个无穷连分式的一种方法是在给定数目的项之后截断，这样的一个截断称为*k项有限连分式*，其形式是：

$$ {N_1 \over {D_1 + {N_2 \over {\ddots + {N_k \over D_k}}}}}\,. $$

Suppose that `n` and `d` are procedures of one argument (the term index ${i}$) that return the ${N_i}$ and ${D_i}$ of the terms of the continued fraction. Define a procedure `cont-frac` such that evaluating `(cont-frac n d k)` computes the value of the ${k}$-term finite continued fraction. Check your procedure by approximating ${1 \big/ \varphi}$ using<br>
假定`n`和`d`都是只有一个参数(项的下标 ${i}$)的过程，它们分别返回连分式的项 ${N_i}$ 和 ${D_i}$。请定义一个过程`cont-frac`,使得对`(cont-frac n d k)`的求值计算出k项有限连分式的值。通过如下调用检查你的过程对于顺序的 ${k}$ 值是否逼进 ${1 \big/ \varphi}$:

```
(cont-frac (lambda (i) 1.0)
           (lambda (i) 1.0)
           k)
```

for successive values of `k`. How large must you make `k` in order to get an approximation that is accurate to 4 decimal places?<br>
你需要取多大的`k`才能保证得到的近似值具有十进制的4位精度?

b. If your `cont-frac` procedure generates a recursive process, write one that generates an iterative process. If it generates an iterative process, write one that generates a recursive process.<br>
b. 如果你的过程产生一个递归计算过程，那么请写另一个产生迭代计算的过程。如果它产生迭代计算，请写出另一个过程，使之产生一个递归计算过程。
</div>

<div id="Exercise1.38" markdown>

Exercise 1.38.  In 1737, the Swiss mathematician Leonhard Euler published a memoir *De Fractionibus Continuis*, which included a continued fraction expansion for ${e - 2}$, where ${e}$ is the base of the natural logarithms. In this fraction, the ${N_i}$ are all 1, and the ${D_i}$ are successively 1, 2, 1, 1, 4, 1, 1, 6, 1, 1, 8, .... Write a program that uses your `cont-frac` procedure from exercise [1.37](#Exercise1.37) to approximate ${e}$, based on Euler's expansion.<br>
练习1.38在1737年，瑞士数学家莱昂哈德·欧拉发表了一篇论文*De Fractionibus Continuis*,文中包含了 ${e - 2}$ 的一个连分式展开，其中的 ${e}$ 是自然对数的底。在这一分式中，${N_i}$ 全都是1,而 ${D_i}$ 依次为1,2,1,1,4,1,1,6,1,1,8,…。请写出一个程序，其中使用你在练习[1.37](#Exercise1.37)中所做的`cont-frac`过程，并能基于欧拉的展开式求出 ${e}$ 的近似值。

</div>

<div id="Exercise1.39" markdown>

Exercise 1.39.  A continued fraction representation of the tangent function was published in 1770 by the German mathematician J.H. Lambert:<br>
正切函数的连分式表示由德国数学家J.H.Lambert在1770年发表：

$$ {\tan x} = {x \over {1 - {x^2 \over {3 - {x^2 \over 5 - \dots}}}}}\,, $$

where ${x}$ is in radians. Define a procedure `(tan-cf x k)` that computes an approximation to the tangent function based on Lambert's formula. ${k}$ specifies the number of terms to compute, as in exercise [1.37](#Exercise1.37).<br>
其中的 ${x}$ 用弧度表示。请定义过程 `(tan-cf x k)`,它基于Lambert公式计算正切函数的近似值。${k}$ 描述的是计算的项数，就像练习[1.37]一样。

</div>

[^1]: 
    We have used 0.001 as a representative ''small'' number to indicate a tolerance for the acceptable error in a calculation. The appropriate tolerance for a real calculation depends upon the problem to be solved and the limitations of the computer and the algorithm. This is often a very subtle consideration, requiring help from a numerical analyst or some other kind of magician.<br>
    这里用0.001作为示意性的“小”数，表示计算中可以接受的容许误差。实际计算中所适用的容许误差依赖于被求解的问题，以及计算机和算法的限制。这一问题常常需要很细节的考虑，需要数值专家或者某些其他类型术士们的帮助。

[^2]:
    This can be accomplished using `error`, which takes as arguments a number of items that are printed as error messages.<br>
    用`error`可以完成此事，该过程以几个项作为参数，将它们打印出来作为出错信息。

[^3]:
    Try this during a boring lecture: Set your calculator to radians mode and then repeatedly press the `cos` button until you obtain the fixed point.<br>
    在一个没意思的课上试试下面计算：将你的计数器设置为弧度模式，而后反复按`cos`键直至你得到了这一不动点。

[^4]: 
    (pronounced ''maps to'') is the mathematician's way of writing `lambda`. ${y \mapsto x \big/\! y}$ means `(lambda(y) (/ x y))`, that is, the function whose value at ${y}$ is ${x \big/\! y}$.<br>
    (读作“映射到”)是数学家写`lambda`的方式，${y \mapsto x \big/\! y}$ 的意思就是`(lambda(y)(/ x y))`,也就是说，那个在 ${y}$ 处的值为 ${x \big/\! y}$ 的函数。


[1.1.4]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_1/1.1.4/

[1.3.1]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_3/1.3.1/

[1.1.7]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_1/1.1.7/

[1.2.2]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_2/1.2.2/

[1.22]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_2/1.2.6/#Exercise1.22