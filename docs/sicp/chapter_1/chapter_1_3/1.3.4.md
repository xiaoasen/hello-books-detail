---
comments: true
---

# 1.3.4  Procedures as Returned Values(过程作为返回值)

The above examples demonstrate how the ability to pass procedures as arguments significantly enhances the expressive power of our programming language. We can achieve even more expressive power by creating procedures whose returned values are themselves procedures.<br>
上面的例子说明，将过程作为参数传递，能够显著增强我们的程序设计语言的表达能力。通过创建另一种其返回值本身也是过程的过程，我们还能得到进一步的表达能力。

We can illustrate this idea by looking again at the fixed-point example described at the end of section [1.3.3]. We formulated a new version of the square-root procedure as a fixed-point search, starting with the observation that ${\sqrt{x}}$ is a fixed-point of the function ${y \mapsto x \big/\! y}$. Then we used average damping to make the approximations converge. Average damping is a useful general technique in itself. Namely, given a function ${f}$, we consider the function whose value at ${x}$ is equal to the average of ${x}$ and ${f(x)}$.<br>
我们将阐释这一思想，现在还是先来看[1.3.3]节最后描述的不动点例子。在那里我们构造出一个平方根程序的新版本，它将这一计算看作一种不动点搜寻过程。开始时，我们注意到 ${\sqrt{x}}$ 就是函数 ${y \mapsto x \big/\! y}$ 的不动点，而后又利用平均阻尼使这一逼进收敛。平均阻尼本身也是一种很有用的一般性技术。很自然，给定了一个函数 ${f}$ 之后，我们就可以考虑另一个函数，它在 ${x}$ 处的值等于 ${x}$ 和 ${f(x)}$ 的平均值。

We can express the idea of average damping by means of the following procedure:<br>
我们可以将平均阻尼的思想表述为下面的过程：

```
(define (average-damp f)
  (lambda (x) (average x (f x))))
```

`Average-damp` is a procedure that takes as its argument a procedure `f` and returns as its value a procedure (produced by the `lambda`) that, when applied to a number `x`, produces the average of `x` and `(f x)`. For example, applying `average-damp` to the `square` procedure produces a procedure whose value at some number ${x}$ is the average of ${x}$ and ${x^2}$. Applying this resulting procedure to 10 returns the average of 10 and 100, or 55:[^1]<br>
这里的`average-damp`是一个过程，它的参数是一个过程`f`,返回值是另一个过程(通过`lambda`产生),当我们将这一返回值过程应用于数`x`时，得到的将是`x`和`(f x)`的平均值。例如，将`average-damp`应用于`square`过程，就会产生出另一个过程，它在数值 ${x}$ 处的值就是 ${x}$ 和 ${x^2}$ 的平均值。将这样得到的过程应用于10,将返回10与100的平均值55°;

```
((average-damp square) 10)
55
```

Using `average-damp`, we can reformulate the square-root procedure as follows:<br>
利用`average-damp`,我们可以重做前面的平方根过程如下：

```
(define (sqrt x)
  (fixed-point (average-damp (lambda (y) (/ x y)))
               1.0))
```

Notice how this formulation makes explicit the three ideas in the method: fixed-point search, average damping, and the function ${y \mapsto x \big/\! y}$. It is instructive to compare this formulation of the square-root method with the original version given in section [1.1.7]. Bear in mind that these procedures express the same process, and notice how much clearer the idea becomes when we express the process in terms of these abstractions. In general, there are many ways to formulate a process as a procedure. Experienced programmers know how to choose procedural formulations that are particularly perspicuous, and where useful elements of the process are exposed as separate entities that can be reused in other applications. As a simple example of reuse, notice that the cube root of ${x}$ is a fixed point of the function ${y \mapsto x \big/\! y^2}$, so we can immediately generalize our square-root procedure to one that extracts cube roots:[^2]
请注意，看看上面这一公式中怎样把三种思想结合在同一个方法里：不动点搜寻，平均阻尼和函数 ${y \mapsto x \big/\! y}$。拿这一平方根计算的过程与[1.1.7]节给出的原来版本做一个比较，将是很有教益的。请记住，这些过程表述的是同一计算过程，也应注意，当我们利用这些抽象描述该计算过程时，其中的想法如何变得更加清晰了。将一个计算过程形式化为一个过程，一般说，存在很多不同的方式，有经验的程序员知道如何选择过程的形式，使其特别地清晰且易理解，使该计算过程中有用的元素能表现为一些相互分离的个体，并使它们还可能重新用于其他的应用。作为重用的一个简单实例，请注意 ${x}$ 的立方根是函数 ${y \mapsto x \big/\! y^2}$ 的不动点，因此我们可以立刻将前面的平方根过程推广为一个提取立方根的过程[^2]

(define (cube-root x)
  (fixed-point (average-damp (lambda (y) (/ x (square y))))
               1.0))


## Newton's method(牛顿法)

When we first introduced the square-root procedure, in section [1.1.7], we mentioned that this was a special case of *Newton's method*. If ${x \mapsto g(x)}$ is a differentiable function, then a solution of the equation ${g(x) = 0}$ is a fixed point of the function ${x \mapsto f(x)}$ where<br>
在[1.1.7]节介绍平方根过程时曾经提到*牛顿法*的一个特殊情况。如果 ${x \mapsto g(x)}$ 是一个可微函数，那么方程 ${g(x) = 0}$ 的一个解就是函数 ${x \mapsto f(x)}$ 的一个不动点，其中：

$$ {f(x) = x} - {g(x)\over Dg(x)} $$

and ${Dg(x)}$ is the derivative of ${g}$ evaluated at ${x}$. Newton's method is the use of the fixed-point method we saw above to approximate a solution of the equation by finding a fixed point of the function ${f\!.}$.[^3] For many functions g and for sufficiently good initial guesses for ${x}$， Newton's method converges very rapidly to a solution of ${g(x) = 0.}$.[^4]<br>
这里的 ${Dg(x)}$ 是 ${g}$ 对 ${x}$ 的导数。牛顿法就是使用我们前面看到的不动点方法，通过搜寻函数 ${f\!.}$. 的不动点的方式，去逼近上述方程的解[^3]。对于许多函数，以及充分好的初始猜测 ${x}$ ,牛顿法都能很快收敛到 ${g(x) = 0.}$ 的一个解[^4]。

In order to implement Newton's method as a procedure, we must first express the idea of derivative. Note that ''derivative,'' like average damping, is something that transforms a function into another function. For instance, the derivative of the function ${x \mapsto x^3}$ is the function ${x \mapsto 3x^2\!.}$. In general, if ${g}$ is a function and ${d\!x}$ is a small number, then the derivative ${Dg}$ of ${g}$ is the function whose value at any number ${x}$ is given (in the limit of small ${d\!x}$) by<br>
为了将牛顿方法实现为一个过程，我们首先必须描述导数的思想。请注意，“导数”不像平均阻尼，它是从函数到函数的一种变换。例如，函数 ${x \mapsto x^3}$ 的导数是另一个函数 ${x \mapsto 3x^2\!.}$。一般而言，如果 ${g}$ 是一个函数而 ${d\!x}$ 是一个很小的数，那么 ${g}$ 的导数在任一数值 ${x}$ 的值由下面函数(作为很小的数 ${d\!x}$ 的极限)给出：

$$ {Dg(x)} = {g(x + {\it dx}) - g(x) \over {\it dx}}\,. $$

Thus, we can express the idea of derivative (taking ${d\!x}$ to be, say, 0.00001) as the procedure<br>
这样，我们就可以用下面过程描述导数的概念(例如取 ${d\!x}$ 为0.00001):

```
(define (deriv g)
  (lambda (x)
    (/ (- (g (+ x dx)) (g x))
       dx)))
```

along with the definition<br>
再加上定义：

```
(define dx 0.00001)
```

Like `average-damp`, `deriv` is a procedure that takes a procedure as argument and returns a procedure as value. For example, to approximate the derivative of ${x \mapsto x^3}$ at 5 (whose exact value is 75) we can evaluate<br>
与`average-damp`一样，`deriv`也是一个以过程为参数，并且返回一个过程值的过程。例如，为了求出函数 ${x \mapsto x^3}$ 在5的导数的近似值(其精确值为75),我们可以求值：

```
(define (cube x) (* x x x))
((deriv cube) 5)
75.00014999664018
```

With the aid of `deriv`, we can express Newton's method as a fixed-point process:<br>
有了`deriv`之后，牛顿法就可以表述为一个求不动点的过程了：

```
(define (newton-transform g)
  (lambda (x)
    (- x (/ (g x) ((deriv g) x)))))
(define (newtons-method g guess)
  (fixed-point (newton-transform g) guess))
```

The `newton-transform` procedure expresses the formula at the beginning of this section, and `newtons-method` is readily defined in terms of this. It takes as arguments a procedure that computes the function for which we want to find a zero, together with an initial guess. For instance, to find the square root of ${x}$, we can use Newton's method to find a zero of the function ${y \mapsto y^2 - x}$ starting with an initial guess of 1.[^5] This provides yet another form of the square-root procedure:<br>
`newton-transform`过程描述的就是在本节开始处的公式，基于它去定义`newtons-method`已经很容易了。这一过程以一个过程为参数，它计算的就是我们希望去找到零点的函数，这里还需要给出一个初始猜测。例如，为确定 ${x}$ 的平方根，可以用初始猜测1,通过牛顿法去找函数 ${y \mapsto y^2 - x}$ 的零点[^5]。这样就给出了求平方根函数的另一种形式：

```
(define (sqrt x)
  (newtons-method (lambda (y) (- (square y) x))
                  1.0))
```

## Abstractions and first-class procedures(抽象和第一级过程)

We've seen two ways to express the square-root computation as an instance of a more general method, once as a fixed-point search and once using Newton's method. Since Newton's method was itself expressed as a fixed-point process, we actually saw two ways to compute square roots as fixed points. Each method begins with a function and finds a fixed point of some transformation of the function. We can express this general idea itself as a procedure:<br>
上面我们已经看到用两种方式，它们都能将平方根计算表述为某种更一般方法的实例，一个是作为不动点搜寻过程，另一个是使用牛顿法。因为牛顿法本身表述的也是一个不动点的计算过程，所以我们实际上看到了将平方根计算作为不动点的两种形式。每种方法都是从一个函数出发，找出这一函数在某种变换下的不动点。我们可以将这一具有普遍性的思想表述为一个函数：

```
(define (fixed-point-of-transform g transform guess)
  (fixed-point (transform g) guess))
```

This very general procedure takes as its arguments a procedure `g` that computes some function, a procedure that transforms `g`, and an initial guess. The returned result is a fixed point of the transformed function.<br>
这个非常具有一般性的过程有一个计算某个函数的过程参数`g`,一个变换`g`的过程，和一个初始猜测，它返回经过这一变换后的函数的不动点。

Using this abstraction, we can recast the first square-root computation from this section (where we look for a fixed point of the average-damped version of ${y \mapsto x \big/\! y}$ as an instance of this general method:<br>
我们可以利用这一抽象重新塑造本节的第一个平方根计算(搜寻 ${y \mapsto x \big/\! y}$ 在平均阻尼下的不动点),以它作为这个一般性方法的实例：

```
(define (sqrt x)
  (fixed-point-of-transform (lambda (y) (/ x y))
                            average-damp
                            1.0))
```

Similarly, we can express the second square-root computation from this section (an instance of Newton's method that finds a fixed point of the Newton transform of ${y \mapsto y^2 - x}$) as<br>
与此类似，我们也可以将本节的第二个平方根计算(是用牛顿法搜寻 ${y \mapsto y^2 - x}$ 的牛顿变换的实例)重新描述为：

```
(define (sqrt x)
  (fixed-point-of-transform (lambda (y) (- (square y) x))
                            newton-transform
                            1.0))
```

We began section [1.3] with the observation that compound procedures are a crucial abstraction mechanism, because they permit us to express general methods of computing as explicit elements in our programming language. Now we've seen how higher-order procedures permit us to manipulate these general methods to create further abstractions.<br>
我们在[1.3]节开始时研究复合过程，并将其作为一种至关重要的抽象机制，因为它使我们能将一般性的计算方法，用这一程序设计语言里的元素明确描述。现在我们又看到，高阶函数能如何去操作这些一般性的方法，以便建立起进一步的抽象。

As programmers, we should be alert to opportunities to identify the underlying abstractions in our programs and to build upon them and generalize them to create more powerful abstractions. This is not to say that one should always write programs in the most abstract way possible; expert programmers know how to choose the level of abstraction appropriate to their task. But it is important to be able to think in terms of these abstractions, so that we can be ready to apply them in new contexts. The significance of higher-order procedures is that they enable us to represent these abstractions explicitly as elements in our programming language, so that they can be handled just like other computational elements.<br>
作为编程者，我们应该对这类可能性保持高度敏感，设法从中识别出程序里的基本抽象，基于它们去进一步构造，并推广它们以创建威力更加强大的抽象。当然，这并不是说总应该采用尽可能抽象的方式去写程序，程序设计专家们知道如何根据工作中的情况，去选择合适的抽象层次。但是，能基于这种抽象去思考确实是最重要的，只有这样才可能在新的上下文中去应用它们。高阶过程的重要性，就在于使我们能显式地用程序设计语言的要素去描述这些抽象，使我们能像操作其他计算元素一样去操作它们。

In general, programming languages impose restrictions on the ways in which computational elements can be manipulated. Elements with the fewest restrictions are said to have *first-class* status. Some of the ''rights and privileges'' of first-class elements are:[^6]<br>
一般而言，程序设计语言总会对计算元素的可能使用方式强加上某些限制。带有最少限制的元素被称为具有*第一级*的状态。第一级元素的某些“权利或者特权”包括：[^6]

- They may be named by variables.<br>
可以用变量命名；
- They may be passed as arguments to procedures.<br>
可以提供给过程作为参数
- They may be returned as the results of procedures.<br>
可以由过程作为结果返回
- They may be included in data structures.[^7]<br>
可以包含在数据结构中[^7]

Lisp, unlike other common programming languages, awards procedures full first-class status. This poses challenges for efficient implementation, but the resulting gain in expressive power is enormous.[^8]<br>
Lisp不像其他程序设计语言，它给了过程完全的第一级状态。这就给有效实现提出了挑战，但由此所获得的描述能力却是极其惊人的"。[^8]


<div id="Exercise1.40" markdown>

Exercise 1.40.  Define a procedure `cubic`s that can be used together with the `newtons-method` procedure in expressions of the form<br>
练习1.40  请定义一个过程`cubic`,它和`newtons-method`过程一起使用在下面形式的表达式里：

```
(newtons-method (cubic a b c) 1)
```

to approximate zeros of the cubic ${x^3 + ax^2 + bx + c}$.<br>
能逼进三次方程 ${x^3 + ax^2 + bx + c}$ 的零点。
</div>

<div id="Exercise1.41" markdown>

Exercise 1.41.  Define a procedure `double` that takes a procedure of one argument as argument and returns a procedure that applies the original procedure twice. For example, if `inc` is a procedure that adds 1 to its argument, then `(double inc)` should be a procedure that adds 2. What value is returned by<br>
练习1.41  请定义一个过程`double`,它以一个有一个参数的过程作为参数，`double`返回一个过程。这一过程将原来那个参数过程应用两次。例如，若`inc`是个给参数加1的过程，`(double inc)`将给参数加2。下面表达式返回什么值：

```
(((double (double double)) inc) 5)
```
</div>

<div id="Exercise1.42" markdown>

Exercise 1.42.  Let ${f}$ and ${g}$ be two one-argument functions. The *composition* ${f}$ after ${g}$ is defined to be the function ${x \mapsto f(g(x))}$. Define a procedure `compose` that implements composition. For example, if `inc` is a procedure that adds 1 to its argument,<br>
练习1.42  令 ${f}$ 和 ${g}$ 是两个单参数的函数，${f}$ 在 ${g}$ 之后的复合定义为函数  ${x \mapsto f(g(x))}$。请定义一个函数`compose`实现函数复合：例如，如果`inc`是将参数加1的函数，那么：

```
((compose square inc) 6)
49
```
</div>

<div id="Exercise1.43" markdown>

Exercise 1.43.  If ${f}$ is a numerical function and ${n}$ is a positive integer, then we can form the $n^{\mathrm{th}}$$ repeated application of ${f}$, which is defined to be the function whose value at ${x}$ is ${f(f(\dots (f(x))\dots ))}$. For example, if ${f}$ is the function ${x \mapsto x + 1}$, then the $n^{\mathrm{th}}$ repeated application of ${f}$ is the function ${x \mapsto x + n}$. If ${f}$ is the operation of squaring a number, then the $n^{\mathrm{th}}$ repeated application of ${f}$ is the function that raises its argument to the ${2^n}$ -th power. Write a procedure that takes as inputs a procedure that computes ${f}$ and a positive integer ${n}$ and returns the procedure that computes the $n^{\mathrm{th}}$ repeated application of ${f}$. Your procedure should be able to be used as follows:<br>
练习1.43 如果 ${f}$ 是一个数值函数，${n}$ 是一个正整数，那么我们可以构造出 ${f}$ 的 ${n}$ 次重复应用，将其定义为一个函数，这个函数在 ${x}$ 的值是 ${f(f(\dots (f(x))\dots ))}$。举例说，如果 ${f}$ 是函数 ${x \mapsto x + 1}$,${n}$ 次重复应用 ${f}$ 就是函数 ${x \mapsto x + n}$。如果 ${f}$ 是求一个数的平方的操作，${n}$ 次重复应用f就求出其参数的 ${2^n}$ 次幂。请写一个过程，它的输入是一个计算f的过程和一个正整数 ${n}$,返回的是能计算 ${f}$ 的 ${n}$ 次重复应用的那个函数。你的过程应该能以如下方式使用：

```
((repeated square 2) 5)
625
```

Hint: You may find it convenient to use `compose` from exercise [1.42].<br>
提示：你可能发现使用练习[1.42]的`compose`能带来一些方便。
</div>

<div id="Exercise1.44" markdown>

Exercise 1.44.  The idea of *smoothing* a function is an important concept in signal processing. If ${f}$ is a function and ${d\!x}$ is some small number, then the smoothed version of ${f}$ is the function whose value at a point ${x}$ is the average of ${f(x - d\!x)}$, ${f(x)}$, and ${f(x + d\!x)}$. Write a procedure `smooth` that takes as input a procedure that computes ${f}$ and returns a procedure that computes the smoothed ${f}$. It is sometimes valuable to repeatedly smooth a function (that is, smooth the smoothed function, and so on) to obtained the *n-fold* smoothed function. Show how to generate the *n-fold* smoothed function of any given function using `smooth` and `repeated` from exercise [1.43].<br>
练习1.44 平滑一个函数的想法是信号处理中的一个重要概念。如果 ${f}$ 是一个函数，${d\!x}$ 是某个很小的数值，那么 ${f}$ 的平滑也是一个函数，它在点 ${x}$ 的值就是 ${f(x - d\!x)}$、${f(x)}$ 和 ${f(x + d\!x)}$ 的平均值。请写一个过程`smooth`,它的输入是一个计算 ${f}$ 的过程，返回一个计算平滑后的 ${f}$ 的过程。有时可能发现，重复地平滑一个函数，得到经过n次平滑的函数(也就是说，对平滑后的函数再做平滑，等等)也很有价值。说明怎样利用`smooth`和练习[1.43]的`repeated`,对给定的函数生成n次平滑函数。
</div>

<div id="Exercise1.45" markdown>

Exercise 1.45.  We saw in section [1.3.3] that attempting to compute square roots by naively finding a fixed point of ${y \mapsto x \big/\! y}$ does not converge, and that this can be fixed by average damping. The same method works for finding cube roots as fixed points of the average-damped ${y \mapsto x \big/\! y^2}$. Unfortunately, the process does not work for fourth roots -- a single average damp is not enough to make a fixed-point search for ${y \mapsto x \big/\! y^3}$ converge. On the other hand, if we average damp twice (i.e., use the average damp of the average damp of ${y \mapsto x \big/\! y^3}$) the fixed-point search does converge. Do some experiments to determine how many average damps are required to compute $n^{\mathrm{th}}$ roots as a fixed-point search based upon repeated average damping of ${y \mapsto x \big/\! y^{n-1}}$. Use this to implement a simple procedure for computing $n^{\mathrm{th}}$ roots using `fixed-point`, `average-damp`, and the `repeated` procedure of exercise [1.43]. Assume that any arithmetic operations you need are available as primitives.<br>
练习1.45  在[1.3.3]节里，我们看到企图用朴素的方法去找 ${y \mapsto x \big/\! y}$ 的不动点，以便计算平方根的方式不收敛，这个缺陷可以通过平均阻尼的方式弥补。同样方法也可用于找立方根，将它看做是平均阻尼后的 ${y \mapsto x \big/\! y^2}$ 的不动点。遗憾的是，这一计算过程对于四次方根却行不通，一次平均阻尼不足以使对 ${y \mapsto x \big/\! y^3}$ 的不动点搜寻收敛。而在另一方面，如果我们求两次平均阻尼(即，用 ${y \mapsto x \big/\! y^3}$ 的平均阻尼的平均阻尼),这一不动点搜寻就会收敛了。请做一些试验，考虑将计算n次方根作为基于 ${y \mapsto x \big/\! y^{n-1}}$ 的反复做平均阻尼的不动点搜寻过程，请设法确定各种情况下需要做多少次平均阻尼。并请基于这一认识实现一个过程，它使用`fixed-point`、`average-damp`和练习[1.43]的`repeated`过程计算n次方根。假定你所需要的所有算术运算都是基本过程。

</div>

<div id="Exercise1.46" markdown>

Exercise 1.46.  Several of the numerical methods described in this chapter are instances of an extremely general computational strategy known as *iterative improvement*. Iterative improvement says that, to compute something, we start with an initial guess for the answer, test if the guess is good enough, and otherwise improve the guess and continue the process using the improved guess as the new guess. Write a procedure `iterative-improve` that takes two procedures as arguments: a method for telling whether a guess is good enough and a method for improving a guess. `Iterative-improve` should return as its value a procedure that takes a guess as argument and keeps improving the guess until it is good enough. Rewrite the `sqrt` procedure of section [1.1.7] and the fixed-point procedure of section [1.3.3] in terms of `iterative-improve`.<br>
练习1.46  本章描述的一些数值算法都是迭代式改进的实例。迭代式改进是一种非常具有一般性的计算策略，它说的是：为了计算出某些东西，我们可以从对答案的某个初始猜测开始，检查这一猜测是否足够好，如果不行就改进这一猜测，将改进之后的猜测作为新的猜测去继续这一计算过程。请写一个过程`iterative-improve`,它以两个过程为参数：其中之一表示告知某一猜测是否足够好的方法，另一个表示改进猜测的方法。`iterative-improve`的返回值应该是一个过程，它以某一个猜测为参数，通过不断改进，直至得到的猜测足够好为止。利用`iterative-improve`重写[1.1.7]节的sqrt过程和[1.3.3]节的fixed-point过程。
</div>


[^1]: 
    Observe that this is a combination whose operator is itself a combination. Exercise [1.4] already demonstrated the ability to form such combinations, but that was only a toy example. Here we begin to see the real need for such combinations -- when applying a procedure that is obtained as the value returned by a higher-order procedure.<br>
    请注意看，这就是一个其中的运算符本身也是一个组合式的组合式。练习[1.4]已经阐释了描述这种形式的组合式的能力，但那里用的是一个玩具例子。在这里可以看到，在应用一个作为高阶函数的返回值而得到的函数时，我们确实需要这种形式的组合式。

[^2]: 
    See exercise [1.45] for a further generalization.<br>
    进一步推广参见练习[1.45]。

[^3]: 
    Elementary calculus books usually describe Newton's method in terms of the sequence of approximations ${x_{n+1} = x_n\,\,\, -}$ ${g(x_n)\,\big/ Dg(x_n)}$. Having language for talking about processes and using the idea of fixed points simplifies the description of the method.<br>
    基础微积分书籍中通常将牛顿法描述为逼进序列 ${x_{n+1} = x_n\,\,\, -}$ ${g(x_n)\,\big/ Dg(x_n)}$。有了能够描述计算过程的语言，采用了不动点的思想，这一方法的描述也得到了简化。

[^4]: 
    Newton's method does not always converge to an answer, but it can be shown that in favorable cases each iteration doubles the number-of-digits accuracy of the approximation to the solution. In such cases, Newton's method will converge much more rapidly than the half-interval method.<br>
    牛顿法并不保证能收敛到一个答案。我们还可以证明，在顺利的情况下，每次选代将使解的近似值的有效数字位数加倍。在处理这些情况时，牛顿法将比折半法的收敛速度快得多。

[^5]:
    For finding square roots, Newton's method converges rapidly to the correct solution from any starting point.<br>
    对于寻找平方根而言，牛顿法可以从任意点出发迅速收敛到正确的答案。

[^6]:
    The notion of first-class status of programming-language elements is due to the British computer scientist Christopher Strachey (1916-1975).<br>
    程序设计语言元素的第一级状态的概念应归功于英国计算机科学家Christopher Strachey (1916-1975)。

[^7]:
    We'll see examples of this after we introduce data structures in chapter 2.<br>
    我们将在第2章介绍了数据结构之后看到这方面的例子

[^8]:
    The major implementation cost of first-class procedures is that allowing procedures to be returned as values requires reserving storage for a procedure's free variables even while the procedure is not executing. In the Scheme implementation we will study in section [4.1], these variables are stored in the procedure's environment.<br>
    实现第一级过程的主要代价是，为使过程能够作为值返回，我们就需要为过程里的自由变量保留空间，即使这一过程并不执行。在[4.1]节有关Scheme实现的研究中，这些变量都被存储在过程的环境里。


[1.4]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_1/1.1.8/#Exercise1.4

[1.45]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_3/1.3.4/#Exercise1.45

[1.42]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_3/1.3.4/#Exercise1.42

[1.43]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_3/1.3.4/#Exercise1.43

[1.3.3]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_3/1.3.3/

[1.1.7]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_1/1.1.7/

[1.3]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_3/

[4.1]: {{ config.extra.homepage_sicp }}/chapter_4/chapter_4_1/
