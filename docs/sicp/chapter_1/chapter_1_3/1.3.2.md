---
comments: true
---

# 1.3.2  Constructing Procedures Using Lambda(用lambda构造过程)

In using `sum` as in section [1.3.1], it seems terribly awkward to have to define trivial procedures such as `pi-term` and `pi-next` just so we can use them as arguments to our higher-order procedure. Rather than define `pi-next` and `pi-term`, it would be more convenient to have a way to directly specify ''the procedure that returns its input incremented by 4'' and ''the procedure that returns the reciprocal of its input times its input plus 2.'' We can do this by introducing the special form `lambda`, which creates procedures. Using `lambda` we can describe what we want as<br>
在[1.3.1]节里用sum时，我们必须定义出一些如`pi-term`和`pi-next`一类的简单函数，以便用它们作为高阶函数的参数，这种做法看起来很不舒服。如果不需要显式定义`pi-term`和`pi-next`,而是有一种方法去直接刻画“那个返回其输入值加4的过程”和“那个返回其输入与它加2的乘积的倒数的过程”,事情就会方便多了。我们可以通过引入一种`lambda`特殊形式完成这类描述，这种特殊形式能够创建出所需要的过程。利用`lambda`,我们就能按照如下方式写出所需的东西：

```
(lambda (x) (+ x 4))
```

and<br>
和

```
(lambda (x) (/ 1.0 (* x (+ x 2))))
```

Then our `pi-sum` procedure can be expressed without defining any auxiliary procedures as<br>
这样就可以直接描述`pi-sum`过程，而无须定义任何辅助过程了:

```
(define (pi-sum a b)
  (sum (lambda (x) (/ 1.0 (* x (+ x 2))))
       a
       (lambda (x) (+ x 4))
       b))
```

Again using `lambda`, we can write the `integral` procedure without having to define the auxiliary procedure `add-dx`:<br>
借助于`1ambda`,我们也可以写出`integral`过程而不需要定义辅助过程`add-dx`:

```
(define (integral f a b dx)
  (* (sum f
          (+ a (/ dx 2.0))
          (lambda (x) (+ x dx))
          b)
     dx))
```

In general, `lambda` is used to create procedures in the same way as `define`, except that no name is specified for the procedure:<br>
一般而言，`lambda`用与`define`同样的方式创建过程，除了不为有关过程提供名字之外：

```
(lambda (<formal-parameters>) <body>)
```

The resulting procedure is just as much a procedure as one that is created using `define`. The only difference is that it has not been associated with any name in the environment. In fact,<br>
这样得到的过程与通过`define`创建的过程完全一样，仅有的不同之处，就是这种过程没有与环境中的任何名字相关联。事实上，

```
(define (plus4 x) (+ x 4))
```

is equivalent to<br>
等价于

```
(define plus4 (lambda (x) (+ x 4)))
```

We can read a `lambda` expression as follows:<br>
我们可以按如下方式来阅读`lambda`表达式：

|  (   |    lambda     |  (   |         x          |  )   |  (   |     +     |  x   |  4   |  )   |  )   |
| :--: | :-----------: | :--: | :----------------: | :--: | :--: | :-------: | :--: | :--: | :--: | :--: |
|      | the procedure |      | of an argument `x` |      |      | that adds |  x   |  4   |      |      |

Like any expression that has a procedure as its value, a `lambda` expression can be used as the operator in a combination such as<br>
像任何以过程为值的表达式一样，`lambda`表达式可用作组合式的运算符，例如：

```
((lambda (x y z) (+ x y (square z))) 1 2 3)
12
```

or, more generally, in any context where we would normally use a procedure name.[^1]<br>
或者更一般些，可以用在任何通常使用过程名的上下文中[^1]

## Using let to create local variables(用let创建局部变量)

Another use of `lambda` is in creating local variables. We often need local variables in our procedures other than those that have been bound as formal parameters. For example, suppose we wish to compute the function<br>
`lambda`的另一个应用是创建局部变量。在一个过程里，除了使用那些已经约束为过程参数的变量外，我们常常还需要另外一些局部变量。例如，假定我们希望计算函数：

$$ f(x,y) = x(1 + xy)^2 + y(1 - y) + (1 + xy)(1 - y), $$

which we could also express as<br>
可能就希望将它表述为：

$$\eqalign{	a 	&= 1 + xy, \cr
		b 	&= 1 - y,  \cr
		f(x,y) 	&= xa^2 + yb + ab. \cr}$$

In writing a procedure to compute ${f}$, we would like to include as local variables not only ${x}$ and ${y}$ but also the names of intermediate quantities like ${a}$ and ${b}$. One way to accomplish this is to use an auxiliary procedure to bind the local variables:<br>
在写计算 ${f}$ 的过程时，我们可能希望还有几个局部变量，不止是 ${x}$ 和 ${y}$,还有中间值的名字如 ${a}$ 和 ${b}$。做到这些的一种方式就是利用辅助过程去约束局部变量：

```
(define (f x y)
  (define (f-helper a b)
    (+ (* x (square a))
       (* y b)
       (* a b)))
  (f-helper (+ 1 (* x y)) 
            (- 1 y)))
```

Of course, we could use a `lambda` expression to specify an anonymous procedure for binding our local variables. The body of `f` then becomes a single call to that procedure:<br>
当然，我们也可以用一个`1ambda`表达式，用以描述约束局部变量的匿名过程。这样，`f`的体就变成了一个简单的对该过程的调用：

```
(define (f x y)
  ((lambda (a b)
     (+ (* x (square a))
        (* y b)
        (* a b)))
   (+ 1 (* x y))
   (- 1 y)))
```

This construct is so useful that there is a special form called `let` to make its use more convenient. Using `let`, the `f` procedure could be written as<br>
这一结构非常有用，因此，语言里有一个专门的特殊形式称为`let`,使这种编程方式更为方便。利用`let`,过程`f`可以写为：

```
(define (f x y)
  (let ((a (+ 1 (* x y)))
        (b (- 1 y)))
    (+ (* x (square a))
       (* y b)
       (* a b))))
```

The general form of a `let` expression is<br>
`let`表达式的一般形式是：

```
(let ((<var1> <exp1>)
      (<var2> <exp2>)
      
      (<varn> <expn>))
   <body>)
```

which can be thought of as saying<br>
可以将它读作：

```
let	<var1> have the value <exp1> and
<var2> have the value <exp2> and

<varn> have the value <expn>
in	<body>
```

The first part of the `let` expression is a list of name-expression pairs. When the `let` is evaluated, each name is associated with the value of the corresponding expression. The body of the `let` is evaluated with these names bound as local variables. The way this happens is that the `let` expression is interpreted as an alternate syntax for<br>
`let`表达式的第一部分是个名字-表达式对偶的表，当`let`被求值时，这里的每个名字将被关联于对应表达式的值。在将过些名字约束为局部变量的情况下求值`let`的体。这一做法正好使`let`表达式被解释为替代如下表达式的另一种语法形式：

```
((lambda (<var1> ...<varn>)
    <body>)
 <exp1>
 
 <expn>)
```

No new mechanism is required in the interpreter in order to provide local variables. A `let` expression is simply syntactic sugar for the underlying `lambda` application.<br>
这样，解释器里就不需要为提供局部变量增加任何新机制。`let`表达式只是作为其基础的`lambda`表达式的语法外衣罢了。

We can see from this equivalence that the scope of a variable specified by a `let` expression is the body of the `let`. This implies that:<br>
根据这一等价关系，我们可以认为，由`let`表达式描述的变量的作用域就是该`let`的体，这也意味着：

- `Let` allows one to bind variables as locally as possible to where they are to be used. For example, if the value of `x` is 5, the value of the expression<br>
`let`使人能在尽可能接近其使用的地方建立局部变量约束。例如，如果`x`的值是5,下面表达式<br>
```
(+ (let ((x 3))
    (+ x (* x 10)))
x)
``` 
is 38. Here, the `x` in the body of the `let` is 3, so the value of the `let` expression is 33. On the other hand, the `x` that is the second argument to the outermost `+` is still 5.<br>
就是38。在这里，位于`let`体里的x是3,因此这一`let`表达式的值是33。另一方面，作
为最外层的`+`的第二个参数的`x`仍然是5。

- The variables' values are computed outside the `let`. This matters when the expressions that provide the values for the local variables depend upon variables having the same names as the local variables themselves. For example, if the value of `x` is 2, the expression<br>
变量的值是在`let`之外计算的。在为局部变量提供值的表达式依赖于某些与局部变量同名的变量时，这一规定就起作用了。例如，如果`x`的值是2,表达式：<br>
```
(let ((x 3)
    (y (+ x 2)))
(* x y))
```
will have the value 12 because, inside the body of the `let`, `x` will be 3 and `y` will be 4 (which is the outer `x` plus 2).<br>
将具有值12,因为在这里`let`的体里，`x`将是3而`y`是4(其值是外面的`x`加2)。

Sometimes we can use internal definitions to get the same effect as with `let`. For example, we could have defined the procedure `f` above as<br>
有时我们也可以通过内部定义得到与`let`同样的效果。例如可以将上述`f`定义为：

```
(define (f x y)
  (define a (+ 1 (* x y)))
  (define b (- 1 y))
  (+ (* x (square a))
     (* y b)
     (* a b)))
```

We prefer, however, to use `let` in situations like this and to use internal `define` only for internal procedures.[^2]<br>
当然，在这种情况下我们更愿意用`let`,而仅将`define`用于内部过程[^2]

Exercise 1.34.  Suppose we define the procedure<br>
练习1.34  假定我们定义了：

```
(define (f g)
  (g 2))
```

Then we have<br>
而后就有：

```
(f square)
4

(f (lambda (z) (* z (+ z 1))))
6
```

What happens if we (perversely) ask the interpreter to evaluate the combination `(f f)`? Explain.<br>
如果我们(坚持)要求解释器去求值`(f f)`,那会发生什么情况呢?请给出解释。


[^1]: 
    It would be clearer and less intimidating to people learning Lisp if a name more obvious than `lambda`, such as `make-procedure`, were used. But the convention is firmly entrenched. The notation is adopted from the ${\lambda}$ calculus, a mathematical formalism introduced by the mathematical logician Alonzo Church (1941). Church developed the ${\lambda}$ calculus to provide a rigorous foundation for studying the notions of function and function application. The ${\lambda}$ calculus has become a basic tool for mathematical investigations of the semantics of programming languages.<br>
    对于学习Lisp的人而言，如果用一个比`lambda`更明确的名字，如`make-procedure`,可能会觉得更清楚。但是习惯成自然，这一记法形式取自 ${\lambda}$ 演算，那是由数理逻辑学家丘奇(Alonzo Church 1941)引进的一种数学记法，为研究函数和函数应用提供一个严格的基础。${\lambda}$ 演算已经成为程序设计语言语义的数学基石。


[^2]:
    Understanding internal definitions well enough to be sure a program means what we intend it to mean requires a more elaborate model of the evaluation process than we have presented in this chapter. The subtleties do not arise with internal definitions of procedures, however. We will return to this issue in section [4.1.6], after we learn more about evaluation.<br>
    要很好地理解内部定义，保证一个程序的意义确实是我们所希望的那个意义，实际上要求另一个比我们在本章给出的求值计算过程更精细的模型。然而这一难以捉摸的问题不会出现在过程内部定义方面。我们将在对求值有了更多理解之后，在[4.1.6]节回到这一问题。


[1.3.1]: {{ config.extra.homepage_sicp }}/chapter_1/chapter_1_3/1.3.1/
[4.1.6]: {{ config.extra.homepage_sicp }}/chapter_4/chapter_4_1/4.1.6/